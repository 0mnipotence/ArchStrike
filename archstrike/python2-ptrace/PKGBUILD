# Maintainer: ArchStrike <team@archstrike.org>
pkgbase=python2-ptrace

buildarch=1

pkgname=(python2-ptrace python-ptrace)
_pkgname=python-ptrace
pkgver=0.9.2
pkgrel=2
pkgdesc="A debugging using ptrace (Linux, BSD and Darwin system call to trace processes) written in Python."
url="https://pypi.python.org/pypi/python-ptrace/"
arch=('any')
license=('GPL2')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/af/fd/67bc03f488bdfb1588ce6009de781d4ee4d1d566ae46e3354b0d93b64e19/python-ptrace-${pkgver}.tar.gz")
sha512sums=('71e4406580a3f121aa1eac2cf44e274c8d14a973178d30ee0ab63dcccf7e2b7d54da26a59cc8dc883275c858cfff918ee0c65cc2cc6a9c72bec29937fb2124e7')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}

  cd $_pkgname-$pkgver-py2
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build

  cd ../$_pkgname-$pkgver-py2
  python2 setup.py build
}

package_python-ptrace() {
  depends=('python')
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --root=$pkgdir/ -O1 --skip-build
  #Fixing for conflicts
  mv $pkgdir/usr/bin/gdb.py $pkgdir/usr/bin/gdb3.py
  mv $pkgdir/usr/bin/strace.py $pkgdir/usr/bin/strace3.py
}

package_python2-ptrace() {
  depends=('python2')
  cd "$srcdir/$_pkgname-$pkgver-py2"

  python2 setup.py install --root=$pkgdir/ -O1 --skip-build
}
