# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=lft
pkgver=3.75
pkgrel=1
groups=('archstrike' 'archstrike-recon')
pkgdesc="A layer four traceroute implementing numerous other features"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('custom')
url="http://pwhois.org/lft/"
depends=('libpcap')
source=("http://pwhois.org/dl/index.who?file=lft-$pkgver.tar.gz" "define_default_source.patch")
sha512sums=('92f2e87c3c2f9f511bee77c39b7260b08c95171560c158bc4dcabe4017c54a5a708a96e24bbcd424c2cb9685ae1d981c13e746622b6c197a86199a8e54dfca25'
            '7b0f3c1a588ff3a55fa949039102a388a77dc12b276023501bd46bef9a44f747423919cac5322596ee6eb4360972ac5ca543c98b12211e65e94f7cfd7f6820bb')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i $srcdir/define_default_source.patch
}

build() {
  cd "$pkgname-$pkgver"
  export CFLAGS+=" -fPIE"
  export CFLAGS+=" -z now -pie"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make "prefix=$pkgdir/usr" install
  install -Dm644 "$srcdir/lft-$pkgver"/COPYING \
    "$pkgdir"/usr/share/licenses/lft/LICENSE

  # Removing setuid on bin
  chmod 755 $pkgdir/usr/bin/lft
}
