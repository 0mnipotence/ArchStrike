# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=inetsim
pkgver=1.2.5
pkgrel=3
groups=('archstrike' 'archstrike-defense')
pkgdesc="A software suite for simulating common internet services in a lab environment, e.g. for analyzing the network behaviour of unknown malware samples"
url="http://www.inetsim.org"
arch=('any')
license=('GPL3')
depends=('perl' 'perl-net-dns' 'perl-net-server' 'perl-ipc-shareable' 'openssl')
install=inetsim.install
source=("http://www.inetsim.org/downloads/inetsim-$pkgver.tar.gz")
sha512sums=('f30b688b7f665ce342a4d792a473c23f9b22839e74425b267fd056aefddf0cde9027450a95748a0fe58fa736374f57cfd72374a04ad1901c128466cfb2fc048d')

package() {
  cd "$srcdir/inetsim-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/etc"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/var/lib"
  install -dm755 "$pkgdir/usr/share/inetsim"
  install -dm755 "$pkgdir/usr/share/doc/inetsim"
  install -dm755 "$pkgdir/var/log/inetsim"

  # Program.
  cp -R --no-preserve=ownership contrib lib inetsim "$pkgdir/usr/share/inetsim"

  # Logs.
  ln -s /var/log/inetsim "$pkgdir/usr/share/inetsim/report"
  ln -s /var/log/inetsim "$pkgdir/usr/share/inetsim/log"

  # Data.
  cp -R --no-preserve=ownership data "$pkgdir/var/lib/inetsim"
  ln -s /var/lib/inetsim "$pkgdir/usr/share/inetsim/data"

  # Configuration.
  cp -R --no-preserve=ownership conf "$pkgdir/etc/inetsim"
  ln -s /etc/inetsim "$pkgdir/usr/share/inetsim/conf"

  # Man pages.
  cp -R --no-preserve=ownership man "$pkgdir/usr/share"

  # Documentation.
  install -m644 CHANGES README doc/* "$pkgdir/usr/share/doc/inetsim"

  # Permissions.
  # This is pretty much copypasted from the upstream-provided `setup.sh`.
  # Group ownership is set in the install file.
  chmod 770 "$pkgdir/var/log/inetsim"
  chmod g+w "$pkgdir/var/lib/inetsim/http/postdata"
  chmod g+w "$pkgdir/var/lib/inetsim/ftp/upload"
  chmod g+w "$pkgdir/var/lib/inetsim/tftp/upload"

  # TODO: install cleanup.sh?
  # Joint script.
  cat > "$pkgdir/usr/bin/inetsim" <<EOF
#!/bin/sh
cd /usr/share/inetsim
perl -T inetsim "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/inetsim"
}
