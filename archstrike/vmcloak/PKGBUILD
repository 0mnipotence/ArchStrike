# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=vmcloak
pkgver=0.3.7
pkgrel=3
pkgdesc='Automated Virtual Machine Generation and Cloaking for Cuckoo Sandbox'
url='https://github.com/jbremer/vmcloak'
arch=('i686' 'x86_64')
license=('GPL3')
groups=('archstrike' 'archstrike-malware')
depends=('cdrtools' 'virtualbox' 'dmidecode' 'lshw' 'python2-lockfile' 'python2-sphinx')
makedepends=('python2-setuptools' 'unzip')
source=("https://github.com/jbremer/$pkgname/archive/$pkgver.zip")
sha512sums=('85296fe6e5637c9b0c307e77adc081956ade357e626d5512155ce24a09794718d9a1fdc56f46d928b25d59b4120552e45acd28286def7a1057a3db93a71332c7')

prepare() {
  find . -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
  find . -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 docs/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
