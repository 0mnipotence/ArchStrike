# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname="matplotlib"
pkgname="python2-${_pkgname}"
pkgver=2.2.5
pkgrel=1
pkgdesc="A python2 plotting library, making publication quality plots"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://matplotlib.org"
license=(custom)
depends=("freetype2" "libpng" "python2-numpy" "python2-cycler" "python2-dateutil"
         "python2-kiwisolver" "python2-pyparsing")
makedepends=("git" "rsync" "python2-setuptools" "tk" "python2-pyqt5" "python2-gobject"
         "python2-wxpython" "python2-cairocffi" "python2-tornado" "python2-pillow"
         "ghostscript" "texlive-bin")
optdepends=('tk: Tk{Agg,Cairo} backends'
            'python2-pyqt5: Qt5{Agg,Cairo} backends'
            'python2-gobject: for GTK3{Agg,Cairo} backend'
            'python2-wxpython: WX{,Agg,Cairo} backend'
            'python2-cairo: {GTK3,Qt5,Tk,WX}Cairo backends'
            'python2-cairocffi: alternative for Cairo backends'
            'python2-tornado: WebAgg backend'
            'ffmpeg: for saving movies'
            'imagemagick: for saving animated gifs'
            'python2-pillow: for reading/saving jpeg/bmp/tiff files'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies')
source=("https://github.com/matplotlib/matplotlib/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e2f7d46a3548a1c79ad6bbcce69e73223808b3066f7f63f893568d9d96f3167fd189af86927abcbc9fafe6b2f1477a1c8227e444d7118164f5af0f69df590d9f')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root "${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm644 doc/users/license.rst -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
