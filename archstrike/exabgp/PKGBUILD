# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Diogo Leal <estranho@diogoleal.com>

buildarch=1

pkgname=exabgp
pkgver=4.0.6
pkgrel=1
pkgdesc='The BGP swiss army knife of networking'
arch=('any')
license=('custom')
groups=('archstrike' 'archstrike-networking')
url='https://github.com/Exa-Networks/exabgp'
depends=('python2-ipaddr' 'python2-nose' 'python2-psutil')
backup=('etc/exabgp.conf')
makedepends=('python2-setuptools')
source=("https://github.com/Exa-Networks/${pkgname}/archive/${pkgver}.zip"
        'exabgp.service')
sha512sums=('6ba9edea4afea70abe91607f5b6fee2c8b65c8c1a0b1ab3757b0eb61e1cc9c23d0641b6b6c826542576ab5d41589327fb1c6379ed3d6edaccd4854e7debac455'
            '225726cf424d9c2a23a2c0e9d3c7ed8c5cf03993f9518efd8e45079822b2d8cbc4d6fd18b49794beee522530478b57e5e3b268b3abd92442680cdd695504360d')

build() {
    # arch python2
    find $pkgname-$pkgver -type f -exec \
        sed -i '1s,^#!/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \; -print
}

package() {
    pushd $pkgname-$pkgver
    # upstream installer
    python2 setup.py install --root="$pkgdir" --install-data=/
    # install license
    install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    popd
    # systemd
    install -D -m 644 $pkgname.service \
        "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    # install empty config file
    install -D -m 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}
