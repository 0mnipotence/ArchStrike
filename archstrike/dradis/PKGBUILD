# Maintainer: ArchStrike <team@archstrike.org>

buildarch=128

pkgname=dradis
pkgver=3.14.0
pkgrel=1
pkgdesc="An open source framework to enable effective information sharing."
groups=('archstrike' 'archstrike-misc')
url="http://dradisframework.org/"
license=('GPL')
depends=('ruby' 'redis')
arch=('x86_64')
options=(!strip)
replaces=('dradis')
source=("https://github.com/dradis/dradis-ce/archive/v${pkgver}.tar.gz")
sha512sums=('1a7928b4d8fe885a09f47c8fcacd975f47cf0a5c0e907f6a4ef2b4803b019cfcd3f3f915abf0c9dea020a46e16870220d4aae8279d2880ffd8b181039116f468')

package() {

  cd "${pkgname}-ce-${pkgver}" || exit
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"

  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}"

  cd "${pkgdir}/usr/bin"

  # Joint scripts.
  cat > dradis-webapp << EOF
#!/bin/sh
cd /usr/share/${pkgname}
./dradis-webapp "\$@"
EOF

  cat > dradis-worker << EOF
#!/bin/sh
cd /usr/share/${pkgname}
./dradis-worker "\$@"
EOF

  chmod 755 dradis-webapp dradis-worker
}
