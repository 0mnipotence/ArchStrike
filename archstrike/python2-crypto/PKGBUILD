# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgbase=pycrypto
pkgname=('python2-crypto')
pkgver=2.6.1
pkgrel=1
pkgdesc="Collection of secure hash functions and encryption algorithms"
arch=('armv6' 'armv7' 'aarch64' 'i686' 'x86_64')
depends=('python2' 'gmp')
makedepends=('python2' 'gmp' 'python2-setuptools')
url="http://www.dlitz.net/software/pycrypto/"
license=('custom')
provides=("pycrypto=${pkgver}")
source=("https://github.com/dlitz/${pkgbase}/archive/v${pkgver}.tar.gz"
        CVE-2013-7459.patch)
sha512sums=('272c5ca2ba6c4e432ae03bd1635822a925a89cd678847ef4789b01a197af1b0abeafc002f93f1ed062765574f1f819c07987a930e5f0f3f73db7260d0d262bfd'
            'b34a28fa886ad749878aee17459307fb3006f392304a368e9c80b9beac78b1bca2f35831664e504097996ae1be4e1e81eae5e68b211e47fbf11f2b05b2379fb8')

prepare() {
  find ${pkgbase}-${pkgver}/LEGAL -type f -exec chmod 644 {} \;
  find ${pkgbase}-${pkgver}/LEGAL -type d -exec chmod 755 {} \;

  patch -d ${pkgbase}-${pkgver} -p1 < "${srcdir}/CVE-2013-7459.patch"
}

build() {
  cd ${pkgbase}-${pkgver} || exit
  python2 setup.py build
}

package() {
  cd ${pkgbase}-${pkgver} || exit
  python2 setup.py install --root="${pkgdir}" -O1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}

check() {
  cd ${pkgbase}-${pkgver} || exit
  python2 setup.py test
}
