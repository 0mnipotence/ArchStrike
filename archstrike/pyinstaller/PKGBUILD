# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pyinstaller
pkgver=2.1
pkgrel=2
groups=('archstrike' 'archstrike-misc')
pkgdesc="A program that converts (packages) Python programs into stand-alone executables, under Windows, Linux, Mac OS X, Solaris and AIX."
arch=('any')
url="http://www.pyinstaller.org/"
license=('GPL')
depends=('python2' 'lib32-zlib')
source=("https://pypi.python.org/packages/source/P/PyInstaller/PyInstaller-$pkgver.tar.gz")
sha512sums=('aad29298b3342ccf94c7234ae6c81ef97ed3bfdbd1b696acf087d08bc5c6536b066b7f2012d7cdd1c47c465dfafd886290be44234fb86935eea56f015add9ea7')
options=('!strip' '!emptydirs')

prepare() {
  cd "$srcdir/PyInstaller-$pkgver"
  grep -RIl '^#!.*python' . | xargs sed -i '/^#!/ s|.*|#! /usr/bin/env python2|'
}

package() {
  cd "$srcdir/PyInstaller-$pkgver"

  # Clean up.
  rm -rf "bootloader" "old" "tests" "PKG-INFO" "setup.py"

  # Base directories
  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/doc/pyinstaller"
  install -dm 755 "$pkgdir/usr/share/pyinstaller"

  install -Dm644 "doc/Manual.pdf" "$pkgdir/usr/share/doc/pyinstaller"
  rm -rf "doc"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/pyinstaller"

  ln -s "/usr/share/pyinstaller/pyinstaller.py" "$pkgdir/usr/bin/pyinstaller"
}
