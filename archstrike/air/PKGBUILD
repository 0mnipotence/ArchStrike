# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname='air'
pkgver='2.0.0'
pkgrel=10
pkgdesc='A GUI front-end to dd/dc3dd designed for easily creating forensic images.'
url='http://air-imager.sourceforge.net/'
groups=('archstrike' 'archstrike-forensics')
license=('GPL')
arch=('any')
depends=('perl-tk' 'sharutils' 'mt-st-git')
install='air.install'
source=("http://downloads.sourceforge.net/air-imager/air-${pkgver}.tar.gz" "air.install")
sha512sums=('3b22b0b2f07fd5bf89b576bb1d96d9bc372534f2063e99c7a9c4931fd927b0cc5c3fe832f0c0d51ceb95fe242fd0d3e4fe6bf59339dd2971b6afcd00106ea92c'
            '1bda2442efdd5bbfe2ac83618b3ef2a551809a0481afbb93c6d4e796bd42b3a6f70ddb169cdeb4cdea4bd114343a293bdf7466b106fb302aa131fc33ee969299')

package() {
  cd "${srcdir}/air-${pkgver}"
  install -dm755 "${pkgdir}/var/log"

  export PERLTK_VER=$(pacman -Q perl-tk | awk -F' ' '{ print $2 }' | sed -e 's/-.*//' -e 's/_//')
  INTERACTIVE="no" INSTALL_DIR="${pkgdir}/usr" TEMP_DIR="${srcdir}/" FINAL_INSTALL_DIR=/usr ./install-air-${pkgver}

  chown -R root:root ${pkgdir}/*
  mv "${pkgdir}/usr/share/air/logs" "${pkgdir}/var/log/air"
  ln -s "/var/log/air" "${pkgdir}/usr/share/air/logs"
  [[ -p "${pkgdir}/usr/share/air/air-fifo" ]] && chmod 664 "${pkgdir}/usr/share/air/air-fifo"
  chmod 775 "${pkgdir}/var/log/air"
  chmod 755 "${pkgdir}/usr/bin/air"
}
