# Maintainer: David Thurstenson <thurstylark@gmail.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

buildarch=220

_pkgname='bluez'
pkgname='bluez-utils-compat'
pkgver=5.49
pkgrel=1
url="http://www.bluez.org/"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
pkgdesc="Development and debugging utilities for the bluetooth protocol stack. Includes deprecated tools."
depends=('dbus' 'systemd' 'glib2')
conflicts=('bluez-hcidump' 'bluez-utils')
provides=('bluez-hcidump' 'bluez-utils')
replaces=('bluez-hcidump' 'bluez<=4.101')
makedepends=('dbus' 'libical' 'systemd')
source=("https://www.kernel.org/pub/linux/bluetooth/${_pkgname}-${pkgver}.tar.xz")
sha512sums=('bc2988649420232b92e2c6836857027369f05ace005972f575ed0601c02cc97a07a3b7a0707a8bad72be73df7e8096c8bf023530443556e87c2ccb667981b37d')

build() {
  cd "${_pkgname}-${pkgver}"
  ./configure \
          --prefix=/usr \
          --mandir=/usr/share/man \
          --sysconfdir=/etc \
          --localstatedir=/var \
          --libexecdir=/usr/lib \
          --enable-sixaxis \
          --enable-experimental \
          --enable-library \
          --enable-deprecated # to enable deprectated tools
  make
}

check() {
  cd "${_pkgname}-${pkgver}"
  make check
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install-binPROGRAMS install-man1

  # add missing tools FS#41132, FS#41687, FS#42716
  for files in `find tools/ -type f -perm -755`; do
    filename=$(basename $files)
    install -Dm755 "${srcdir}/${_pkgname}-${pkgver}/tools/${filename}" "${pkgdir}/usr/bin/${filename}"
  done

  # add gatttool usefulbdaddr with Bluetooth 4.0LE 
  install -Dm755 "${srcdir}/${_pkgname}-${pkgver}/attrib/gatttool" "${pkgdir}/usr/bin/gatttool"
  
  # libbluetooth.so* are part of libLTLIBRARIES and binPROGRAMS targets
  #make DESTDIR=${pkgdir} uninstall-libLTLIBRARIES
  rm -rf "${pkgdir}/usr/lib"
  
  # move the hid2hci man page out
  mv "${pkgdir}/usr/share/man/man1/hid2hci.1" "${srcdir}/"
}

# vim:se ts=2 sw=2 et:
