# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=whichcraft
pkgbase=python-${_pkgname}
pkgname=(python-${_pkgname} python2-${_pkgname})
pkgver=0.5.2
pkgrel=1
arch=('any')
url="https://github.com/pydanny/whichcraft"
license=('BSD')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
replaces=('python2-whichcraft')
source=("${pkgname}::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('28b7be346b781f69c8fef419efd91fc2daa6b337c1eda0a2fc47d04134c479a5ded6eed3583825b111a48d7a79667676b6ce49693552233e78c7d517ac91a73c')

prepare(){
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py build
}

package_python-whichcraft() {
  pkgdesc="provides cross-platform cross-python shutil.which functionality"
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-whichcraft() {
  pkgdesc="provides cross-platform cross-python shutil.which functionality"
  depends+=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}/" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
