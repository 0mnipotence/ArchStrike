# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=ruby1.9-bundler
_gemname=${pkgname#ruby1.9-}
pkgver=1.11.2
pkgrel=1
pkgdesc="Manages an application's dependencies through its entire life, across many machines, systematically and repeatably"
arch=('any')
url='http://gembundler.com'
license=('MIT')
depends=('ruby1.9')
source=(
  "http://rubygems.org/downloads/$_gemname-$pkgver.gem"
  'https://raw.githubusercontent.com/bundler/bundler/master/LICENSE.md'
)
noextract=("$_gemname-$pkgver.gem")
sha512sums=('c7a5e3456fea7d21c25476fb67a41e05456b8c930c6ecc41156d7e0606d68f016881dae76a63cf3f3ec6ddfd397f853ef89d65dd0a14d9d45984719228675a73'
            'fa43908bb0e9af7b5f2efd785e052749276671c1af46df762df337b24312f0b157d291484a5a0299a644a3d1455e6465a57d120defb859aaa26b158a4a02677a')

package() {
  local _gemdir="$(ruby-1.9 -rubygems -e'puts Gem.default_dir')"
  if [[ "$CARCH" = arm* ]] ; then
    gem-1.9 install --ignore-dependencies --no-user-install --no-rdoc --no-ri -i "$pkgdir$_gemdir" $_gemname-$pkgver.gem
  else
    gem-1.9 install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" $_gemname-$pkgver.gem
  fi
  install -dm755 "$pkgdir/usr/bin"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  ln -s "$_gemdir/bin/bundle" "$pkgdir/usr/bin/bundle-1.9"
}
