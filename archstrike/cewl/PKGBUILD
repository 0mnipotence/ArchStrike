# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=cewl
_pkgname=CeWL
pkgver=5.4.8.1
pkgrel=1
groups=('archstrike' 'archstrike-autonomous' 'archstrike-crackers')
pkgdesc='A custom word list generator'
arch=('any')
url="https://github.com/digininja/CeWL"
license=('CCPL')
depends=('ruby' 'ruby-spider' 'ruby-mini_exiftool' 'ruby-nokogiri'
         'ruby-http_configuration' 'ruby-rubyzip' 'ruby-zip' 'ruby-mime' 'ruby-mime-types')
source=("${url}/archive/${pkgver}.tar.gz")
sha512sums=('c82b173d8fd2961e3276bd8665d2af9bc627bba6119103dfc1d5dbe09fb48bf52907f3652bb9957626bc1f64dcdde69fb5da9be6a6ef0535a7feb7147da2ddfe')

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"

  cp -a --no-preserve=ownership ./* "${pkgdir}/usr/share/${pkgname}"

cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/usr/bin/env bash
cd "/usr/share/${pkgname}"
ruby cewl.rb "\$@"
EOF

chmod 755 "${pkgdir}/usr/bin/${pkgname}"
}
