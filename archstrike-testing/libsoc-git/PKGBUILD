# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname=libsoc
pkgname=${_pkgname}-git
pkgver=r262.7bce543
pkgrel=1
pkgdesc="A C library for interfacing with common SoC peripherals through generic kernel interfaces"
arch=('x86_64' 'i686' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/jackmitch/${_pkgname}"
depends=('glibc')
makedepends=('autoconf' 'git')
replaces=("${_pkgname}")
provides=("${_pkgname}")
conflicts=("${_pkgname}")
install=${_pkgname}.install
license=('custom')
source=("${_pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_pkgname}"
  autoreconf -i
  ./configure --prefix=/usr --enable-debug
  make
}

package() {
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:se ts=2 sw=2 et:
