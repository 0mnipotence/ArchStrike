# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=dicttoxml
pkgbase="python-$_pkgname"
pkgname=("python-$_pkgname" "python2-$_pkgname")
pkgver=1.7.4
pkgrel=4
pkgdesc='Converts a Python dictionary or other native data type into a valid XML string'
url='https://pypi.python.org/pypi/dicttoxml'
license=('GPL')
arch=('any')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("https://pypi.python.org/packages/74/36/534db111db9e7610a41641a1f6669a964aacaf51858f466de264cc8dcdd9/dicttoxml-${pkgver}.tar.gz")
sha512sums=('91abcf2b9b248717618e9fc1c8694e881b9deaa16438dd4674f94a22b4aabfdab3b13f95c3d44a60577d49eca82fb268f59b33d1312cf5388bdaf949a2865cbf')

prepare() {
  cp -a ${_pkgname}-$pkgver{,-py2}
  grep -ZRil 'python' "${_pkgname}-$pkgver-py2" | xargs -0 sed -i '1 s|#!.*/usr/bin/python3?|#!/usr/bin/python2|;s|#!.*/usr/bin/env python3?$|#!/usr/bin/env python2|'
}

build() {
  cd "${srcdir}/${_pkgname}-$pkgver"
  python setup.py build

  cd "${srcdir}/${_pkgname}-$pkgver-py2"
  python2 setup.py build
}

package_python-dicttoxml() {
  depends=('python')
  cd dicttoxml-$pkgver
  python setup.py install --root="$pkgdir" -O1
}

package_python2-dicttoxml() {
  depends=('python2')
  cd dicttoxml-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1
}
