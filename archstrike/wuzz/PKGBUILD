# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=wuzz
pkgver=0.4.0
pkgrel=2
pkgdesc="Interactive cli tool for HTTP inspection"
arch=('armv6h' 'armv7h' 'x86_64' 'i686' 'aarch64')
url="https://github.com/asciimoo/wuzz"
groups=('archstrike' 'archstrike-misc')
license=("AGPL")
depends=('glibc')
makedepends=('go' 'git')
source=("https://github.com/asciimoo/wuzz/archive/v${pkgver}.tar.gz")
sha512sums=('9e7a2f88ea24ada95fbdadc4a64b4d4c93a3048481f20e33a041f1833e27a135e496969bd569694b606eda0461dc2cb7df618a650101c10e51bbfad34a0eea66')

prepare() {
  mkdir -p ${srcdir}/src/github.com/asciimoo/
  mv ${srcdir}/${pkgname}-${pkgver} ${srcdir}/src/github.com/asciimoo/wuzz
  GOPATH="${srcdir}" go get -d -t github.com/asciimoo/wuzz
}

build() {
  cd src/github.com/asciimoo/wuzz
  GOPATH="${srcdir}" go build -v github.com/asciimoo/wuzz
}

package() {
  cd src/github.com/asciimoo/wuzz
  install -Dm755 ${pkgname} ${pkgdir}/usr/bin/wuzz
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
