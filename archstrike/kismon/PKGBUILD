# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=kismon
pkgver=1.0.1
pkgrel=1
pkgdesc="GUI client for kismet (wireless scanner/sniffer/monitor)"
arch=('any')
groups=('archstrike' 'archstrike-wireless')
url="https://www.salecker.org/software/kismon.html"
license=('BSD')
depends=('gtk3' 'gobject-introspection-runtime' 'python-gobject' 'python-simplejson' 'python-cairo' 'osm-gps-map')
makedepends=('python-setuptools')
source=("https://files.salecker.org/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('060c31149f558691395f43ddd506bf6e0c48cb370f5175c2b91a02dfed03cb95af21e51ea96147945b81716bd33e6367d89ad9dbec3c2c66143eec9ff10feba6')

build() {
  cd "${pkgname}-${pkgver}" || exit
  python3 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}" || exit
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

