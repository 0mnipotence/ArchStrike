# Maintainer: ArchStrike <team@archstrike.org>
_pkgname=pysnmp

buildarch=1

pkgname=python2-$_pkgname
pkgver=4.4.5
pkgrel=1
pkgdesc="SNMP library for Python"
url='https://github.com/etingof/pysnmp'
license=('BSD')
arch=('any')
depends=("python2-pyasn1" "python2-pycryptodomex")
makedepends=("python2-setuptools")
optdepends=("python2-pysnmp-mibs")
conflicts=('pysnmp')
provides=('pysnmp')
source=(https://github.com/etingof/pysnmp/archive/v${pkgver}.tar.gz)
sha512sums=('64e7fad231f41ff0b92d9c1a8d72485d6ad8f5fb09c5f9e4dd0c57c3192c6d43ca93806462b0a314782d988575db431298bb86c14baccdc800a23d744ac21397')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/python2-pysnmp/LICENSE.rst
  # Package python module only.
  rm -fr "$pkgdir"/usr/bin
}
