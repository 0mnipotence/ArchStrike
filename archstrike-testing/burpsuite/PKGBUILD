# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=burpsuite
pkgver=1.6.28
pkgrel=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-scanners' 'archstrike-fuzzers' 'archstrike-proxy')
pkgdesc="An integrated platform for attacking web applications (free edition)."
url='http://portswigger.net/burp/'
depends=('java-environment' 'bash')
arch=('any')
license=('custom')
noextract=("burpsuite.jar")
source=("burpsuite.jar::https://portswigger.net/DownloadUpdate.ashx?Product=Free" LICENSE)
sha512sums=('5fb04c4b8fce97c6fa8a00422d0f35a5cddd65e36e2c4e43468d82ac42fbd00df7009f49c9d0e28f870e12a5525ee3febd900108cc004079a6ec5f9454509abd'
            '37abc24b19571bfd2941e3dd7aa8a8a330bd3bbbafb3d1f813035ab0313e189c1f4a43a68f820a32656b86dd29cca94afcaefc6a4bfb4219ef18dc0fc4923b86')

package() {
  cd ${srcdir}

  # Make base directories.
  install -dm755 $pkgdir/usr/share/licenses/burpsuite
  install -dm755 $pkgdir/usr/share/burpsuite
  install -dm755 $pkgdir/usr/bin

  install -m644 burpsuite.jar $pkgdir/usr/share/burpsuite/burpsuite.jar
  install -m644 LICENSE $pkgdir/usr/share/licenses/burpsuite

  # Make joint script.
  cat > ${pkgdir}/usr/bin/burpsuite <<EOF
#!/bin/sh
exec \$JAVA_HOME/bin/java -jar /usr/share/burpsuite/burpsuite.jar "\$@"
EOF
  chmod +x ${pkgdir}/usr/bin/burpsuite
}
