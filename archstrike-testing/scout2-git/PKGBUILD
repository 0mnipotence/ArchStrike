# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=scout2-git
pkgver=20170108.r736
pkgrel=1
pkgdesc="Security auditing tool for AWS environments"
url="http://isecpartners.github.io/Scout2/"
arch=('any')
license=('GPL2')
groups=('archstrike')
depends=('python-boto3' 'python-dateutil' 'python-netaddr' 'python-requests' 'python-botocore' 'python-opinel')
makedepends=('git')
provides=('scout2')
conflicts=('scout2')
replaces=('scout2')
source=($pkgname::git+https://github.com/iSECPartners/Scout2.git)
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" 
}

prepare() {
  cd "$pkgname"
  git submodule init
  git submodule update
}

package() {
  cd "$pkgname"
  install -dm755 "$pkgdir"/usr/bin/
  install -dm755 "$pkgdir"/usr/share/scout2-git
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/scout2-git/LICENSE
  install -m644 README.md "$pkgdir"/usr/share/scout2-git/README.md
  install -m755 *.py "$pkgdir"/usr/share/scout2-git/
  install -m644 *.json "$pkgdir"/usr/share/scout2-git/
  cp --no-preserve=ownership -r AWSScout2 filters html inc-bootstrap inc-handlebars inc-jquery inc-scout2 listall-configs recurse_configs rules rulesets tests "$pkgdir"/usr/share/scout2-git
  grep -iRl 'python' "$pkgdir" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  ln -s /usr/share/$pkgname/Scout2.py "$pkgdir/usr/bin/$pkgname"
}

