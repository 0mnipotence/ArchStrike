# Maintainer: ArchStrike <team archstrike org>

buildarch=1

pkgname=pwntools
pkgver=3.12.0
pkgrel=1
pkgdesc='CTF framework and exploit development library'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-exploit' 'archstrike-misc')
depends_x86_64+=('lib32-glibc')
depends=('python2-dateutil' 'python2-paramiko' 'python2-mako' 'python2-pyelftools' 'capstone' 'ropgadget' 'python2-pyserial' 
         'python2-requests' 'python2-pygments' 'python2-pysocks' 'python2-tox' 'python2-pip' 'python2-psutil' 'python2-unicorn'
         'python2-intervaltree' 'python2-pypandoc' 'python2-sortedcontainers')
makedepends=('python2-setuptools')
replaces=('binjitsu-git')
provides=('binjitsu-git')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz")
sha512sums=('dd6a66cdb8710bb537484774aa052c810027c8a6a403e0e6a95fa4d3f4e65db9862c7ded596a79bc6b876bd9509b2a19c876ee2c703d0a29c5f71aa1b9cab992')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build(){
    cd "$pkgname-$pkgver"
    python2 setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir/" -O1 --skip-build

    # install license
    install -Dm644 LICENSE-pwntools.txt "$pkgdir/usr/share/licenses/$pkgname/license-pwntools.txt"
   
    # fix conflict bins
    mv "$pkgdir/usr/bin/checksec" "$pkgdir/usr/bin/pwntools-checksec"
    mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/pwntools-disasm"
    mv "$pkgdir/usr/bin/errno" "$pkgdir/usr/bin/pwntools-errno"

} 
