# Maintainer: ArchStrike <team@archstrike.org>
# Contributer: fnord0 <fnord0 AT riseup DOT net>

buildarch=212

pkgname=ghostpdl
pkgver=9.18
pkgrel=1
pkgdesc='An interpreter for the Postscript language and for PDF - GhostPCL (PCL-5, and PCL-XL), GhostXPS (XPS), GhostPDF and ghostscript'
url='http://www.artifex.com'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archstrike')
depends=('libxt' 'libxext' 'python' 'tcl')
source=("http://downloads.ghostscript.com/public/$pkgname-$pkgver.tar.gz")
sha512sums=('bad9d6e65503db1f01b70e0bfd52cb1f023291d80a6db845003796eec1a38885fcc5edc7ea1d2da044bef7e97f25f272721ec9eaab357c5eb0a3338769d92c2f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 bin/gpcl6 "$pkgdir/usr/bin/gpcl6"
  install -Dm755 bin/gxps "$pkgdir/usr/bin/gxps"
  install -Dm755 pcl/tools/pcl2pdf "$pkgdir/usr/bin/pcl2pdf"
  install -Dm755 pcl/tools/pcl2pdfwr "$pkgdir/usr/bin/pcl2pdfwr"
  cp -r --no-preserve=ownership doc/* "$pkgdir/usr/share/doc/$pkgname/"
  cp -r --no-preserve=ownership pcl/tools "$pkgdir/usr/share/$pkgname/"
  cp -r --no-preserve=ownership pcl/urwfonts "$pkgdir/usr/share/$pkgname/"
}
