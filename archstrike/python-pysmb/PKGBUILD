# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=pysmb
_pkgver="rel-1-1-28"
pkgname=("python-${_pkgname}" "python2-${_pkgname}")
pkgver=1.1.28
pkgrel=1
pkgdesc="An experimental SMB/CIFS library written in Python"
arch=('any')
url="https://miketeo.net/wp/index.php/projects/pysmb"
license=('GPL')
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/miketeo/pysmb/archive/${_pkgver}.tar.gz")
sha512sums=('30862ba4240254e781b673f8982e057a3011ee50fed926c3de844142d5bdd40f0332b58c57fe43ae8f8624e6c5a5f6e4542cb4bd45dac944aafb38d7a2dd151f')

prepare(){
  cp -a ${_pkgname}-${_pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}-${_pkgver}" || exit
  python setup.py build

  cd "${srcdir}/${_pkgname}-${_pkgver}-py2" || exit
  python2 setup.py build
}

package_python-pysmb() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${_pkgver}" || exit
  python3 setup.py install --root="${pkgdir}/" -O1 --skip-build
}

package_python2-pysmb() {
  depends=('python2')

  cd "${srcdir}/${_pkgname}-${_pkgver}-py2" || exit
  python2 setup.py install --root="${pkgdir}/" -O1 --skip-build
}
