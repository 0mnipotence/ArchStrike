# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=dfdatetime
pkgname=("python-${_pkgname}" "python2-${_pkgname}")
pkgver=20190517
pkgrel=1
pkgdesc="Provides date and time objects to preserve accuracy and precision"
url="https://github.com/log2timeline/dfdatetime"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
arch=('any')
options=('!emptydirs')
source=("https://github.com/log2timeline/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('ba77f794a560ef758b8f0bfdf0a32ba42b3f4442a965c0d7e3521bdf0d0f38bd206015cd1e4684aa2e8999ea8ca8389fe569ed8ad4a95cec0c59249c833ef191')

prepare(){
  cp -r ${_pkgname}-${pkgver}{,-py2}
}

build() {
  cd ${srcdir}/${_pkgname}-${pkgver} || exit
  python setup.py build

  cd ${srcdir}/${_pkgname}-${pkgver}-py2 || exit
  python2 setup.py build

}

package_python-dfdatetime() {
depends=('python' 'python-pip')
  cd ${srcdir}/${_pkgname}-${pkgver} || exit
  install -dm755 "${pkgdir}/usr/bin"
  python setup.py install --root="${pkgdir}" -O1 --skip-build
}

package_python2-dfdatetime() {
depends=('python2' 'python2-pip')
  cd ${srcdir}/${_pkgname}-${pkgver}-py2 || exit
  install -dm755 "${pkgdir}/usr/bin"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
}
