# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="Bluto"
pkgname="bluto"
pkgver=2.4.16
pkgrel=1
pkgdesc="Recon, Subdomain Bruting, Zone Transfers"
arch=('any')
groups=('archstrike' 'archstrike-recon' 'archstrike-dns')
url="https://github.com/darryllane/Bluto"
license=("GPL3")
depends=("python2-termcolor" "python2-lxml" "python2-beautifulsoup4" "python2-requests" "pythonwhois" "python2-dnspython" "python2-docopt" "python2-oletools" "pdfminer" "python2-olefile")
makedepends=("python2-setuptools")
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('379cd60c1e18985c0ad94071a3b64ac859975a2f8868e411fba8d372d4aaa36cdd8abef81018bdc34ca612cab8df38d9d2441975be5896538523f8a6cf49c57e')

prepare() {
  grep -iRl 'python' ${_pkgname}-${pkgver}/${_pkgname}/modules | xargs sed -i 's|python$|python2|'
}

build() {
  cd "${_pkgname}-${pkgver}/" || exit
  python2 setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}/" || exit
  python2 setup.py install --root="${pkgdir}/" -O1
  # fixing python
  sed -i 's|python|python2|' "${pkgdir}"/usr/lib/python2.7/site-packages/Bluto/bluto
}
