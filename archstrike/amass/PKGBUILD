# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="Amass"
pkgname="amass"
pkgver=3.4.4
pkgrel=1
pkgdesc=" tool to help information security professionals perform network mapping of attack surfaces and perform external asset discovery"
arch=('any')
url="https://github.com/OWASP/Amass"
license=('Apache')
makedepends=('go' 'git')
source=("https://github.com/OWASP/Amass/archive/v${pkgver}.tar.gz")
sha512sums=('47f99b8b817ea18b3068ff3dc0f2be5f2302041636f075d4f51ca41bf72ca508f694586e6a8feb9ac31e7244fefc7a8d0c75e067207f59a22739c5bccf496ba1')

prepare() {
  mkdir -vp "${srcdir}/github.com/OWASP"
  mv "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/github.com/OWASP/${_pkgname}"

  export GOPATH="${srcdir}"
  cd "${GOPATH}/github.com/OWASP/${_pkgname}" || exit
  go get -d ./...
}

check() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}" || exit
  export GOPATH="${srcdir}"
  go test ./...
}

build() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}" || exit
  export GOPATH="${srcdir}"
  go install -v ./...
}

package() {
  export GOPATH="${srcdir}"
  cd "${GOPATH}/bin" || exit
  install -dm 755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  cp -a --no-preserve=ownership "${GOPATH}/github.com/OWASP/${_pkgname}/"{wordlist/,examples/} "${pkgdir}/usr/share/${pkgname}"
  chmod 644 "${pkgdir}/usr/share/${pkgname}/"{wordlist/*,examples/*}
}
