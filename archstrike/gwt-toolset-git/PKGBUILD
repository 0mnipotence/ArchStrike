# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname='gwt-toolset-git'
pkgver=20120831.r7.f27a5aa
pkgrel=1
epoch=1
pkgdesc='analyzes the obfuscated Javascript produced by Google Web Toolkit (GWT) applications in order to enumerate all services and method calls'
url='http://www.gdssecurity.com/l/t/d.php?k=GwtEnum'
groups=('archstrike' 'archstrike-webapps' 'archstrike-recon')
license=('GPL3')
arch=('any')
depends=('python2')
makedepends=('git')
source=("$pkgname::git+https://github.com/GDSSecurity/GWT-Penetration-Testing-Toolset.git")
replaces=('gwtenum')
provides=('gwtenum')
conflicts=('gwtenum')
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//  g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$pkgname"

  _tools="gwtenum gwtfuzzer gwtparse"

  install -dm755 "$pkgdir/usr/share/gwt-toolset"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/gwt-toolset"

  for tool in $_tools
  do
    cat > "$pkgdir/usr/bin/$tool" <<EOF
#!/bin/sh
cd /usr/share/gwtenum/$tool
python2 $tool.py \$@
EOF
    chmod +x "$pkgdir/usr/bin/$tool"
  done
}
