# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Salan54 <salan at fremenil dot com>

buildarch=212

pkgname=zulucrypt
pkgver=4.9.0
pkgrel=1
pkgdesc="a cli and gui frontend to cryptsetup."
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url="https://github.com/mhogomchungu/zuluCrypt/"
license=('GPL')
depends=('cryptsetup' 'qt4' 'libpwquality' 'libsecret' 'tcplay' 'gtk-update-icon-cache')
makedepends=('cmake' 'git' 'automoc4')
optdepends=('kdeutils-kwallet: retrieve volume keys from kde kwallet')
options=('!strip' '!buildflags' '!distcc')
source=("https://github.com/mhogomchungu/zuluCrypt/releases/download/$pkgver/zuluCrypt-$pkgver.tar.xz")
sha512sums=('5de6627354df42e51dfa0a4207eeb9c7df345988d8d8c76307bd34673e9fdd610dedc45d90ebc9681041a6dbad362fc4c70d175d7495367dd155fd0288fed146')
install=${pkgname}.install

build() {
    cd $srcdir/zuluCrypt-"${pkgver}"
    mkdir -p "${srcdir}"/zuluCrypt-"${pkgver}"/build && cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DNOKDE=true -DUDEVSUPPORT=true \
    -DCMAKE_BUILD_TYPE=release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_LIB_SUFFIX=/usr/lib . ..
    make
}

package() {
  cd "zuluCrypt-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  chown root:root "${pkgdir}/usr/bin/zuluCrypt-cli"
  chown root:root "${pkgdir}/usr/bin/zuluMount-cli"
  chmod 0755 "${pkgdir}/usr/bin/zuluMount-cli"
  chmod 0755 "${pkgdir}/usr/bin/zuluCrypt-cli"
  if [[ -d "${pkgdir}"/usr/lib64 ]] ; then
    mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  fi
}
