# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: xorond <xorond@gmail.com>
_author='Karel Miko'
_perlmod='CryptX'

buildarch=220

pkgname=perl-crypt-x
pkgver=0.067
pkgrel=1
pkgdesc="Cryptographically secure random number generator"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://metacpan.org/pod/CryptX"
license=('custom')
depends=('perl>=5.10.0')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIK/CryptX-${pkgver}.tar.gz")
sha512sums=('587eb4563183d897be1b58c3256c1f61d1183ffad439e84c519160c1f706fe4b0df7389b195a3f4de0376a2218121a6f6ed288c30b3f9fc283ada7ef64bf324f')

build() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_perlmod}-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  make install DESTDIR="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
