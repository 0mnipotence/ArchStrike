# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=3proxy
pkgver=0.8.3
pkgrel=1
groups=('archstrike' 'archstrike-misc')
pkgdesc='Tiny free proxy server'
url='http://3proxy.ru'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('glibc')
options=('!makeflags')
source=("https://github.com/z3APA3A/3proxy/archive/3proxy-0.8.3.tar.gz")
sha512sums=('dd6414ff0995b3d8d9d19e3de5e9ae7c602371d9ded9212b1ec725fe5ae4e3ccb0c3a57ce5575fa4c508f17fe2faa34f22798c04b9b8a0c8b622fc1abc309015')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  sed -i 's|/usr/bin/install|/bin/install|g' Makefile.Linux
  sed -i 's|/usr/local|/usr|g' Makefile.Linux
  sed -i 's|-s ||g' Makefile.Linux
  sed -i "s|-Wall -g -O2|$CFLAGS|g" Makefile.Linux
  sed -i 's|\$(prefix)/etc/|/etc/|g' Makefile.Linux
}

build() {
  cd $pkgname-$pkgname-$pkgver
  make -f Makefile.Linux prefix=/usr
}

package() {
  cd $pkgname-$pkgname-$pkgver
  make -f Makefile.Linux install DESTDIR="$pkgdir"
  find "$pkgdir/etc/$pkgname" -type f | xargs chmod 644
  mv "$pkgdir/usr/bin/proxy" "$pkgdir/usr/bin/$pkgname"
}
