# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=nassl
pkgver=0.16.3
pkgrel=1
groups=('archstrike' 'archstrike-misc' 'archstrike-crypto')
pkgdesc='Experimental Python wrapper for OpenSSL'
arch=('i686' 'x86_64')
url='https://github.com/nabla-c0d3/nassl'
license=('GPL')
depends=('python2')
makedepends=('openssl' 'python2-setuptools')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/nabla-c0d3/nassl/archive/${pkgver}.tar.gz"
  "${pkgname}-lfs-${pkgver}.tar.gz::https://github.com/ArchStrike/nassl-lfs/archive/${pkgver}.tar.gz"
)

sha512sums=(
  'f5ecbb3e4e5a0877df9ffd55d7b5939d131571bd700982e82129e41ed5aa05d8d368cc0ebb3b3afa08a47c7f5f705901516bb6c6aa2e50c0ced81fe5fa5f5048'
  '6a24a6046e450a4a9b9f859faa4141d4c5ecd4f13d3414d795d58c70ee0b986d5cda4991cf7bc0956a9a467f6f39cd72e656361a417a308af68c610cb7e04595'
)

prepare() {
  cp -r ${pkgname}-lfs-${pkgver}/bin ${pkgname}-${pkgver}/
  cd ${pkgname}-${pkgver}
  grep -iRl python | xargs sed -i 's|python$|python2|g'
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build_ext -i
}

package() {
  cd ${pkgname}-${pkgver}
  python2 setup.py install --root="$pkgdir" --optimize=1
}
