#Maintainer: ArchStrike <team@arcstrike.org>

buildarch=1

pkgbase=python2-boto3

pkgname=(python2-boto3 python-boto3)
pkgver=1.7.25
pkgrel=1
pkgdesc="The AWS SDK for Python"
url="https://github.com/boto/boto3"
depends=('python2' 'python2-nose' 'python2-mock' 'python2-wheel' 'python2-unittest2' 'python2-botocore')
makedepends=('python-setuptools' 'python2-setuptools')
license=('APACHE')
arch=('any')
source=("https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('8cee65a71578fc77d3e157747a42a48183112a62311ea9579c52b5ab3a0f0872a56bb7da765a2cb021455a5595763aede223c135626b688c3f85841901977c72')

prepare() {
  cp -a boto3-$pkgver{,-py2}
}

build() {
  cd "$srcdir/boto3-$pkgver"
  python3 setup.py build

  cd "$srcdir/boto3-$pkgver-py2"
  python2 setup.py build
}

package_python2-boto3() {
    depends=('python2')

    cd $srcdir/boto3-$pkgver-py2
    python2 setup.py install --root="$pkgdir" -O1 --skip-build
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package_python-boto3() {
    depends=('python')

    cd $srcdir/boto3-$pkgver
    python setup.py install --root="$pkgdir" -O1 --skip-build
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
