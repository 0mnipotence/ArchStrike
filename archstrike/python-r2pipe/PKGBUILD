# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=r2pipe
pkgbase=python-$_pkgname

buildarch=1

pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=1.2.0
pkgrel=1
pkgdesc="A Pipe interface for radare2"
url="https://pypi.python.org/pypi/r2pipe/"
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('37a029593b7f035dac4d29dc25d52aa84d2ff9deaabf9a9dbf82ddc7ad23baf3a8fcf7d0d4189ec870579325e16568902579d5a690ceafacd3e1c6d695675e67')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py build

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py build
}

package_python-r2pipe() {
  depends=('python')

  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir/" -O1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" -O1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  sed -i 's|env python|env python2|' $pkgdir/usr/lib/python2.7/site-packages/r2pipe/__init__.py
}

