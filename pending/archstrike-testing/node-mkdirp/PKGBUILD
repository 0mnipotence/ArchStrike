# Maintainer: ArchStrike <team@archstrike.org>
_pkgname=mkdirp

buildarch=1

pkgname=node-$_pkgname
pkgver=0.5.1
pkgrel=1
pkgdesc='Recursively mkdir, like mkdir -p, but in node.js'
arch=('any')
url='https://github.com/substack/node-mkdirp'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("$url/archive/$pkgver.tar.gz")
sha512sums=('53224522b139ec76e550ce8045087f05a880f1d2ee5dc838f59898ac7f3f7195a24c12ca74e3167d71d09af4b873db5645f5a6bd0416f13f66d02ab44e81654e')

prepare() {
  # Install node dependencies
  cd $pkgname-$pkgver
  npm install 2>/dev/null
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 readme.markdown "$pkgdir/usr/share/doc/$pkgname/readme.markdown"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 package.json "$pkgdir/usr/lib/node_modules/$_pkgname/package.json"
  install -Dm755 index.js "$pkgdir/usr/lib/node_modules/$_pkgname/index.js"
  cp -r --no-preserve=ownership {bin,examples,node_modules,test} "$pkgdir/usr/lib/node_modules/$_pkgname/"
  install -dm755 "$pkgdir/usr/bin"
  ln -s /usr/lib/node_modules/$_pkgname/bin/cmd.js "$pkgdir/usr/bin/$_pkgname"
}
