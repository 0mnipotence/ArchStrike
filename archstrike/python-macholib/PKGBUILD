# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python-macholib
_pkgname=macholib
pkgver=1.11
pkgrel=2
pkgdesc="Mach-O header analysis and editing"
url="https://pypi.org/project/macholib/"
depends=('python' 'python-altgraph')
makedepends=('python-setuptools')
license=('MIT')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('626cb564ea22e61a5b395c5ffbe9b17df0c341f2355bd0def88faabab52d2bc43f8f61871b50e7bd53147965627b4d3399f96a651bd985418e32b519b7bb03bb')


build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  install -dm755 "${pkgdir}/usr/bin"
  python setup.py install --root="${pkgdir}" --skip-build -O1

  # fix conflict with python2-macholib
  for i in dump find standalone; do
    mv ${pkgdir}/usr/bin/macho_${i} ${pkgdir}/usr/bin/macho_${i}3
  done
}
