# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname=("pytesseract")
pkgname=("python-${_pkgname}")
pkgver=0.3.0
pkgdesc="Python wrapper for Google Tesseract"
pkgrel=1
arch=('any')
url="https://github.com/madmaze/pytesseract"
license=('GPL3')
depends=('python' 'python-pytest' 'flake8' 'python-pep8-naming'
         'python-flake8-quotes' 'tesseract')
makedepends=('python-setuptools')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('bea57caa829f7095b7fbf7e3700863693f4aa0256aa14812848c0da44a83f7c2362c81487d9d6a3ea5efb7f86d8128e9c868f0298d69748568011c2638e48cb3')

build() {
  cd "${_pkgname}-${pkgver}" || exit
  python setup.py build
}

check() {
  cd "${_pkgname}-${pkgver}" || exit
  python setup.py test
}

package() {
  cd "${_pkgname}-${pkgver}" || exit

  # Create tests directory and move files
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}/tests"
  cp -a --no-preserve=ownership ./tests/* "${pkgdir}/usr/share/${pkgname}/tests"

  python setup.py install --root="${pkgdir}" -O1 --skip-build
}
