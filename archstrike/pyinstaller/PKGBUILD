# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=pyinstaller
_pkgname=PyInstaller
pkgver=3.4
pkgrel=1
pkgdesc="Bundles a Python application and all its dependencies into a single package."
url="https://github.com/pyinstaller/pyinstaller"
depends=('python' 'python-altgraph' 'python-crypto' 'python-pefile' 'python-macholib' 'binutils' 'zlib' 'gcc' 'glibc')
makedepends=('python-setuptools')
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
source=("${url}/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('8a1e0065e8c7760ffcae906d3469914c98a69139658a3a85f1bb7b1a63265e33603314586897bf3b588400d2bdd48bb6ee06cef68c19232c4ec8a8df84e57330')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}"
  python setup.py install --root="${pkgdir}" -O1 --skip-build
}

# vim:se ts=2 sw=2 sts=2 et:
