# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=spiderfoot
pkgver=2.7.0
pkgrel=1
groups=('archstrike' 'archstrike-recon')
pkgdesc='The Open Source Footprinting Tool'
url='http://spiderfoot.net'
arch=('any')
license=('GPL2')
depends=('python2-lxml' 'python2-mako' 'python2-cherrypy' 'python2-m2crypto' 'python2-netaddr')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver-src.tar.gz")
sha512sums=('0bc5cbfcdda0b47478bd4451bfbde47c1232a8b84bfa06db0a68372453619c5e38d793eda87bdf26bbb0334e7061b6cc9498335956103247cb0e5b138d28836e')

prepare() {
  grep -iRl 'python' ${pkgname}-${pkgver} | xargs sed -i 's|python$|python2|'
}

package() {
  install -dm755 "$pkgdir"/usr/{bin,share}
  cp -r $pkgname-$pkgver "$pkgdir/usr/share/$pkgname"
  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/usr/bin/env bash
cd /usr/share/$pkgname
./sf.py
EOF
  chmod 755 "$pkgdir/usr/bin/$pkgname"
}
