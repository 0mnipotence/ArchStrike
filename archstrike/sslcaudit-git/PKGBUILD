# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=sslcaudit-git
pkgver=r524.f218b9b
pkgrel=4
groups=('archstrike' 'archstrike-scanners')
pkgdesc="Utility to perform security audits of SSL/TLS clients"
url='https://github.com/abbbe/sslcaudit'
arch=('any')
license=('GPL3')
depends=('python2-m2crypto')
makedepends=('git' 'python2-setuptools')
source=('sslcaudit::git+https://github.com/abbbe/sslcaudit.git')
provides=('sslcaudit')
replaces=('sslcaudit')
conflicts=('sslcaudit')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/sslcaudit"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/sslcaudit"
  python2 setup.py build
}

package() {
  cd "$srcdir/sslcaudit"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
