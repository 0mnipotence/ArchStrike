# Maintainer: ArchStrike <team[at]archstrike[dot]org
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Ainola
# Contributor: Chris Fordham

pkgname=python2-botocore
pkgver=1.7.47
pkgrel=1
pkgdesc='A low-level interface to a growing number of Amazon Web Services'
arch=('any')
url="https://github.com/boto/botocore"
license=('Apache')
depends=('python2-dateutil' 'python2-jmespath' 'python2-docutils')
makedepends=('python2-setuptools')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('899aa029c17703e63239601429968d61d98cfe89ac09fd6987f80a52a1d21508dbd64471aba42942d5149003ab8484070fc57e57ecc63d031bd9f7c80462b8ee')

build() {
  cd botocore-$pkgver
  python2 setup.py build
}

package() {
  cd botocore-$pkgver
  python2 setup.py install --root="$pkgdir" -O1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  grep -iRl 'python' "$pkgdir" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|' 
}
