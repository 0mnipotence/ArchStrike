# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="Amass"
pkgname="amass"
pkgver=3.2.1
pkgrel=1
pkgdesc=" tool to help information security professionals perform network mapping of attack surfaces and perform external asset discovery"
arch=('any')
url="https://github.com/OWASP/Amass"
license=('Apache')
makedepends=('go' 'git')
source=("https://github.com/OWASP/Amass/archive/v${pkgver}.tar.gz")
sha512sums=('09d875bd435c203eed651e931b8703d9745ca4b5a262b78fd59fb9049059a15cec9d0d57812afe7ae52555738a7306d818f9b1b25c61b97f8ac87ef5c66e1f83')

prepare() {
  mkdir -vp "${srcdir}/github.com/OWASP"
  mv "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/github.com/OWASP/${_pkgname}"

  export GOPATH="${srcdir}"
  cd "${GOPATH}/github.com/OWASP/${_pkgname}" || exit
  go get -d ./...
}

check() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}" || exit
  export GOPATH="${srcdir}"
  go test ./...
}

build() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}" || exit
  export GOPATH="${srcdir}"
  go install -v ./...
}

package() {
  export GOPATH="${srcdir}"
  cd "${GOPATH}/bin" || exit
  install -dm 755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  cp -a --no-preserve=ownership "${GOPATH}/github.com/OWASP/${_pkgname}/"{wordlist/,examples/} "${pkgdir}/usr/share/${pkgname}"
  chmod 644 "${pkgdir}/usr/share/${pkgname}/"{wordlist/*,examples/*}
}
