# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=netsniff-ng
pkgver=0.6.3
pkgrel=1
groups=('archstrike' 'archstrike-sniffers')
pkgdesc="A high performance Linux network sniffer for packet inspection."
depends=('geoip' 'libsodium' 'liburcu' 'libcli' 'libnet' 'libnetfilter_conntrack' 'libpcap')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://netsniff-ng.org/'
license=('GPL2')
makedepends=('hardening-wrapper')
source=(https://github.com/netsniff-ng/netsniff-ng/archive/v${pkgver}.tar.gz)
sha512sums=('ee8383fa05a5a6ee4ae3ecbe2b29e79af3f5625f73a97e2131b3a4086dc408bed6723ac3ab0bf6627731af045daaeb27a1857d5272124593485ebf2f87e9eabb')

build() {
  cd "$pkgname-$pkgver"
  # forces use of newer libsodium library for curvetun
  export NACL_LIB=sodium
  export NACL_INC_DIR=$(pkg-config --variable=includedir libsodium )/sodium
  ./configure --prefix=/usr
  # fix location of sbin
  sed -i 's|sbin|bin|' Makefile
  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  install -dm755 "$pkgdir"/usr/bin
  install -dm755 "$pkgdir"/usr/share/man/man8/
  make DESTDIR="${pkgdir}" install
}
