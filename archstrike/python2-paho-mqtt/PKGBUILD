# Maintainer: ArchStrike <team archstrike org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

buildarch=1

pkgbase=paho-mqtt
_pkgname=paho.mqtt.python
pkgname=(python-$pkgbase python2-$pkgbase)
pkgver=1.4.0
pkgrel=1
url="http://www.eclipse.org/paho/clients/python"
license=('EPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/eclipse/paho.mqtt.python/archive/v${pkgver}.tar.gz")
sha512sums=('2e49f0f146207ab4fbc8c346b10d3e2b50869b2d9db7c999b6645f7213fb635b64cff01c5405e7833a8b25334d24685ce6ed734a6e4b6b0660b48f65cf4a941c')

prepare() {
   cp -r $_pkgname-${pkgver} python2-$_pkgname-${pkgver}
}

build() {
   cd $srcdir/$_pkgname-${pkgver}
   python setup.py build

   cd $srcdir/python2-$_pkgname-${pkgver}
   python2 setup.py build
}

package_python-paho-mqtt() {
    pkgdesc="Python client library for MQTT v3.1 and v3.1.1"
    depends=('python')

    cd $srcdir/$_pkgname-$pkgver
    python setup.py install --root="$pkgdir" -O1 --skip-build
}

package_python2-paho-mqtt() {
    pkgdesc="Python 2 client library for MQTT v3.1 and v3.1.1"
    depends=('python2')

    cd $srcdir/python2-$_pkgname-$pkgver
    python2 setup.py install --root="$pkgdir" -O1 --skip-build
}
