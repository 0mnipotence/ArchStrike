# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: Thomas Conneely <tc116 at le dot ac dot uk>

buildarch=1

pkgname=python2-bitstring
pkgver=3.1.4
pkgrel=1
pkgdesc='Python module designed to help make the creation, manipulation and analysis of binary data as simple and natural as possible'
arch=('any')
url="http://code.google.com/p/python-bitstring/"
license=('MIT')
depends=('python2')
makedepends=('unzip' 'python2-setuptools')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/b/bitstring/bitstring-$pkgver.zip)
sha512sums=('9c10b742db32a7f4ee1173169ae4f599ccba3570a6bfcb2f466b5e7411584b90b53274bd5dd03351234431ed5fcf8df42a021143fb921d878d511776e1c8997c')

prepare(){
  grep -iRl 'python' "$srcdir/bitstring-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  sed -i '/README.rst/d' $srcdir/bitstring-$pkgver/setup.py
  sed -i '/kwds/d' $srcdir/bitstring-$pkgver/setup.py
}

build() {
  cd ${srcdir}/bitstring-${pkgver}

  #get the license file
  python2 -c "import bitstring; print(bitstring.__licence__)" >> ${srcdir}/LICENSE
}

package() {
  cd ${srcdir}/bitstring-${pkgver}

  # build
  python2 setup.py install --root=${pkgdir}/ --optimize=1

  #install license
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
