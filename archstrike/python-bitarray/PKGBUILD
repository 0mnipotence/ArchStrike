# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

buildarch=220

_pkgname=bitarray
pkgbase=python-bitarray
pkgname=("python-${_pkgname}" "python2-${_pkgname}")
pkgver=1.0.1
pkgrel=1
pkgdesc="Efficient arrays of booleans for Python"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/ilanschnell/bitarray"
license=('PSF')
makedepends=('python2-setuptools' 'python-setuptools')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/b/bitarray/bitarray-${pkgver}.tar.gz)
sha512sums=('1fb1f1c2037ab598ff0c845d59eb68c3fa7e741fcfcb2ecb78c6b5d06c118f5fadb79af292191d19bf58c4da1aacec1fb8c21e61d353b5068742ded8d4baf476')

prepare(){
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py build
}

package_python-bitarray() {
  depends=('python')

  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
}

package_python2-bitarray() {
  depends=('python2')

  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
}
