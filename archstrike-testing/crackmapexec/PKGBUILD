# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=crackmapexec
_pkgname=CrackMapExec
pkgver=1.0.9
pkgrel=1
groups=('archstrike' 'archstrike-exploits')
pkgdesc=" A swiss army knife for pentesting Windows/Active Directory environments"
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('GPL3')
depends=('impacket' 'python2-gevent' 'python2-netaddr' 'python2-crypto' 'python2-pyasn1' 'python2-termcolor')
source=("https://github.com/byt3bl33d3r/CrackMapExec/archive/v$pkgver.tar.gz")
sha512sums=('27dc60d1d45d022bd090265eb9136c91780c07ad14b689048028268a4f9cfc81f9c8eab497c4c4eb2ac9e0a8deec7ce048b0170608f1781caea04735792e01f2')

prepare(){
  cd "${_pkgname}-${pkgver}"
  grep -iRl 'python' "$srcdir/${_pkgname}-${pkgver}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "${_pkgname}-${pkgver}"

  install -dm755 "$pkgdir/usr/share/${pkgname}/"
  install -dm755 "$pkgdir/usr/bin/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/${pkgname}/"

  cd ${pkgdir}
  find ./ -type d -exec chmod 755 {} \+
  find ./ -type f -exec chmod 644 {} \+

cat > "${pkgdir}/usr/bin/$pkgname" <<EOF
#!/bin/sh
cd /usr/share/$pkgname
python2 ${pkgname}.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/$pkgname"
}
