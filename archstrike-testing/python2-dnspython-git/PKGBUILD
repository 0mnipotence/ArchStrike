# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=python2-dnspython-git
pkgver=v1.12.0.r47.g40dcfc8
pkgrel=1
epoch=1
pkgdesc="A DNS toolkit for python"
arch=('any')
url="http://www.dnspython.org"
license=('BSD')
depends=('python2')
makedepends=('git')
sha512sums=('SKIP')
source=("git+https://github.com/rthalley/dnspython.git")

pkgver() {
  cd "$srcdir"/dnspython
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir"/dnspython
  python2 setup.py build
}

package() {
  cd "$srcdir"/dnspython
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/python2-dnspython-git/LICENSE
  install -Dm644 README.md "$pkgdir"/usr/share/python2-dnspython-git/README.md
  install -Dm644 ChangeLog "$pkgdir"/usr/share/python2-dnspython-git/ChangeLog
}
