# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=stringcase
pkgbase=python-${_pkgname}
pkgname=(python-${_pkgname} python2-${_pkgname})
pkgdesc="String case converter"
pkgver=1.2.0
pkgrel=2
arch=('any')
url="https://github.com/okunishinishi/python-stringcase"
license=('MIT')
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/f3/1f/1241aa3d66e8dc1612427b17885f5fcd9c9ee3079fc0d28e9a3aeeb36fa3/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('c987f9aa42d22bcfaa6559791bf9292e4300af00d2cf2165838cacd6203b913da197852d49285c30188ca529fd529513cce73a1984f5555cfb81e704977786d3')

prepare(){
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

package_python-stringcase() {
  depends=('python')
  makedepends=('python-setuptools')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" -O1
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-stringcase() {
  depends=('python2')
  makedepends=('python2-setuptools')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}/" -O1
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
