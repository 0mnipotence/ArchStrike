# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=sulley-git
pkgver=1.0.3bce87a
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-fuzzers')
pkgdesc="A pure-python fully automated and unattended fuzzing framework"
arch=('any')
url='https://github.com/OpenRCE/sulley/'
license=('GPL2')
makedepends=('git')
replaces=('sulley')
provides=('sulley')
depends=('libpcap' 'python2-pcapy' 'impacket')
source=("$pkgname::git+https://github.com/OpenRCE/sulley.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  # Grabbing major-version from install script
  printf "%s.%s" \
    $(grep -i 'define PRODUCT_VERSION' installer/Nullsoft\ Installer\ Script.nsi | cut -d'"' -f2) \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  grep -iRl '/usr/bin/python' "$pkgname" | xargs sed -i 's|#!.*/usr/bin/python$|#!/usr/bin/python2|'
}

package() {
  cd "$pkgname"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname"
}
