# Maintainer: ArchStrike <team@archstrike.org>

pkgname=bettercap
pkgver=1.5.1
pkgrel=2
pkgdesc='Complete, modular, portable and easily extensible MITM framework'
url='https://github.com/evilsocket/bettercap'
arch=('any')
license=('GPL3')
depends=('ruby' 'ruby-bundler') # 'ruby-rubydns
makedepends=('libpcap')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/evilsocket/bettercap/archive/v${pkgver}.tar.gz)
sha512sums=('282c7dd5da347aef3c322c0beff1e625eafb0696454ce43ecfd9657cb7b10130911c4aec026a6effa2de15ef3937a28b3d9360ba665a5f8d1a1dfa730fbb0a69')

package() {
  cd ${pkgname}-${pkgver}
  install -dm755 $pkgdir/usr/share/$pkgname
  install -dm755 $pkgdir/usr/bin/
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Move application files to the share folder
  cp -a --no-preserve=owner * "$pkgdir/usr/share/$pkgname"

  cd $pkgdir/usr/share/$pkgname
  bundle config build.nokogiri --use-system-libraries
  bundle install --path vendor/bundle
  find . -type d -name "extensions" |xargs rm -rf

  # Create the launcher
  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/usr/bin/env bash
cd /usr/share/$pkgname
unset GEM_HOME
/usr/bin/ruby ./bin/$pkgname "\$@"
EOF
  chmod 0755 "$pkgdir/usr/bin/$pkgname"
}


# vim: ts=2 sw=2 et:
