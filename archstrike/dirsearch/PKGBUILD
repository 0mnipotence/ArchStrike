# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=dirsearch
pkgver=0.3.9
pkgrel=1
groups=('archstrike' 'archstrike-webapps')
pkgdesc='A HTTP(S) directory/file brute forcer'
arch=('any')
url='https://github.com/maurosoria/dirsearch'
license=('custom')
depends=('python')
source=("https://github.com/maurosoria/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('c774e939ff1bf9d110bc3b62e344814f4371b9fdf06ccf70ac25866437c262f7aea46d83a3f176a6e736abc949d5cfec49fe3fc4fcb8aa69ffacb83aa62f4b5c')

package() {
  cd "${pkgname}-${pkgver}" || exit

  # Make base directories.
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  for dir in db logs lib thirdparty reports; do
    install -dm755 "${pkgdir}/usr/share/${pkgname}/${dir}"
    cp -a --no-preserve=ownership "${dir}"/* "${pkgdir}/usr/share/${pkgname}/${dir}/"
  done

  install -Dm755 ${pkgname}.py "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 default.conf "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 README.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  ln -s "/usr/share/${pkgname}/${pkgname}.py" "${pkgdir}/usr/bin/${pkgname}"
}
