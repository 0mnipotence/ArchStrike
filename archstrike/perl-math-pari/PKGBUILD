# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=perl-math-pari
pkgver=2.01080900
pkgrel=1
pkgdesc="Perl interface to PARI"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
epoch='1'
url='http://search.cpan.org/~ilyaz/Math-Pari-2.01088/Pari.pm'
license=('GPL Artistic')
depends=('perl')
options=('!emptydirs' '!distcc')
source=("http://search.cpan.org/CPAN/authors/id/I/IL/ILYAZ/modules/Math-Pari-$pkgver.zip")
sha512sums=('844324472632081e16d8c70e2f3b14a04cc125dc65ac1e4e75b495f7b83393cadc637c6b81d2e6657380b153a6d91eba4a437087481ffbd4c6c58c5da0e4d05b')

build() {
  cd "$srcdir/Math-Pari-$pkgver"
  if [[ $CARCH == "i686" ]]; then
        echo -e "y\n" | perl Makefile.PL machine=ix86 installdirs=vendor
  else
        echo -e "y\n" | perl Makefile.PL installdirs=vendor
  fi
  make
}

check() {
  cd "$srcdir/Math-Pari-$pkgver"
  make test
}

package() {
  cd "$srcdir/Math-Pari-$pkgver"

  make install "DESTDIR=$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
