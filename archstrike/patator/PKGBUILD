# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=patator
pkgver=0.8
pkgrel=1
groups=('archstrike' 'archstrike-crackers')
pkgdesc="A multi-purpose bruteforcer"
arch=('any')
url='https://github.com/lanjelot/patator'
license=('GPL2')
depends=('python')
optdepends=('python-paramiko: SSH'
            'python-pycurl: HTTP'
            'openldap: LDAP'
            'impacket: SMB'
            'python-cx_oracle: Oracle'
            'mysql-python: MySQL'
            'python-psycopg2: PostgreSQL'
            'python-cryptography: VNC'
            'python-pydns: DNS'
            'python-pysnmp: SNMP'
            'python-ipy: NETx keywords'
            'java-runtime: keystore files'
            'unzip: zip archives')
source=("https://github.com/lanjelot/patator/archive/$pkgver.tar.gz")
sha512sums=('1c69e9663dd9afb9328bc053d28a0846ca1cfe28f2c7565b254451a77dbd17b9ac0a444999b7fff4e0f0cceff6c4796405e6ad075f6542566778528c615c92b4')

package() {
  install -D -m 0755 "$srcdir/$pkgname-$pkgver/patator.py" "$pkgdir/usr/bin/patator"

  _modules=(
    'ftp_login'
    'ssh_login'
    'telnet_login'
    'smtp_login'
    'smtp_vrfy'
    'smtp_rcpt'
    'finger_lookup'
    'http_fuzz'
    'pop_login'
    'pop_passd'
    'imap_login'
    'ldap_login'
    'smb_login'
    'smb_lookupsid'
    'vmauthd_login'
    'mssql_login'
    'oracle_login'
    'mysql_login'
    'pgsql_login'
    'vnc_login'
    'dns_forward'
    'dns_reverse'
    'snmp_login'
    'unzip_pass'
    'keystore_pass'
  )
  for module in ${_modules[@]}; do
    ln -s /usr/bin/patator "$pkgdir/usr/bin/$module"
  done
}

