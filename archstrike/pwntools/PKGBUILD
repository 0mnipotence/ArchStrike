# Maintainer: ArchStrike <team archstrike org>

buildarch=1

pkgname=pwntools
pkgver=3.4.1
pkgrel=3
pkgdesc='CTF framework and exploit development library'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-exploit' 'archstrike-misc')
depends_x86_64+=('lib32-glibc')
depends=('python2-dateutil' 'python2-paramiko' 'python2-mako' 'python2-pyelftools' 'capstone' 'ropgadget' 'python2-pyserial' 
         'python2-requests' 'python2-pygments' 'python2-pysocks' 'python2-tox' 'python2-pip' 'python2-psutil' 'unicorn'
         'python2-intervaltree' 'python2-pypandoc')
makedepends=('python2-setuptools')
conflicts=('unicorn-engine')
replaces=('binjitsu-git' 'unicorn-engine')
provides=('binjitsu-git')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz")
sha512sums=('680bb96c8c6485e1a4b7b6e34d1cdcaade4ad9efcfb29eb1931b5380efa7343ac939eaa6943ec5dbcf3eafb71a1580143bf4c555457bd9382db8e9bb685198d8')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build(){
    cd "$pkgname-$pkgver"
    python2 setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir/" --optimize=1

    # install license
    install -Dm644 LICENSE-pwntools.txt "$pkgdir/usr/share/licenses/$pkgname/license-pwntools.txt"
   
    # fix conflict bins
    mv "$pkgdir/usr/bin/checksec" "$pkgdir/usr/bin/pwntools-checksec"
    mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/pwntools-disasm"
    mv "$pkgdir/usr/bin/errno" "$pkgdir/usr/bin/pwntools-errno"

} 
