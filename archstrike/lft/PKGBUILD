# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=lft
pkgver=3.76
pkgrel=1
groups=('archstrike' 'archstrike-recon')
pkgdesc="A layer four traceroute implementing numerous other features"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('custom')
url="http://pwhois.org/lft/"
depends=('libpcap')
makedepends=('hardening-wrapper')
source=("http://pwhois.org/dl/index.who?file=lft-$pkgver.tar.gz" "config.guess-for-aarch64.patch" "define_default_source.patch")
sha512sums=('b641a388293f6877b992089a9ab0cbe93f598aca6b181588e77823ba30f54692ebb8651f8aed5700ba84d2e083f0979885552e637f7928d1c7fb7b075c6630ff'
            '784ad436f82c02845e464a9735eb211158cf36df1b74bb2706c1fa38b69f32862723ef96a1469cc290d82dee29b1d237f9b44390b01bbdf157b7eeaeb5322fe4'
            '7b0f3c1a588ff3a55fa949039102a388a77dc12b276023501bd46bef9a44f747423919cac5322596ee6eb4360972ac5ca543c98b12211e65e94f7cfd7f6820bb')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i $srcdir/define_default_source.patch
  patch -Np1 -i $srcdir/config.guess-for-aarch64.patch
}

build() {
  cd "$pkgname-$pkgver"
  export CFLAGS+=" -fPIE"
  export CFLAGS+=" -z now -pie"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make "prefix=$pkgdir/usr" install
  install -Dm644 "$srcdir/lft-$pkgver"/COPYING \
    "$pkgdir"/usr/share/licenses/lft/LICENSE

  # Removing setuid on bin
  chmod 755 $pkgdir/usr/bin/lft
}
