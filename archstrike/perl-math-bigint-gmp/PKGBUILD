# Maintainer: ArchStrike <team@archstrike.org>
_author=PJACKLAM
_perlmod=Math-BigInt-GMP

buildarch=220

pkgname=perl-math-bigint-gmp
pkgver=1.6002
pkgrel=1
pkgdesc="Big integer calculations using the GNU Multiple Precision Arithmetic Library."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='http://search.cpan.org/~PJACKLAM/Math-BigInt-GMP-1.37/'
license=('GPL' 'PerlArtistic')
depends=('perl>=5.16.2' 'gmp')
options=(!emptydirs)
source=(http://cpan.perl.org/modules/by-authors/id/P/PJ/$_author/$_perlmod-$pkgver.tar.gz)
sha512sums=('1c3a4a616bfb386e246ca7fd784954c97fa3a20997db0372e7f4691896e5633e2abb9d9ca42fb781a169d10d36dd3a59759399485a1859cc95c4cf7ff4e5ca09')

build() {
  cd "$srcdir/$_perlmod-$pkgver"
  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
