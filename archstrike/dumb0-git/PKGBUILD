# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=dumb0
pkgname=${_pkgname}-git
pkgver=r21.7f2ca01
pkgrel=1
groups=('archstrike' 'archstrike-scanners' 'archstrike-webapps')
pkgdesc="A simple tool to dump users in popular forums and CMS :)"
arch=('any')
url='https://github.com/0verl0ad/Dumb0'
license=('GPL2')
depends=('python2' 'python2-requests' 'python2-argparse')
makedepends=('git')
provides=("${_pkgname}")
replaces=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${_pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  sed -i '1i\#!/usr/bin/python2' ${_pkgname}/Dumb0.py
}

package() {
  cd "${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share"
  install -dm755 "${pkgdir}/usr/share/licenses/dumb0"

  install -Dm755 Dumb0.py "${pkgdir}/usr/bin/dumb0"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/dumb0/"
  install -Dm644 README.md "${pkgdir}/usr/share/dumb0/README.md"

}
