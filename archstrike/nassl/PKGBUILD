# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=nassl
pkgver=2.2.0
pkgrel=1
groups=('archstrike' 'archstrike-misc' 'archstrike-crypto')
pkgdesc='Experimental Python wrapper for OpenSSL'
arch=('i686' 'x86_64')
url='https://github.com/nabla-c0d3/nassl'
license=('GPL')
depends=('python')
makedepends=('openssl' 'openssl-1.0' 'zlib' 'python-setuptools' 'python-invoke' 'python-pipenv')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/nabla-c0d3/nassl/archive/${pkgver}.tar.gz")

sha512sums=('75dc81adb2c01663dbb9b8f4e7d457e206c53ea988e55024c09bcfae16a9f13915b718721624377a8e0330e1fcc6019dbc3a8925a53a791d7f2582c0a6f9202f')

build() {
  cd ${pkgname}-${pkgver}
  # since archlinux does not include libssl.a files, ArchStrike used to host them at
  #    https://github.com/ArchStrike/nassl-lfs
  # following the compile instructions the static files which will be linked are created. So, nassl-lfs was removed
  # and a hybrid of their compile instructions is used.
  pipenv install --dev
  invoke build.zlib
  invoke build.legacy-openssl
  invoke build.modern-openssl
  python setup.py build_ext -i -I/usr/include/openssl-1.0/ -I/usr/include/openssl/
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="$pkgdir" -O1 --skip-build
}
