# Maintainer: ArchStrike <team archstrike org>

buildarch=196

pkgname=pwntools
pkgver=3.12.2
pkgrel=1
pkgdesc='CTF framework and exploit development library'
arch=('armv7' 'i686' 'x86_64')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-exploit' 'archstrike-misc')
depends_x86_64+=('lib32-glibc')
depends=('python2-dateutil' 'python2-paramiko' 'python2-mako' 'python2-pyelftools' 'capstone' 'ropgadget' 'python2-pyserial' 
         'python2-requests' 'python2-pygments' 'python2-pysocks' 'python2-tox' 'python2-pip' 'python2-psutil' 'python2-unicorn'
         'python2-intervaltree' 'python2-pypandoc' 'python2-sortedcontainers')
makedepends=('python2-setuptools')
replaces=('binjitsu-git')
provides=('binjitsu-git')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz")
sha512sums=('6dcb3a83e668b2ab9ac191e66430702b9e62813f6d7872f8ba08a6b1753235c542810eaeef8bed94f4286309db4094e66ee9ff9895bbc049983be8d4c67a01f8')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build(){
    cd "$pkgname-$pkgver"
    python2 setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir/" -O1 --skip-build

    # install license
    install -Dm644 LICENSE-pwntools.txt "$pkgdir/usr/share/licenses/$pkgname/license-pwntools.txt"
   
    # fix conflict bins
    mv "$pkgdir/usr/bin/checksec" "$pkgdir/usr/bin/pwntools-checksec"
    mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/pwntools-disasm"
    mv "$pkgdir/usr/bin/errno" "$pkgdir/usr/bin/pwntools-errno"

} 
