# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=mrtparse
pkgname=${_pkgname}-git
pkgver=r468.1549311
pkgrel=1
groups=('archstrike')
pkgdesc="a module to read and analyze the MRT format data."
arch=('any')
url='https://github.com/YoshiyukiYamauchi/mrtparse'
license=('APACHE')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
replaces=("${_pkgname}")
depends=('python2')
makedepends=('git')
source=("${pkgname}::git+https://github.com/YoshiyukiYamauchi/mrtparse.git")
sha512sums=('SKIP')

build() {
  cd ${pkgname}
  sed -i 's|env python$|env python2|' *.py
  python2 setup.py build
}

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname}
  python2 setup.py install --root="$pkgdir/" -O1 --skip-build
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|python$|python2|g'
}
