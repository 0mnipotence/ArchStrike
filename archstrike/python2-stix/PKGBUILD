# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-stix
pkgver=1.2.0.6
pkgrel=1
pkgdesc="An API for parsing and generating STIX content."
url="http://stix.mitre.org"
depends=('python2' 'python2-mixbox' 'python2-ordered-set' 'python2-lxml' 'python2-dateutil' 'python2-cybox' 'python2-weakrefmethod')
makedepends=('python2-setuptools')
license=('custom')
arch=('any')
source=("https://github.com/STIXProject/python-stix/archive/v${pkgver}.tar.gz")
sha512sums=('c7a656e12fb3c8e6223904d5c66b8c4916197497f70635eb7375ba8f73a3b6787559a895a1cad932950691d73e04e92afed850495f38bd0ec5d1ba6f31b10ef0')

build() {
  cd python-stix-"${pkgver}"
  python2 setup.py build
}

package() {
  cd python-stix-"${pkgver}"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm0644 LICENSE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE.txt
  sed -i 's|python$|python2|' $pkgdir/usr/lib/python2.7/site-packages/stix/bindings/ttp.py
}
