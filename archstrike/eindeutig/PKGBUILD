# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=eindeutig
pkgver=20050628_1
pkgrel=4
groups=('archstrike' 'archstrike-forensics')
pkgdesc="Examine the contents of Outlook Express DBX email repository files (forensic purposes)"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.jonesdykstra.com/"
license=('GPL')
source=("http://downloads.sourceforge.net/project/fast/Eindeutig/Eindeutig%20v20050628_1/eindeutig_${pkgver}.zip")
sha512sums=('1263acb7d67514821f3186f47da9796e043fe8334369675078024435739dba3838ffdf0a75cb5e1d97119960e43f61e5a0a7ab86f2f724e5240ac4277114a8e4')

prepare(){
  cd "${srcdir}/eindeutig_${pkgver}/src" || exit
  mv Eindeutig.c eindeutig.c
}

build() {
  cd "${srcdir}/eindeutig_${pkgver}/src" || exit
  make
}

package() {
  cd "${srcdir}/eindeutig_${pkgver}/src" || exit

  # Base directories.
  install -dm755 "${pkgdir}/usr/bin/"

  # Bin.
  # Renaming to dbxparse as the usage is for dbxparse not eindeutig.
  install -m755 ../bin/eindeutig "${pkgdir}/usr/bin/dbxparse"
}
