# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Ari Mizrahi <codemunchies@debugsecurity.com>

buildarch=220

pkgname=ubertooth
pkgver=2017.03.R2
pkgrel=1
groups=('archstrike' 'archstrike-bluetooth')
pkgdesc='A 2.4 GHz wireless development board suitable for Bluetooth experimentation. Open source hardware and software. Tools only'
url='https://greatscottgadgets.com/ubertoothone'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL')
makedepends=('cmake' 'libpcap')
depends=('libusbx' 'libbtbb' 'bluez-libs' 'python-numpy' 'python-pyusb' 'python-pyside' 'libpcap')
source=(
  "https://github.com/greatscottgadgets/$pkgname/archive/${pkgver//./-}.tar.gz"
  'specan_ui.patch'
)
sha512sums=(
  '2684e7d70e5008eeb37403077d8a6eae80d13ee963e91d70deba9dd9b46f980aeeeba2f9a54dcbdfbfcc472cbd01260799d49ae802abcd451977b00b182e5770'
  'c89cf605005d1fe5030d7e05f3cc9899294065699907b93d64c1adfa3b21d6ef337baa153f01b7ed4e208f3fd767554fe294610d36562e46bb7434f87c4ef47d'
)

prepare(){
  cd $pkgname-${pkgver//./-}/host/python/specan_ui
  patch CMakeLists.txt < "$srcdir/specan_ui.patch"
}

build() {
  cd $pkgname-${pkgver//./-}/host
  install -dm755 build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" -DPYTHON_EXECUTABLE=/usr/bin/python3 ..
  make
}

package() {
  cd $pkgname-${pkgver//./-}/host/build
  make clock_debug=true install
}
