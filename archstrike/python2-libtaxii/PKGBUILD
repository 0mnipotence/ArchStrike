# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-libtaxii
pkgver=1.1.111
pkgrel=1
pkgdesc="TAXII Library."
url="https://github.com/TAXIIProject/libtaxii"
depends=('python2-lxml' 'python2-dateutil')
makedepends=('python2-setuptools')
license=('custom')
arch=('any')
source=("https://github.com/TAXIIProject/libtaxii/archive/$pkgver.tar.gz")
sha512sums=('f638317200bb0691c3f994a4d624295bb3b64f4aba249e5b04d5b831eb985550702ef1c7653ca41fd8bb3972cab1c9d524ec540f87bf8581a0c0799ee5a7f831')

build() {
  cd libtaxii-"${pkgver}"
  python2 setup.py build
}

package() {
  cd libtaxii-"${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm0644 LICENSE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE.txt
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
