# Maintainer: ArchStrike <team archstrike org>

buildarch=1

pkgname=pwntools
pkgver=3.0.1
pkgrel=2
pkgdesc='CTF framework and exploit development library'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT')
groups=('archstrike' 'archstrike-exploits' 'archstrike-misc')
depends_x86_64+=('lib32-glibc')
depends=('python2-dateutil' 'python2-paramiko' 'python2-mako' 'python2-pyelftools' 'capstone' 'ropgadget' 'python2-pyserial' 
         'python2-requests' 'python2-pygments' 'python2-pysocks' 'python2-tox' 'python2-pip' 'python2-psutil')
makedepends=('python2-setuptools')
replaces=('binjitsu-git')
provides=('binjitsu-git')
source=("https://github.com/Gallopsled/pwntools/archive/$pkgver.tar.gz")
sha512sums=('e8780f47a2cad184394f0d59ef892b18608918a41405c0b6d000e7f86ca575f983b26720c03b71127547b2387bf1349b5ad41eabc564d4a8adaec79594d478d9')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i 's|python|python2|' pwnlib/elf/datatypes.py
}

build(){
    cd "$pkgname-$pkgver"
    python2 setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir/" --optimize=1

    # install license
    install -Dm644 LICENSE-pwntools.txt "$pkgdir/usr/share/licenses/$pkgname/license-pwntools.txt"
   
    # fix conflict bins
    mv "$pkgdir/usr/bin/checksec" "$pkgdir/usr/bin/binjitsu-checksec"
    mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/binjitsu-disasm"
    mv "$pkgdir/usr/bin/errno" "$pkgdir/usr/bin/binjitsu-errno"

} 
