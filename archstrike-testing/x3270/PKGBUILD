# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: tuftedocelot <tuftedocelot@fastmail.fm>
# Contributor: Davbo <dave@davbo.org>

buildarch=220

pkgname=x3270
pkgver=3.6ga5
pkgrel=1
pkgdesc="An IBM 3270 terminal emulator for the X Window System patched for birp(https://github.com/sensepost/birp)"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://x3270.bgp.nu/"
license=('BSD' 'MIT')
depends=('openssl' 'libxaw' 'tcl' 'xorg-mkfontdir')
makedepends=('xorg-xset' 'xorg-bdftopcf' 'fontconfig' 'xorg-mkfontdir' 'imake' 'openssl' 'libx11' 'libxaw' 'libxt' 'xbitmaps')
backup=(etc/x3270/ibm_hosts)
install=x3270.install
source=("http://downloads.sourceforge.net/sourceforge/x3270/suite3270-$pkgver-src.tgz"
        "https://raw.githubusercontent.com/sensepost/birp/master/suite3270-full.patch")
sha512sums=('33c36c2cd091cde8d28a8bd1b4d7bd9481856854c1353ad068ffbbcfa6661002e9d7f588131bd1ce4f321208ba17b659c26a1dd32fa7a6fae8ae4a81f89ad631'
            '2826366438d84c125516b2646991911df39492c651be62782c5d8e9bb879d900dfd69ed4e691577d7b2cf1b1ec4d13022b375358b0c52c2802639f3b9dae6c2e')

prepare(){
   cd $srcdir/${pkgname/x/suite}-${pkgver:0:3}
   patch -p1 -i "$srcdir/suite3270-full.patch"
}

build() {
   cd $srcdir/${pkgname/x/suite}-${pkgver:0:3}
   export CFLAGS+=" -std=gnu89"
   ./configure --prefix=/usr --bindir=/usr/bin --sysconfdir=/etc --with-fontdir=/usr/share/fonts/3270
   make
 }
 package() {
   cd $srcdir/${pkgname/x/suite}-${pkgver:0:3}
   install -dm755 $pkgdir/usr/share/fonts/3270/
   install -dm755 $pkgdir/usr/share/applications
   install -dm755 $pkgdir/usr/share/pixmaps
   install -dm755 $pkgdir/usr/share/licenses
   install -dm755 $pkgdir/usr/share/licenses/x3270
   for FILE in $(find . -iname license); do
      _FILE=${FILE/.\//}
      install -dm755 $pkgdir/usr/share/licenses/x3270/${_FILE/\/LICENSE/}
      install -m644 ${FILE} $pkgdir/usr/share/licenses/x3270/${_FILE}
   done
   install -m644 x3270/x3270-icon2.xpm $pkgdir/usr/share/pixmaps/

   make DESTDIR=$pkgdir install

   chmod 644 $pkgdir/etc/x3270/ibm_hosts
}
