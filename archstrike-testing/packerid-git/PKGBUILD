# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=packerid-git
pkgver=r10.024bae1
pkgrel=1
groups=('archstrike' 'archstrike-reverse')
pkgdesc="Script which uses a PEiD database to identify which packer (if any) is being used by a binary."
arch=('any')
license=('custom')
url="https://github.com/sooshie/packerid"
makedepends=('git')
provides=('packerid')
replaces=('packerid')
depends=('python2' 'python2-pefile' 'python2-capstone')
source=('packerid-git::git+https://github.com/sooshie/packerid.git')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  cd "$srcdir/$pkgname"
  sed -i 's|/usr/local/bin/python$|/usr/bin/python2|' *.py
  sed -i 's|/usr/local/etc/userdb.txt|/usr/share/pefile/UserDB.TXT|' packerid.py
}

package() {
  cd "$srcdir/$pkgname"

  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/packerid"

  cp -r --no-preserve=ownership * "$pkgdir/usr/share/packerid"

  chmod +x "$pkgdir/usr/share/packerid/packerid.py"

  ln -s "/usr/share/packerid/packerid.py" "$pkgdir/usr/bin/packerid"
}
