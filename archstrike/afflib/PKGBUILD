# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=afflib
pkgver=3.7.17
pkgrel=1
groups=('archstrike' 'archstrike-forensics')
pkgdesc='An extensible open format for the storage of disk images and related forensic information'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/sshock/AFFLIBv3/'
license=('custom')
install="$pkgname.install"
makedepends=('cython2')
depends=('fuse' 'openssl' 'expat' 'zlib' 'python2' 'curl' 'libtool' 'readline')
source=("https://github.com/sshock/AFFLIBv3/archive/v$pkgver.tar.gz")
sha512sums=('21067a8cb7c7b3d75f91883b2f9b1c978e055a2df6331dc68245332226d042a6b82f9f5d996e6bd1d5155de511827d5636c75ab1ee0a6957815aab3bb1cff49b')

build() {
  cd AFFLIBv3-$pkgver
  ./bootstrap.sh
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --enable-python=yes --enable-s3=yes
  make
}

package() {
  cd AFFLIBv3-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
