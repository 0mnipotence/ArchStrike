# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=recon-ng
pkgname=${_pkgname}-git
pkgver=20190222.r912
pkgrel=1
groups=('archstrike' 'archstrike-recon')
pkgdesc='A full-featured Web Reconnaissance framework written in Python'
arch=('any')
url='https://bitbucket.org/LaNMaSteR53/recon-ng'
license=('GPL3')
depends=('python2-unicodecsv' 'python2-dicttoxml' 'python2-pypdf2' 'python2-lxml'
         'python2-slowaes' 'python2-dnspython' 'python2-jsonrpclib-pelix'
		     'python2-xlsxwriter' 'python2-olefile')
makedepends=('git')
source=("${pkgname}::git+https://bitbucket.org/LaNMaSteR53/${_pkgname}.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd ${pkgname}

  while read -r; do
    sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' "$REPLY"
  done < <(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -R * "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  ln -sf /usr/share/${pkgname}/recon-cli "${pkgdir}/usr/bin/recon-cli"
  ln -sf /usr/share/${pkgname}/recon-ng "${pkgdir}/usr/bin/recon-ng"
  ln -sf /usr/share/${pkgname}/recon-rpc "${pkgdir}/usr/bin/recon-rpc"
  ln -sf /usr/share/${pkgname}/recon-web "${pkgdir}/usr/bin/recon-web"
}
