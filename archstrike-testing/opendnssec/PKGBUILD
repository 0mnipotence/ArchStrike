# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Javier Torres <javitonino [at] gmail [dot] com>

buildarch=220

pkgname=opendnssec
pkgver=2.1.4
pkgrel=1
pkgdesc="Turn-key solution for DNSSEC (sqlite3)"
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="http://www.opendnssec.org/"
license=('BSD')
groups=('archstrike')
depends=('libxml2' 'ldns' 'sqlite3' 'softhsm')
makedepends=('doxygen' 'dot2tex' 'automake' 'java-environment')
options=('!emptydirs')
backup=("etc/opendnssec/conf.xml"
        "etc/opendnssec/kasp.xml"
        "etc/opendnssec/zonelist.xml")
install="opendnssec.install"
source=("https://dist.opendnssec.org/source/$pkgname-$pkgver.tar.gz"
        "pid-path.patch"
        "ods-signer.service"
        "ods-enforcer.service"
        "opendnssec.tmpfiles")
sha512sums=('ac2f2b9615ff0ea73d1bdef8ea10dcabdbd139f19b1ee9043144988ca116dcbe32fcb1cc38114bd3e3b0cb1736ede2bfb96dd0545a5e2d0df2c6a5d7e1711723'
            'd6c764d26fbc1c72479b1a176dca3e5a0c5cfdcfef5650d7a018b0e961f65fc15b0661918f5c7ec9c1d671cde28539779e3cf4e6b6e375e0c91d6c80e056da6d'
            '28d868feedbee8251b0000556705ee094707782dad2072f4e37ebff3ac1658d09c884488dd6852e9734fa859218048daff50b66fe25930f6445619e3c89ca877'
            'a3700c82e6577bdacbce9cfd749e71e3c749814884ad4e9a1359e97105f9c045dc1472ba231ecb52c23855cacf67874623c8eef715955bfd41239b199d03a0db'
            'b221eec802ac471cd90908faa252d49ef2d217d6e48262a02490ae99fb64f3b82bc7f2dd2056d6e58b8bb1ca62072d52bffbc2bfd9f4eb794ec2d02275e88899')

build() 
{
  cd "${srcdir}/${pkgname}-${pkgver}"

  # /var/lib/run -> /var/run
  patch -p0 < "${srcdir}/pid-path.patch"

  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var/lib --sysconfdir=/etc --with-pkcs11-softhsm=/usr/lib/libsofthsm.so --sbindir=/usr/bin

  # Create the correct (/var/run/opendnssec) directory
  sed -i \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run\/opendnssec/#Removed/" \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run/\$(INSTALL) -d \$(DESTDIR)\$(OPENDNSSEC_PID_DIR)/" \
  Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm0644 "${srcdir}/ods-signer.service" \
        "${pkgdir}/usr/lib/systemd/system/ods-signer.service"
  install -Dm0644 "${srcdir}/ods-enforcer.service" \
        "${pkgdir}/usr/lib/systemd/system/ods-enforcer.service"
  install -Dm0644 "${srcdir}/opendnssec.tmpfiles" \
        "${pkgdir}/usr/lib/tmpfiles.d/opendnssec.conf"
  install -Dm0644 "LICENSE" \
        "${pkgdir}/usr/share/licenses/opendnssec/LICENSE"

  # Migration scripts for 2.0
  cp -r "enforcer/utils/1.4-2.0_db_convert/" "${pkgdir}/usr/share/opendnssec/"
  install -Dm0644 "enforcer/src/db/schema.mysql" "${pkgdir}/usr/share/opendnssec/1.4-2.0_db_convert/schema.mysql"
  install -Dm0644 "enforcer/src/db/schema.sqlite" "${pkgdir}/usr/share/opendnssec/1.4-2.0_db_convert/schema.sqlite"

  chown 227:227 "${pkgdir}/etc/opendnssec" -R
  chown 227:227 "${pkgdir}/var/lib/opendnssec" -R

  chmod 750 "${pkgdir}/etc/opendnssec" \
            "${pkgdir}/var/lib/opendnssec"
            
  rm "${pkgdir}/run" -R
}
