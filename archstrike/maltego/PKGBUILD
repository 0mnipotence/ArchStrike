# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=maltego
pkgver=4.2.3.12223
pkgrel=1
groups=('archstrike' 'archstrike-forensics' 'archstrike-recon' 'archstrike-scanners')
pkgdesc="An open source intelligence and forensics application, enabling to easily gather information about DNS, domains, IP addresses, websites, persons, etc."
arch=('any')
url='http://www.paterva.com/web5'
license=('GPL')
depends=('java-environment' 'giflib' 'python2')
provides=('casefile')
source=("https://www.paterva.com/malv423/Maltego.v4.2.3.12223.linux.zip"
        "https://www.paterva.com/web7/docs/M3GuideGUI.pdf")
sha512sums=('a1e77d440256aadca7f74c2b02534fe235fd19b8e10beb357a41c08b5d67e34b0d7e92694de95882989ae492efb99735faa634c57f86b13bea62799204589f61'
            '20dda5f7bbf211bb227ef740ae664037e33a0849b15a5e47f9ae4f39bd2b276ef116b492b9be57a44c89f3ffc23ee63d39c6f465136b917c6aeeb08dfe94c6e1')

prepare() {
  grep -iRl 'python' ${pkgname}_${pkgver} | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}_${pkgver}

  # Create base directories.
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 "$srcdir/M3GuideGUI.pdf" "${pkgdir}/usr/share/doc/${pkgname}/M3GuideGUI.pdf"

# Create joint script.
cat > ${pkgdir}/usr/bin/maltego <<EOF
#!/usr/bin/env bash
cd /usr/share/maltego/bin
./maltego "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/maltego"
}
