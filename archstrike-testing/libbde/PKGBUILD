# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname="libbde"
pkgver="20150905"
pkgrel=1
pkgdesc="Library and tools to access the BitLocker Drive Encryption (BDE) encrypted volumes"
arch=("i686" "x86_64" "armv6h" "armv7h")
url='https://github.com/libyal/libbde'
license=('GPL3')
depends=('fuse' 'libcsystem' 'libbfio' 'python2')
makedepends=('git')
install="$pkgname.install"
source=("https://github.com/libyal/libbde/archive/$pkgver.tar.gz")
sha512sums=('96b8a76cef7448c054f2eecfea7b6b7b86120046f4f77349b72f860092d6ff7d41fffa9f27444a8d8bbd8a7a30899469ebab4cbfb62a45d2de6a7e44db1f2fb3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./synclibs.sh
  ./autogen.sh
  PYTHON_VERSION="2" ./configure --prefix=/usr --disable-rpath --enable-verbose-output --enable-debug-output --enable-python
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
