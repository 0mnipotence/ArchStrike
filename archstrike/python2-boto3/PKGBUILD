#Maintainer: ArchStrike <team@arcstrike.org>

buildarch=1

pkgbase=python2-boto3

pkgname=(python2-boto3 python-boto3)
pkgver=1.5.22
pkgrel=1
pkgdesc="The AWS SDK for Python"
url="https://github.com/boto/boto3"
depends=('python2' 'python2-nose' 'python2-mock' 'python2-wheel' 'python2-unittest2')
makedepends=('python-setuptools' 'python2-setuptools')
license=('APACHE')
arch=('any')
source=("https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('b0812024534d9ce49443500d79a8aa1ec39207a2e896a8bb431c9e879385c1ddd8dc879513369028461dbd7a371e3db4e643cb5e25b6ab30558a5d4ecb0698ad')

prepare() {
  cp -a boto3-$pkgver{,-py2}
}

build() {
  cd "$srcdir/boto3-$pkgver"
  python3 setup.py build

  cd "$srcdir/boto3-$pkgver-py2"
  python2 setup.py build
}

package_python2-boto3() {
    depends=('python2')

    cd $srcdir/boto3-$pkgver-py2
    python2 setup.py install --root="$pkgdir" -O1 --skip-build
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package_python-boto3() {
    depends=('python')

    cd $srcdir/boto3-$pkgver
    python setup.py install --root="$pkgdir" -O1 --skip-build
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
