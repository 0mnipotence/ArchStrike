# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Filip Gralinski <filipg@amu.edu.pl>

buildarch=1

pkgname=apache-tika
pkgver=1.13
pkgrel=1
pkgdesc='Toolkit for detecting and extracting metadata and structured text content'
arch=('any')
license=('Apache')
url='https://tika.apache.org'
depends=('java-environment' 'bash')
noextract=("tika-server-$pkgver.jar")
source=(
  "http://central.maven.org/maven2/org/apache/tika/tika-server/$pkgver/tika-server-$pkgver.jar"
  "$pkgname.service"
)
sha512sums=('22eb60fa230e1bb8602d6d07b002688570e797122d00c97088f353234136ec64b04f1ad8f3d5f9b3982eec7bc41272cbe01815a992c6c74a892f001f83a85141'
            'e46522bfcc86ada8776c4f12cf645a81c34492b1c86e9bd1258d7fa248fb8fed9c2bd57e5971c5c9a4687f35626f872f63920649279cfaba5498a55c3331bcf5')

package() {
  install -dm755 "$pkgdir/usr/bin"
  install -Dm755 "tika-server-$pkgver.jar" "$pkgdir/usr/share/java/$pkgname/tika-server-$pkgver.jar"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"

cat > "$pkgdir/usr/bin/tika" <<EOF
#!/usr/bin/env bash
java -jar /usr/share/java/$pkgname/tika-server-$pkgver.jar "\$@"
EOF

  chmod 755 "$pkgdir/usr/bin/tika"
}
