# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=autosploit
pkgname=${_pkgname}-git
pkgver=r25.e03e7f2
pkgrel=2
groups=('archstrike' 'archstrike-exploit')
pkgdesc="Automate the exploitation of remote hosts"
arch=('any')
url='https://github.com/NullArray/AutoSploit'
license=('GPL3')
depends=('python2' 'shodan' 'python2-blessings' 'metasploit' 'sudo')
makedepends=('git')
source=("${pkgname}::git+${url}.git" 'service.patch')
sha512sums=('SKIP'
            'd47a0907443be9f7dd4304016bdcd032df96045742200e2580f4ae1ee13504dbc5a119b123817a916242e64260515de1e7c9da6c4a36df272a727126f210ac79')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "${srcdir}/${pkgname}" | xargs sed -i 's|python$|python2|g'

  # patch services for arch
  cd ${pkgname}
  patch -Np1 -i ${srcdir}/service.patch
}

package() {
  cd "${srcdir}/${pkgname}"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  install -Dm755 "${_pkgname}.py" "${pkgdir}/usr/share/${pkgname}/${_pkgname}.py"
  install -Dm755 "modules.txt" "${pkgdir}/usr/share/${pkgname}/modules.txt"

cat > "${pkgdir}/usr/bin/${_pkgname}" << EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
sudo python2 ${_pkgname}.py "\$@"
EOF

chmod 755 "${pkgdir}/usr/bin/${_pkgname}"
}
