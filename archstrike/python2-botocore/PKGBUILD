# Maintainer: ArchStrike <team[at]archstrike[dot]org
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Ainola
# Contributor: Chris Fordham

pkgname=python2-botocore
pkgver=1.5.8
pkgrel=1
pkgdesc='A low-level interface to a growing number of Amazon Web Services'
arch=('any')
url="https://github.com/boto/botocore"
license=('Apache')
depends=('python2-dateutil' 'python2-jmespath' 'python2-docutils')
makedepends=('python2-setuptools')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('d8412e15b7cca3a449992dca2e3661f065dca15ab891a86a3456705e0acf9c3fd898bcc84be1ed168dce7297d83d33b5015ce6c2e841947e5fe17e7f8618ab44')

build() {
  cd botocore-$pkgver
  python2 setup.py build
}

package() {
  cd botocore-$pkgver
  python2 setup.py install --root="$pkgdir" -O1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  grep -iRl 'python' "$pkgdir" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|' 
}
