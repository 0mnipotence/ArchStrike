# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=xsstrike
pkgver=3.1.4
pkgrel=1
pkgdesc='Most advanced XSS detection suite'
groups=('archstrike' 'archstrike-webapp' 'archstrike-scanner')
arch=('any')
url='https://github.com/s0md3v/XSStrike'
license=('MIT')
depends=('python' 'python-requests' 'python-fuzzywuzzy' 'python-tld')
source=("$url/archive/${pkgver}.tar.gz")
sha512sums=('37132b1ff3ccb21df0f9403c5cb6e1a5b32871546d3dbb2993494603cc840706ba87f785621ccae23e81fd6e7fdd23558708160e04affb36dda9c48a966e3838')

package() {
  cd "${srcdir}/XSStrike-${pkgver}" || exit
  install -dm 755 "${pkgdir}/usr/bin"
  install -dm 755 "${pkgdir}/usr/share/xsstrike"
  install -Dm644 -t "${pkgdir}/usr/share/doc/xsstrike/" README.md
  cp -a ./{core,xsstrike.py} "${pkgdir}/usr/share/xsstrike"

  cat > "${pkgdir}/usr/bin/xsstrike" << EOF
#!/usr/bin/env sh
cd /usr/share/${pkgname}
exec python ${pkgname}.py "\${@}"
EOF
  chmod a+x "${pkgdir}/usr/bin/xsstrike"
}
