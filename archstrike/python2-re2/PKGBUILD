# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=python2-re2
pkgver=0.2.24
pkgrel=1
pkgdesc="Python wrapper for Google's RE2 using Cython"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://pypi.python.org/pypi/re2/'
license=('BSD')
depends=('python2' 're2')
makedepends=('python2-setuptools')
source=(https://pypi.python.org/packages/source/r/re2/re2-$pkgver.tar.gz)
sha512sums=('b056cfbf4d227afb3fc83fa8ecbc16311a009d5c7132fcdf1eba827d76c1c3ef90545dbfa97cabec22a50b85720f7af9284e9317302adc130e1205ff1b78f0ea')

prepare(){
  grep -iRl 'python' "$srcdir/re2-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/re2-$pkgver"

  python2 setup.py install --root=$pkgdir -O1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
