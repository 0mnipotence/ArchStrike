# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=r2pipe
pkgbase=python-$_pkgname

buildarch=1

pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.8.0
pkgrel=1
pkgdesc="A Pipe interface for radare2"
url="https://pypi.python.org/pypi/r2pipe/"
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/r/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('b07b6baa9a1d706d0f8c7c2dc3afe5274a84bf16cfcedc6b7f90f79ec25e43dcd4a62ce631252fab43dd23bfcf8ea0ff3536030611e2b33f51c55634282ccf43')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py build

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py build
}

package_python-r2pipe() {
  depends=('python')

  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  sed -i 's|env python|env python2|' $pkgdir/usr/lib/python2.7/site-packages/r2pipe/__init__.py
}

