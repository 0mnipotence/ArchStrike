# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=3proxy
pkgver=0.8.4
pkgrel=1
groups=('archstrike' 'archstrike-misc')
pkgdesc='Tiny free proxy server'
url='http://3proxy.ru'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('glibc' 'openssl')
options=('!makeflags')
source=("https://github.com/z3APA3A/3proxy/archive/3proxy-$pkgver.tar.gz")
sha512sums=('0e016c89a0e19198e7bc6a93c6e69d61b79205b130028efa3acf7540d63fa7c3a3d717aae4c83559361081759b89fbf2cf9c344949d75959e9cb2162022538b2')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  sed -i 's|/usr/bin/install|/bin/install|g' Makefile.Linux
  sed -i 's|/usr/local|/usr|g' Makefile.Linux
  sed -i 's|-s ||g' Makefile.Linux
  sed -i "s|-Wall -g -O2|$CFLAGS|g" Makefile.Linux
  sed -i 's|\$(prefix)/etc/|/etc/|g' Makefile.Linux
}

build() {
  cd $pkgname-$pkgname-$pkgver
  make -f Makefile.Linux prefix=/usr
}

package() {
  cd $pkgname-$pkgname-$pkgver
  make -f Makefile.Linux install DESTDIR="$pkgdir"
  find "$pkgdir/etc/$pkgname" -type f | xargs chmod 644
  mv "$pkgdir/usr/bin/proxy" "$pkgdir/usr/bin/$pkgname"
}
