# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Javier Torres <javitonino [at] gmail [dot] com>

buildarch=220

pkgname=opendnssec
pkgver=2.1.3
pkgrel=1
pkgdesc="Turn-key solution for DNSSEC (sqlite3)"
arch=('i686' 'x86_64' 'armv7h' 'armv6h' 'aarch64')
url="http://www.opendnssec.org/"
license=('BSD')
groups=('archstrike')
depends=('libxml2' 'ldns' 'sqlite3' 'softhsm')
makedepends=('doxygen' 'dot2tex' 'automake' 'java-environment')
options=('!emptydirs')
backup=("etc/opendnssec/conf.xml"
        "etc/opendnssec/kasp.xml"
        "etc/opendnssec/zonelist.xml")
install="opendnssec.install"
source=("https://dist.opendnssec.org/source/$pkgname-$pkgver.tar.gz"
        "pid-path.patch"
        "ods-signer.service"
        "ods-enforcer.service")
sha512sums=('2d7dfb3a9d1d2256ba615378f9c1a04567e27b455bd76530fe6582eb58de66051ecbcb7a86ee0dcfa513d5affde41304243cbc5b9a6e3ecd98dd72a4b57fb060'
            '0e58eec6b07a8da0462dfc3292b5a10bd1e67bd894d4d8efd50b88a3089433d5bda9531017d1e1e1679c72186b83c654d87200d0ecf9b9064359204ca7e3f616'
            '28d868feedbee8251b0000556705ee094707782dad2072f4e37ebff3ac1658d09c884488dd6852e9734fa859218048daff50b66fe25930f6445619e3c89ca877'
            'a3700c82e6577bdacbce9cfd749e71e3c749814884ad4e9a1359e97105f9c045dc1472ba231ecb52c23855cacf67874623c8eef715955bfd41239b199d03a0db')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # /var/lib/run -> /var/run
  patch -p1 < "${srcdir}/pid-path.patch"
  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var/lib --sysconfdir=/etc --with-pkcs11-softhsm=/usr/lib/libsofthsm.so --sbindir=/usr/bin
  # Create the correct (/var/run/opendnssec) directory
  sed -i \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run\/opendnssec/#Removed/" \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run/\$(INSTALL) -d \$(DESTDIR)\$(OPENDNSSEC_PID_DIR)/" \
  Makefile
  CFLAGS='$CFLAGS -fuse-ld=gold' make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -dm755 "${pkgdir}/var/run/opendnssec"
  install -dm755 "${pkgdir}/var/lib/opendnssec"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/opendnssec/LICENSE
  install -Dm644 "${srcdir}/ods-signer.service" \
        "${pkgdir}/usr/lib/systemd/system/ods-signer.service"
  install -Dm644 "${srcdir}/ods-enforcer.service" \
        "${pkgdir}/usr/lib/systemd/system/ods-enforcer.service"
  chmod 644 ${pkgdir}/etc/opendnssec/{conf.xml.sample,conf.xml}
}
