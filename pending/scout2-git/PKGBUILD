# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=scout2-git
pkgver=20181122.r1182
pkgrel=1
pkgdesc="Security auditing tool for AWS environments"
url="https://nccgroup.github.io/Scout2/"
arch=('any')
license=('GPL2')
groups=('archstrike')
depends=('python2-boto3' 'python2-dateutil' 'python2-netaddr' 'python2-requests'
         'python2-botocore' 'python2-opinel' 'python2-pyaml' 'python2-iampoliciesgonewild')
makedepends=('git' 'python2-setuptools')
source=(${pkgname}::git+https://github.com/nccgroup/Scout2.git)
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" 
}

prepare() {
  cd "${pkgname}"
  git submodule init
  git submodule update
}

package() {
  cd "${pkgname}"
  sed -i 's/>=.*//' AWSScout2/data/requirements.txt
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
