# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Diogo Leal <estranho@diogoleal.com>

buildarch=1

pkgname=exabgp
pkgver=3.4.15
pkgrel=1
pkgdesc='The BGP swiss army knife of networking'
arch=('any')
license=('custom')
groups=('archstrike' 'archstrike-networking')
url='https://github.com/Exa-Networks/exabgp'
depends=('python2-ipaddr')
backup=('etc/exabgp.conf')
makedepends=('python2-setuptools')
source=("https://github.com/Exa-Networks/${pkgname}/archive/${pkgver}.zip"
        'exabgp.service')
sha512sums=('1ac9408496dc7164e3a38491007fd72a323bd29df5e494af688e353e30e9b548a56163caf884f7505a59e57a4b3b010c75b4802192ae66cf3c27ba36f15eccbf'
            '225726cf424d9c2a23a2c0e9d3c7ed8c5cf03993f9518efd8e45079822b2d8cbc4d6fd18b49794beee522530478b57e5e3b268b3abd92442680cdd695504360d')

build() {
    # arch python2
    find $pkgname-$pkgver -type f -exec \
        sed -i '1s,^#!/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \; -print
}

package() {
    pushd $pkgname-$pkgver
    # upstream installer
    python2 setup.py install --root="$pkgdir" --install-data=/
    # install license
    install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    popd
    # systemd
    install -D -m 644 $pkgname.service \
        "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    # install empty config file
    install -D -m 644 /dev/null "$pkgdir/etc/$pkgname.conf"
    # move data in a more private directory
    mv "$pkgdir/etc/$pkgname" "$pkgdir/usr/share/$pkgname"
}
