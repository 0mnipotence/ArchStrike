# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=1

pkgname=pyew-git
pkgver=20140827.r100
pkgrel=1
groups=('archstrike' 'archstrike-malware')
pkgdesc="a (command line) python tool to analyse malware."
arch=('any')
url="https://github.com/joxeankoret/pyew"
license=('GPL2')
depends=('distorm')
makedepends=('git')
conflicts=('pyew')
provides=('pyew')
source=("${pkgname}::git+https://github.com/joxeankoret/pyew.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci VERSION_3X | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare(){
    cd $pkgname

    # Fix python invocation
    find . -iname "*.py" -exec \
        sed -i -E 's|\ */usr/bin/env python|/usr/bin/env python2|g;s|\ */usr/(local/)?bin/python(2\.4)?|/usr/bin/env python2|g' '{}' \;
}

package(){
    cd $pkgname

    install -Dm755 pyew "${pkgdir}"/usr/share/${pkgname}/pyew
    for i in *.py; do
        install -Dm755 $i "${pkgdir}"/usr/share/${pkgname}/${i}
    done
    for i in Elf anal contrib logo plugins scripts; do
        install -dm755 "${pkgdir}"/usr/share/${pkgname}/${i}
        cp -a --no-preserve=ownership ${i}/* "${pkgdir}"/usr/share/${pkgname}/${i}/
    done

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    install -dm755 "${pkgdir}"/usr/bin
    echo -e '#!/usr/bin/env bash\n\ncd /usr/share/pyew/\n./pyew.py "$@"' > "${pkgdir}/usr/bin/pyew"
    chmod 755 "${pkgdir}/usr/bin/pyew"
}
