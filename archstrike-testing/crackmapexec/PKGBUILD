# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=crackmapexec
_pkgname=CrackMapExec
pkgver=1.0.8
pkgrel=1
groups=('archstrike' 'archstrike-exploits')
pkgdesc=" A swiss army knife for pentesting Windows/Active Directory environments"
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('GPL3')
depends=('impacket' 'python2-gevent' 'python2-netaddr' 'python2-crypto' 'python2-pyasn1' 'python2-termcolor')
source=("https://github.com/byt3bl33d3r/CrackMapExec/archive/v$pkgver.tar.gz")
sha512sums=('1f1ddf0f69c94b5e1cf65eef88f181083b023ac5a74dd9ec52e32dfff01eb5e36f883335448eecf5b5ef9dc7d95260b5db0f37cbac8e98483bbeca95a2a2247c')

prepare(){
  cd "${_pkgname}-${pkgver}"
  grep -iRl 'python' "$srcdir/${_pkgname}-${pkgver}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "${_pkgname}-${pkgver}"

  install -dm755 "$pkgdir/usr/share/${pkgname}/"
  install -dm755 "$pkgdir/usr/bin/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/${pkgname}/"

  cd ${pkgdir}
  find ./ -type d -exec chmod 755 {} \+
  find ./ -type f -exec chmod 644 {} \+

cat > "${pkgdir}/usr/bin/$pkgname" <<EOF
#!/bin/sh
cd /usr/share/$pkgname
python2 ${pkgname}.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/$pkgname"
}
