#Maintainer:  ArchStrike <team@archstrike.org>

groups=('archstrike' 'archstrike-crackers')
pkgname='oclhashcat'
_pkgname='oclHashcat'
pkgver=2.01
pkgrel=1
pkgdesc='Worlds fastest WPA cracker with dictionary mutation engine'
url='http://hashcat.net/oclhashcat/'
arch=('i686' 'x86_64')
license=('custom')
depends=('bash' 'libcl' 'catalyst-utils>=15.7')
source=("http://hashcat.net/files/$_pkgname-$pkgver.7z")
sha512sums=('068f900aad1fbed9cbb767d1fdff5a41f98c60c3805c13121fe803e2aa461a8411f4904b5f1716a3dc82b9db4b08258a69a07d3f8aac087ad28b43cc2319e0e1')
options=('!strip')

package() {
  # Copy files
  install -dm755 "$pkgdir/opt/$pkgname"
  cp -a --no-preserve=ownership  $_pkgname-$pkgver/* "$pkgdir/opt/$pkgname/"
  cd "$pkgdir/opt/$pkgname"

  # Set up bash pivot
  install -dm755 "$pkgdir/usr/bin"
  if [ "$CARCH" = "x86_64" ]; then
    rm ${_pkgname}32.bin
    echo -e "#!/bin/bash\n/opt/$pkgname/${_pkgname}64.bin \$@" > "$pkgdir/usr/bin/$_pkgname"
  else
    rm ${_pkgname}64.bin
    echo -e "#!/bin/bash\n/opt/$pkgname/${_pkgname}32.bin \$@" > "$pkgdir/usr/bin/$_pkgname"
  fi
  chmod a+x "$pkgdir/usr/bin/$_pkgname"

  # Get rid of windows stuff
  find . \( -name "*.cmd" -o -name "*.exe" \) -exec rm '{}' \;

  # Cleanup permissions
  find "$pkgdir" -iname '*.txt' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.llvmir' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.hash' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.sh' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.rule' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.hcmask' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.hcstat' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.dict' -exec chmod 644 '{}' \;
  find "$pkgdir" -iname '*.bin' -exec chmod 755 '{}' \;
}
