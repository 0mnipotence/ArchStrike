# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=arch-audit
pkgver=0.1.5
pkgrel=1
groups=('archstrike' 'archstrike-misc')
arch=('x86_64' 'i686')
pkgdesc='pkg-audit-like utility for Arch Linux'
url="https://github.com/ilpianista/arch-audit"
license=('MIT')
depends=('curl')
depends_x86_64+=('gcc-libs-multilib')
makedepends=('cargo')
source=("https://github.com/ilpianista/arch-audit/archive/${pkgver}.tar.gz" "arch-audit.service" "arch-audit.timer")
sha512sums=('c06a1c1d7bd4a025b76f8b60bb61308f1d30040b66366ea29c38c99b1c99b03be95382b55ebc5d817611d5a6fcfb7d96f5197e459704e212021e4adb65436e33'
            '817183379b8bc7ee6d7935a046ad48ee5c7316b1c041c17ba06ba500a79fdf3c80baa1e4795f7df91652408424741d848bb9d0ea27d7577cd32fc7bc12f26c76'
            'a7f76ee5ef51c704decc85a7839453188eb308964b82d96bde7d8ee182884941d0b2e20d69d05a1aa6c12bea08a2d7f30fdf56c135ceefc42e91672f28fc2903')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -dm755 "${pkgdir}/usr/bin/"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"

  install -Dm755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 ${srcdir}/arch-audit.service "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 ${srcdir}/arch-audit.timer "${pkgdir}/usr/share/${pkgname}"
}
