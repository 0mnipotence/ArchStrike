# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=knock-git
pkgver=20180502.r274
pkgrel=1
groups=('archstrike' 'archstrike-dns' 'archstrike-scanners' 'archstrike-bruteforce' 'archstrike-intel' 'archstrike-info' 'archstrike-enumeration')
arch=('any')
pkgdesc="python tool designed to enumerate subdomains on a target domain through a wordlist"
url="https://github.com/guelfoweb/knock"
license=('custom')
depends=('python2' 'python2-dnspython')
makedepends=('git' 'python2-setuptools')
source=("${pkgname}::git+${url}.git")
install="${pkgname}.install"
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
# weird branch name but such is life
  printf "%s.r%s" "$(git show -s --format=%ci 4.1 | sed "s/\ .*//g;s/-//g")" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl python . | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}
  python2 setup.py install -O1 --root="${pkgdir}"
}
