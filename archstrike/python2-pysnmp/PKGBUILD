# Maintainer: ArchStrike <team@archstrike.org>
_pkgname=pysnmp

buildarch=1

pkgname=python2-$_pkgname
pkgver=4.4.6
pkgrel=1
pkgdesc="SNMP library for Python"
url='https://github.com/etingof/pysnmp'
license=('BSD')
arch=('any')
depends=("python2-pyasn1" "python2-pycryptodomex")
makedepends=("python2-setuptools")
optdepends=("python2-pysnmp-mibs")
conflicts=('pysnmp')
provides=('pysnmp')
source=(https://github.com/etingof/pysnmp/archive/v${pkgver}.tar.gz)
sha512sums=('de18de7500c0283885b01f280bcc3f20318b806fd28fd574351edfba3ddba4b95ddde0f59d55a743047cc4c16d2a0b60187339097c428ff21f3cc4f96c24a6b3')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/python2-pysnmp/LICENSE.rst
  # Package python module only.
  rm -fr "$pkgdir"/usr/bin
}
