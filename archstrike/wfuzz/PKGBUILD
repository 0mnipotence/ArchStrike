# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=wfuzz
pkgver=2.2.3
pkgrel=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-fuzzers')
pkgdesc="Utility to fuzz web applications to find their not linked resources"
url="https://github.com/xmendez/wfuzz"
arch=('any')
license=('GPL')
depends=('python2-pycurl')
source=("https://github.com/xmendez/wfuzz/archive/v${pkgver}.tar.gz")
sha512sums=('0c5c7d4f83f65072d99af5c7c41abf36ee5966d2be68860fab820668d7d1374e46155ba0b2c2c3357d75923c9b7ac0afab710dde85de699dcc2f9605dcbbeb20')

prepare() {
  grep -iRl 'python' ${pkgname}-${pkgver} | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}-${pkgver}
  install -dm755 ${pkgdir}/usr/share/${pkgname}/
  install -dm755 ${pkgdir}/usr/bin
  cp -a --no-preserve=ownership * ${pkgdir}/usr/share/${pkgname}/
  # fix python issues
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
python2 ${pkgname}-cli.py "\$@"
EOF
  chmod 755 ${pkgdir}/usr/bin/${pkgname}
}
