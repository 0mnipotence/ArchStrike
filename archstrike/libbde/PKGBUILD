# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname="libbde"
pkgver=20190701
pkgrel=2
pkgdesc="Library and tools to access the BitLocker Drive Encryption (BDE) encrypted volumes"
arch=("i686" "x86_64" "armv6h" "armv7h" 'aarch64')
url='https://github.com/libyal/libbde'
license=('GPL3')
depends=('fuse' 'libcsystem' 'libbfio' 'python')
install="$pkgname.install"
source=("https://github.com/libyal/libbde/releases/download/${pkgver}/libbde-alpha-${pkgver}.tar.gz")
sha512sums=('0777bcf78a29e2d08c3c5dfc048b851fe625a99947dc0cbebee032be608d9c58ff8036f45665c6c31f28068f640ab8d62ca1d844a617bbc27427b123e6bc6fd8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON_VERSION="3" ./configure --prefix=/usr --disable-rpath --enable-verbose-output --enable-debug-output --enable-python
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
