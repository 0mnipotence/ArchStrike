# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="Bluto"
pkgname="bluto"
pkgver=2.01
pkgrel=1
pkgdesc="Recon, Subdomain Bruting, Zone Transfers"
arch=('any')
groups=('archstrike' 'archstrike-recon' 'archstrike-dns')
url="https://github.com/darryllane/Bluto"
license=("GPL3")
depends=("python2-termcolor" "python2-lxml" "python2-beautifulsoup4" "python2-requests" "pythonwhois" "python2-dnspython")
makedepends=("python2-setuptools")
source=("https://github.com/darryllane/Bluto/archive/${pkgver}.tar.gz")
sha512sums=('a0fa51ed58735e78b134b8949dd9dd6e77edf77cf31d03eba49f8b323f81b7e5a74f7affc72f86a3276fa238bf5a1cc39aa8c90f1c6cd71c259c9ab633325d1c')

prepare() {
  grep -iRl 'python' ${_pkgname}-${pkgver}/${_pkgname}/modules | xargs sed -i 's|python$|python2|'
}

build() {
  cd "${_pkgname}-${pkgver}/"
  python2 setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}/"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  # fixing python
  sed -i 's|python|python2|' $pkgdir/usr/lib/python2.7/site-packages/Bluto/bluto
}
