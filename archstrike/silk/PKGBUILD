# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: William Robertson <nullptr@gmail.com>

buildarch=220

pkgname=silk
pkgver=3.18.2
pkgrel=1
pkgdesc='CERT netflow tools.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'armv8' 'aarch64')
url='http://tools.netsa.cert.org/silk'
license=('GPL')
groups=('archstrike' 'archstrike-networking')
depends=('libfixbuf' 'lzo' 'libpcap' 'python' 'gnutls' 'c-ares' 'adns' 'snappy')
makedepends=('perl' 'flex' 'bison')
install="$pkgname.install"
source=("http://tools.netsa.cert.org/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('f285ad1409b377f6da2b789362f9d70b66e5233b6f74d092860fb2f691a9f727c789d200cd2f3d577b4603c1821930c703064e2e3f6efe1f6d634aad8b65a307')

build() {
  cd "${pkgname}-${pkgver}"
  export LIBS="-lpthread"
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6 --with-python=$(which python) --with-pcap \
  --with-pic --with-libfixbuf --with-gnutls --enable-output-compression --enable-localtime --enable-data-rootdir=/usr/share/$pkgname
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
