# Maintainer: ArchStrike <team@archstrike.org>
_pkgname=pysnmp

buildarch=1

pkgname=python2-$_pkgname
pkgver=4.4.4
pkgrel=1
pkgdesc="SNMP library for Python"
url='https://github.com/etingof/pysnmp'
license=('BSD')
arch=('any')
depends=("python2-pyasn1" "python2-pycryptodomex")
makedepends=("python2-setuptools")
optdepends=("python2-pysnmp-mibs")
conflicts=('pysnmp')
provides=('pysnmp')
source=(https://github.com/etingof/pysnmp/archive/v${pkgver}.tar.gz)
sha512sums=('8ac888dc61c986407e0f5f750237a7268d7d83b37a2830c2942324d9f5e33c69fc6aadb8c975bd01fc42b9642b6623d26a9a48555a18df94f0e87595dd7c6774')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/python2-pysnmp/LICENSE.txt
  # Package python module only.
  rm -fr "$pkgdir"/usr/bin
}
