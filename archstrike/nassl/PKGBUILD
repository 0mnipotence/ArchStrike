# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=nassl
pkgver=0.14.0
pkgrel=1
groups=('archstrike' 'archstrike-misc' 'archstrike-crypto')
pkgdesc='Experimental Python wrapper for OpenSSL'
arch=('i686' 'x86_64')
url='https://github.com/nabla-c0d3/nassl'
license=('GPL')
depends=('python2')
makedepends=('openssl' 'python2-setuptools')
source=("https://github.com/nabla-c0d3/nassl/archive/$pkgver.tar.gz")
sha512sums=('55b21deefd93e7ba77ee86fdb10130c997f7d1fd5fa8291a224e20166e9613def4410154296265347da8bc0ba6c825d8ac5935384792d7223e39193c24f46abc')

prepare() {
  find . -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build_ext -i
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
