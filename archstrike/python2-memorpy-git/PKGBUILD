# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-memorpy-git
pkgver=20170523.r63
pkgrel=1
arch=('any')
pkgdesc="Python library using ctypes to search/edit windows/linux/OSX/SunOS programs memory."
url="https://github.com/n1nj4sec/memorpy"
license=('GPL3')
depends=('python2')
makedepends=('python2-setuptools' 'git')
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed "s/\ .*//g;s/-//g")" "$(git rev-list --count HEAD)"
}

prepare() {
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

build() {
  cd ${pkgname}
  python2 setup.py build
}

package() {
  cd ${pkgname}
  python2 setup.py install --root=${pkgdir} -O1 --skip-build
}
