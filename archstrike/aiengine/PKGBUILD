# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=aiengine
pkgver=1.7.0
pkgrel=2
groups=('archstrike' 'archstrike-networking')
pkgdesc="A packet inspection engine with capabilities of learning without any human intervention."
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://bitbucket.org/camp0/aiengine"
license=('GPL3')
if [[ ${CARCH} == 'x86_64' ]]; then
     makedepends=('boost' 'boost-bloom-filters-git' 'log4cxx-svn' 'python2' 'gcc-libs-multilib')
else
     makedepends=('boost' 'boost-bloom-filters-git' 'log4cxx-svn' 'python2' 'gcc-libs')
fi
depends=('libpcap' 'boost-libs' 'python2' 'log4cxx-svn' 'pcre')
source=("https://bitbucket.org/camp0/aiengine/downloads/aiengine-${pkgver}.tar.gz")
sha512sums=('f708e2dc542bb485e4853deb269fc77285f74b3d904be65c989d8c52175d42b0eecf43aa4c95612825d976fdd443f88f0df77ba803c8ae3d9c66dbb25095b794')

prepare(){
   grep -iRl 'python' "${pkgname}-${pkgver}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
  export CXXFLAGS="-llog4cxx"
  export PYTHON=/usr/bin/python2.7
  ./configure --prefix=/usr --enable-adaptor --enable-bloomfilter --enable-tcpqos \
   --enable-reject --enable-fscompress --enable-pythongil --with-pic --with-boost-python --with-pcre
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
