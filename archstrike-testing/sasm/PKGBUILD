# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=sasm
pkgver=3.4.0
pkgrel=1
pkgdesc="A simple crossplatform IDE for NASM, MASM, GAS and FASM assembly languages"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/Dman95/SASM"
license=('custom')
depends=('nasm' 'gdb' 'qt5-base')
makedepends=('qt4>=4.8')
sha512sums=('43b0bf84510b196043b025a7a0e209f2a6b069bd9fd9eb19b0aa85d7c9a879c18a204e0ce9074b2d83dacec2b64a4bac86277c8c8220afecb2962a24e925e7f8')
source=("https://github.com/Dman95/SASM/archive/v$pkgver.tar.gz")

build() {
  cd "$srcdir/SASM-$pkgver"
  qmake QMAKE_CXXFLAGS="$CFLAGS -Wa,--noexecstack -Wl,-z,noexecstack -W -D_REENTRANT -fPIC" QMAKE_LFLAGS="-Wl,-z,noexecstack"
  make CXXFLAGS="$CXXFLAGS -Wa,--noexecstack -W -D_REENTRANT -fPIC" LDFLAGS="-Wl,-z,noexecstack"
}

package() {
  cd "$srcdir/SASM-$pkgver"
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/sasm"
  install -dm755 "$pkgdir/usr/share/doc/sasm"
  install -dm755 "$pkgdir/usr/share/sasm/NASM"
  install -dm755 "$pkgdir/usr/share/sasm/Projects"
  install -dm755 "$pkgdir/usr/share/sasm/include"

  # Bins
  install -Dm755 Linux/bin/fasm "$pkgdir/usr/bin/fasm"
  install -Dm755 Linux/bin/listing "$pkgdir/usr/bin/listing"
  install -Dm755 sasm "$pkgdir/usr/bin/sasm"

  # Docs & License
  install -Dm644 Linux/share/doc/sasm/* "$pkgdir/usr/share/doc/sasm/"
  install -Dm644 Linux/share/doc/sasm/copyright "$pkgdir/usr/share/licenses/$pkgname/copyright"

  # Share dir
  install -Dm644 Linux/share/sasm/keys.ini "$pkgdir/usr/share/sasm/"
  install -Dm644 Linux/share/sasm/sasm.png "$pkgdir/usr/share/sasm/"
  install -Dm644 Linux/share/sasm/NASM/* "$pkgdir/usr/share/sasm/NASM/"
  install -Dm644 Linux/share/sasm/Projects/* "$pkgdir/usr/share/sasm/Projects/"
  install -Dm644 Linux/share/sasm/include/* "$pkgdir/usr/share/sasm/include/"

}

# vim:set ts=2 sw=2 et:
