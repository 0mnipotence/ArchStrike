# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=r2pipe
pkgbase=python-$_pkgname

buildarch=1

pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.8.4
pkgrel=1
pkgdesc="A Pipe interface for radare2"
url="https://pypi.python.org/pypi/r2pipe/"
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/82/e2/262d935510ec59c79b00ea53d1acb2357019618e79789c3a8654978ba7d2/${_pkgname}-${pkgver}.tar.gz" "print_statement.patch")
sha512sums=('da02f9d82f8127e6314bcbc9d110e2719b929ccfd0aeeb48779773e744533a95cd29fc0aa65f4229b5f00bde3a606142915078e7d310372fb59f2d3294f8666b'
            '5eedee19ab38500bbfaee8a25284d519ed166a227d44c8a55e0d2b1ac45b96b938bedb9d2f44e025bde7306194aade7828edbab10b30e0fb15d09b9559131b23')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
  cd $_pkgname-$pkgver
  patch -Np1 -i $srcdir/print_statement.patch
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py build

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py build
}

package_python-r2pipe() {
  depends=('python')

  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  sed -i 's|env python|env python2|' $pkgdir/usr/lib/python2.7/site-packages/r2pipe/__init__.py
}

