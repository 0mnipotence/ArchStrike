# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname='perl-text-csv-xs'
pkgver=1.41
pkgrel=1
pkgdesc="comma-separated values manipulation routines"
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='https://metacpan.org/pod/Text::CSV_XS'
source=("http://search.cpan.org/CPAN/authors/id/H/HM/HMBRAND/Text-CSV_XS-${pkgver}.tgz")
sha512sums=('a518a1cfe328c8fb3c439bde5137c4cda6081f6ab2d87fe9a8b2943d05f51705c5dd3ab2500b9f09484418d82577cb6a6e60b1bbe1747a177d2158f7d10b93f6')
_distdir="Text-CSV_XS-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR=${pkgdir}"     \
      PERL_MB_OPT="--installdirs vendor --destdir ${pkgdir}" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_distdir}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_distdir}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "${srcdir}/${_distdir}"
  make install

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
