# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-scapy
pkgver=2.4.3
pkgrel=1
pkgdesc='Powerful interactive packet manipulation program written in Python'
url='http://www.secdev.org/projects/scapy/'
makedepends=('git' 'tcpdump' 'python2' 'python2-setuptools')
checkdepends=('python2-cryptography' 'python2-matplotlib'
              'graphviz' 'sox')
arch=('any')
license=('GPL2')
source=("https://github.com/secdev/scapy/archive/v${pkgver}.tar.gz")
sha512sums=('f1ff86187508074337eea35c00619794a4f50ddffcbd580201f6fceedf419ce95e1af17943cd6cd701c99122926ae8420689cc511ef9010f741875a5e90a96b1')

prepare() {
  cd scapy-${pkgver}
  sed -e 's|env python|env python2|g' -i doc/scapy/*.rst scapy/contrib/*.py setup.py \
    run_scapy scapy/pipetool.py scapy/tools/check_asdis.py scapy/layers/*.py
}

build() {
  cd scapy-${pkgver}
  python2 setup.py build
}

package() {
  cd scapy-${pkgver}
  python2 setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm 644 doc/scapy/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}"{/usr/bin,/usr/share/man}
}
