# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-cybox
pkgver=2.1.0.20
pkgrel=1
pkgdesc="A Python library for parsing and generating CybOX 2.1.0 content."
url="https://github.com/CybOXProject/python-cybox"
depends=('python2' 'python2-mixbox' 'python2-ordered-set' 'python2-weakrefmethod' 'python2-lxml')
makedepends=('python2-setuptools')
license=('custom')
arch=('any')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('d2f4623a6ec3578161687b4a332a3bcfb8e8004c3521d6c628699590d6dde6a8ac96451f54cdec279f367470123b29b89f555dc0b47adb1642c3c870f4d517ce')

build() {
  cd "python-cybox-${pkgver}" || exit
  python2 setup.py build
}

package() {
  cd "python-cybox-${pkgver}" || exit
  python2 setup.py install --root="${pkgdir}" -O1
  install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
