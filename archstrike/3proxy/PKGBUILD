# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=3proxy
pkgver=0.8.10
pkgrel=1
groups=('archstrike' 'archstrike-misc')
pkgdesc='Tiny free proxy server'
url='http://3proxy.ru'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
depends=('glibc' 'openssl')
source=("https://github.com/z3APA3A/3proxy/archive/${pkgver}.tar.gz")
sha512sums=('595bbc8014acbe3609226839ccba64c6c0a4debf14e5b60fd2f34ef3ed0a0338be53c513de0a1334083226af7bdf90683c7973709fcde427abf08dd4054a44c8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|/usr/bin/install|/bin/install|g' Makefile.Linux
  sed -i 's|/usr/local|/usr|g' Makefile.Linux
  sed -i 's|-s ||g' Makefile.Linux
  sed -i "s|-Wall -g -O2|${CFLAGS}|g" Makefile.Linux
  sed -i 's|\$(prefix)/etc/|/etc/|g' Makefile.Linux
}

build() {
  cd ${pkgname}-${pkgver}
  export CFLAGS+="${CFLAGS}"
  make -f Makefile.Linux prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.Linux install DESTDIR="${pkgdir}"
  find "${pkgdir}/etc/${pkgname}" -type f | xargs chmod 644
  mv "${pkgdir}/usr/bin/proxy" "${pkgdir}/usr/bin/${pkgname}"
}
