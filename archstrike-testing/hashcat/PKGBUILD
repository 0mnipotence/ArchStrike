# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=hashcat
pkgver=2.00
pkgrel=1
groups=('archstrike' 'archstrike-crackers')
pkgdesc="A multithreaded cross platform hash cracker."
arch=('i686' 'x86_64')
url='http://hashcat.net/hashcat/'
license=('MIT')
makedepends=('p7zip')
depends=('gmp')
source=("https://hashcat.net/files/hashcat-${pkgver}.7z")
sha512sums=('197680747e81831471c8835c7b3469f4291a50a759f5ebe83be5e062c529a4a7a72841eb71a44e9f316dd6b508aec2619b4b80957f254eabc60fc6cac030bb16')

prepare() {
  cd "${srcdir}/hashcat-${pkgver}"

  if [[ "$CARCH" == 'x86_64' ]]; then
    mv hashcat-cli64.bin hashcat
  else
    mv hashcat-cli32.bin hashcat
  fi
}

package() {
  cd "${srcdir}/hashcat-${pkgver}"

  rm *.{exe,bin,app}

  install -dm755 "${pkgdir}/usr/share/doc/hashcat"
  install -dm755 "${pkgdir}/etc/hashcat"

  install -Dm755 hashcat "${pkgdir}/usr/bin/hashcat"

  cp -dpr --no-preserve=ownership "${srcdir}/hashcat-${pkgver}"/{charsets,rules,salts,tables} "${pkgdir}"/etc/"hashcat"/
  cp -dpr --no-preserve=ownership "${srcdir}/hashcat-${pkgver}"/examples "${pkgdir}"/usr/share/doc/"hashcat"/
  # License
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"

  # Fix permissions.
  cd "${pkgdir}"
  find . -type d -exec chmod 755 {} \+
  find . -type f -exec chmod 644 {} \+
  chmod a+x "${pkgdir}/usr/bin/hashcat"
}
