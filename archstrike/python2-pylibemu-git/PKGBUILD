# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=python2-pylibemu-git
pkgver=20161024.r118
pkgrel=1
pkgdesc="A Libemu Cython wrapper"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/buffer/pylibemu'
license=('GPL2')
depends=('python2' 'libemu')
makedepends=('python2-setuptools' 'git')
source=("${pkgname}::git+${url}.git")
replaces=('python2-pylibemu')
provides=('python2-pylibemu')
conflicts=('python2-pylibemu')
sha512sums=('SKIP')

pkgver(){
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed "s/\ .*//g;s/-//g")" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' ${pkgname} | xargs sed -i 's/python$/python2/g'
}

package() {
  cd ${pkgname}

  git submodule sync
  git submodule foreach git fetch --all
  git submodule update --init --recursive

  python2 setup.py install --root=$pkgdir -O1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
