# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=lft
pkgver=3.79
pkgrel=1
groups=('archstrike' 'archstrike-recon')
pkgdesc="A layer four traceroute implementing numerous other features"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('custom')
url="http://pwhois.org/lft/"
depends=('libpcap')
makedepends=('hardening-wrapper')
source=("http://pwhois.org/dl/index.who?file=lft-$pkgver.tar.gz" "config.guess-for-aarch64.patch" "define_default_source.patch")
sha512sums=('83f35cb24966467a7da180ca8ac9d642e7ca6267a14e752a813f172956554c3d4b278a450d230535c15db1b14d415dca752ad05ed1cec8c1c7cf4953b90a646d'
            '784ad436f82c02845e464a9735eb211158cf36df1b74bb2706c1fa38b69f32862723ef96a1469cc290d82dee29b1d237f9b44390b01bbdf157b7eeaeb5322fe4'
            '7b0f3c1a588ff3a55fa949039102a388a77dc12b276023501bd46bef9a44f747423919cac5322596ee6eb4360972ac5ca543c98b12211e65e94f7cfd7f6820bb')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i $srcdir/define_default_source.patch
  patch -Np1 -i $srcdir/config.guess-for-aarch64.patch
}

build() {
  cd "$pkgname-$pkgver"
  export CFLAGS+=" -fPIE"
  export CFLAGS+=" -z now -pie"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make "prefix=$pkgdir/usr" install
  install -Dm644 "$srcdir/lft-$pkgver"/COPYING \
    "$pkgdir"/usr/share/licenses/lft/LICENSE

  # Removing setuid on bin
  chmod 755 $pkgdir/usr/bin/lft
}
