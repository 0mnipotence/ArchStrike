# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=dirsearch
pkgver=0.3.8
pkgrel=1
groups=('archstrike' 'archstrike-webapps')
pkgdesc='A HTTP(S) directory/file brute forcer'
arch=('any')
url='https://github.com/maurosoria/dirsearch'
license=('custom')
depends=('python')
provides=('dirs3arch')
replaces=('dirs3arch')
conflicts=('dirs3arch')
source=("https://github.com/maurosoria/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('c4a9fe46f839daeb82e4ba50eae085c0bf7b62c3fd4ed6d9f54cf6b940c3b6f008e0e9e7894cfe96e8af790b2cc9b58ac6df7caa08b17ccbfb54a91ecd65454e')

package() {
  cd $pkgname-$pkgver

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  for dir in db logs lib thirdparty reports; do
    install -dm755 "$pkgdir/usr/share/$pkgname/$dir"
    cp -a --no-preserve=ownership "$dir"/* "$pkgdir/usr/share/$pkgname/$dir/"
  done

  install -Dm755 $pkgname.py "$pkgdir/usr/share/$pkgname"
  install -Dm755 default.conf "$pkgdir/usr/share/$pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  ln -s "/usr/share/${pkgname}/${pkgname}.py" "${pkgdir}/usr/bin/${pkgname}"
}
