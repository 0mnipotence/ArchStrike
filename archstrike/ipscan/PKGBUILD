# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=ipscan
pkgver=3.6.0
pkgrel=2
pkgdesc="Angry IP scanner is a very fast IP address and port scanner."
arch=('i686' 'x86_64')
groups=('archstrike' 'archstrike-scanners')
license=('GPL2')
url="http://www.angryziber.com"
depends=('java-runtime>=7')
source=()
source_i686+=("https://github.com/angryip/${pkgname}/releases/download/${pkgver}/${pkgname}-linux-${pkgver}.jar")
source_x86_64+=("https://github.com/angryip/${pkgname}/releases/download/${pkgver}/${pkgname}-linux64-${pkgver}.jar")
sha512sums_i686=('92fbaea066f13dbc4378f9d6acd38af111218111c9ebfe97cf000c97e2707fb0b1076df2a1dfc51cf545fb72bb22863486f7a1c04b53ecf84fef0ec35fe4da84')
sha512sums_x86_64=('4a1383cfa37d169fdb6b7056ce4312171300f60f4295c747c8d11386dea55d89d824be0d5b4f0f820c564d853e08deb8f7b13a2709d7250bd4c73986f047347c')

package() {
  cd "${srcdir}" || exit
  [[ ${CARCH} == "i686" ]] && _SRC=ipscan-linux-${pkgver}.jar
  [[ ${CARCH} == "x86_64" ]] && _SRC=ipscan-linux64-${pkgver}.jar
  _DST="${pkgdir}/usr/share/java/ipscan/${_SRC}"
  _BIN="${pkgdir}/usr/bin/ipscan"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/java/ipscan"
  cat > "${_BIN}" << EOF
#!/usr/bin/env sh
java -jar /usr/share/java/ipscan/${_SRC} "\$@"
EOF
  chmod 755 "${_BIN}"
  install -Dm644 "${_SRC}" "${_DST}"
}
