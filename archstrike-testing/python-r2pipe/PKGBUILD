# Maintainer: ArchStrike <team@archstrike.org>

_pkgname=r2pipe
pkgbase=python-$_pkgname

buildarch=1

pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.8.2
pkgrel=1
pkgdesc="A Pipe interface for radare2"
url="https://pypi.python.org/pypi/r2pipe/"
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/01/23/983c16c49a81fb68f029b428c0756152e7da70bd3deac244dd563215d75e/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('82c6dc3696ba295a57f17bf9ece9f274c0a6b6850689c2f5add006f513c21c6c2d541f63af1d5433dd172fcf62835215dbeef0860fa9097f19f25a0435258c1c')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py build

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py build
}

package_python-r2pipe() {
  depends=('python')

  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-r2pipe() {
  depends=('python2')

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  sed -i 's|env python|env python2|' $pkgdir/usr/lib/python2.7/site-packages/r2pipe/__init__.py
}

