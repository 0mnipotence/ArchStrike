# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=inguma
pkgver=0.1.1
pkgrel=2
groups=('archstrike' 'archstrike-exploit' 'archstrike-scanners' 'archstrike-fuzzers' 'archstrike-crackers')
pkgdesc="A free penetration testing and vulnerability discovery toolkit entirely written in python. Framework includes modules to discover hosts, gather information about, fuzz targets, brute force usernames and passwords, exploits, and a disassembler."
url='http://inguma.sourceforge.net'
license=('GPL')
depends=('python2' 'python2-paramiko' 'python2-pyqt')
optdepends=('python2-cx_oracle: Access to Oracle databases'
            'python2-pysnmp: SNMP support'
            'pyqt3: GUI')
arch=('any')
source=(http://downloads.sourceforge.net/inguma/inguma-${pkgver}.tar.gz)
sha512sums=('0d732e0e5d288f950efd33da817bf1a0bd4fe7faf45ecadec69e17842bdad3af58fcfb948136931045d1aaff8f74f3951eeb5a19ed70cb4baf1ae587b84eda6d')

prepare() {
  # python -> python2
  find . -type f -name '*.py' -exec sed -i 's|usr/bin/python|usr/bin/env python2|' '{}' \;
}

package() {
  cd "$srcdir/inguma"

  install -dm755 $pkgdir/usr/share/inguma
  install -dm755 $pkgdir/usr/bin

  cp --no-preserve=ownership -r * "$pkgdir/usr/share/inguma"
  find "$pkgdir/usr/share" -executable -exec chmod 755 '{}' \;

  cat > "$pkgdir/usr/bin/inguma" <<EOF
#!/bin/sh
cd /usr/share/inguma
./inguma.py "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/inguma"
}
