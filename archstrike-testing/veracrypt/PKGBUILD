# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: Eric Ozwigh <ozwigh at gmail dot com>
# Contributor: drathir <drathir87 at gmail dot com>

buildarch=212

pkgname=veracrypt
_pkgver=1.16
pkgver=${_pkgver/\-/_}
pkgrel=1
pkgdesc='Free disk encryption software based on TrueCrypt'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archstrike' 'archstrike-crypto')
url='http://veracrypt.codeplex.com'
license=('custom')
depends=('fuse' 'wxgtk' 'device-mapper')
makedepends=('git' 'nasm')
options=('!makeflags')
source=("https://github.com/veracrypt/VeraCrypt/archive/VeraCrypt_$_pkgver.tar.gz"
        "$pkgname.desktop")
sha512sums=('9f924de8bb4f6e081e167447d9d0666e14110c9b08edcc78c874033fbef4e0f3e7549034e7999e4f9c7ab1176bd95957cf8a43fc6c8b5ec441c22813d7604684'
            'd779d831f8c235266710aa2e29a9216d783e7c1e9a31674d546df607834f7b8c217f1775a4a9e4e33c4d3476b2e24c176239d0e2749326250586918a5a7d7101')

prepare() {
  # Disable the makeself command
  cd VeraCrypt-VeraCrypt_$_pkgver/src
  sed -i 's|makeself|#makeself|' Main/Main.make
}

build() {
  cd VeraCrypt-VeraCrypt_$_pkgver/src
  WX_CONFIG='/usr/bin/wx-config' make LFLAGS+='-ldl'
}

package() {
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  cd VeraCrypt-VeraCrypt_$_pkgver/src
  install -Dm755 Main/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 Setup/Linux/usr/share/pixmaps/$pkgname.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 License.txt "$pkgdir/usr/share/licenses/$pkgname/License.txt"
}
