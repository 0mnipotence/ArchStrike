# Maintainer: ArchStrike <team[at]archstrike[dot]org>

buildarch=192

pkgname=frida
pkgver=12.7.11
pkgrel=1
pkgdesc="Inject JavaScript to explore native apps on Windows, Mac, Linux, iOS and Android"
url="http://www.frida.re"
depends=('python-colorama' 'python-pygments' 'python-prompt_toolkit')
makedepends=('python-setuptools')
license=('custom')
arch=('i686' 'x86_64')
source=("https://files.pythonhosted.org/packages/source/f/frida/frida-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/frida/frida/master/COPYING")
sha512sums=('ead41f86382ec4f76a7cbfb40b5ac4062d4a71228c3b78603bbe701cd23bb2108f9b9b0ea2ba9dad7af7c893b7ff57775707d309ddf01b75cdae55dafe97f22f'
            'e36c8d5a0538fa4bb3575a60322057f7a687aab04c13deed25399462ba0be74ce6071cc41d967e23b9a96e552462d210280427a945c6cc956919b062878efc9a')

build() {
    cd $pkgname-$pkgver
    python setup.py build
}

package() {
    cd $pkgname-$pkgver
    python setup.py install --root="$pkgdir" -O1 --skip-build
    install -Dm644 $srcdir/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
