# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=hashdb
pkgver=3.1.0
pkgrel=1
groups=('archstrike' 'archstrike-forensics' 'archstrike-misc')
pkgdesc="A block hash toolkit"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/NPS-DEEP/hashdb/'
license=('GPL')
depends=('tre' 'libxml2' 'python2' 'openssl' 'libewf' 'libtool')
makdepends=('autoconf')
source=("https://github.com/NPS-DEEP/hashdb/archive/v${pkgver}.tar.gz")
sha512sums=('2515102217f279b139cb62a71f138ae64b1a163196fcab3490b48afa9aee24c38af680380d9d2da66d89956a1d223bb0ecada1c4ab0748c118a5dc7a58498f42')

build() {
  cd "${pkgname}-${pkgver}"
  ./bootstrap.sh
  # fix error with compilation
  sed -i s/SSL_library_init/OPENSSL_init_ssl/ configure.ac configure
  ./configure --prefix=/usr --with-openssl
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
