# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=dirsearch
pkgver=0.3.6
pkgrel=1
groups=('archstrike' 'archstrike-webapps')
pkgdesc='A HTTP(S) directory/file brute forcer'
arch=('any')
url='https://github.com/maurosoria/dirsearch'
license=('custom')
depends=('python')
provides=('dirs3arch')
replaces=('dirs3arch')
conflicts=('dirs3arch')
source=("https://github.com/maurosoria/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('c30bce11e5f955c145bb66e9b3671aaab537d689dd006c02fb3a1830d5ba5448cde6f50e31bf1f4599971b93e40b85a91a529840b89844f4717312aa78e24942')

package() {
  cd $pkgname-$pkgver

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -dm755 "$pkgdir/usr/bin"

  for dir in db lib thirdparty reports; do
    install -dm755 "$pkgdir/usr/share/$pkgname/$dir"
    cp -a --no-preserve=ownership "$dir"/* "$pkgdir/usr/share/$pkgname/$dir/"
  done

  install -Dm755 $pkgname.py "$pkgdir/usr/share/$pkgname"
  install -Dm755 default.conf "$pkgdir/usr/share/$pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  ln -s /usr/share/$pkgname/$pkgname.py "$pkgdir/usr/bin/$pkgname"
}
