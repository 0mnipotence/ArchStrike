# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=CrackMapExec
pkgname=crackmapexec
pkgver=2.3
pkgrel=1
groups=('archstrike' 'archstrike-exploit')
pkgdesc='A swiss army knife for pentesting Windows/Active Directory environments'
arch=('any')
url='https://github.com/byt3bl33d3r/CrackMapExec'
license=('GPL3')
depends=('impacket' 'python2-gevent' 'python2-netaddr' 'python2-crypto' 'python2-pyasn1' 'python2-termcolor' 'python2-colorama' 'python2-pyopenssl')
source=("https://github.com/byt3bl33d3r/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('865ab679b0e35d08d848882e270054e509b2e7ce3818eb3fc5ad8c781473e12119dcac70d8861fc607f38ef82d6391ae13251bbf3a6226599a2d7fb4cc51500b')

prepare(){
  cd $_pkgname-$pkgver

  while read -r; do
      sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' "$REPLY"
  done < <(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

package() {
  cd $_pkgname-$pkgver

  # Install the license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Install the application files
  install -dm755 "$pkgdir/usr/share/$pkgname/"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/$pkgname/"

  # Symlink the main script to the system $PATH
  install -dm755 "$pkgdir/usr/bin"
  ln -s /usr/share/$pkgname/$pkgname.py "$pkgdir/usr/bin/$pkgname"
}
