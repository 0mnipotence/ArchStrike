# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=lorcon-git
pkgrel=1
makedepends=('git')
depends=('libpcap')
pkgver=20150109.r16
pkgdesc="Create what libradiate could have been: A generic library for injecting 802.11 frames, capable of injection via multiple driver frameworks, without forcing modification of the application code, old API version"
url="http://802.11ninja.net/lorcon/"
license="GPL"
groups=('archstrike')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
provides=("lorcon")
source=("$pkgname::git+https://code.google.com/p/lorcon/")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

build() {
  cd "$pkgname"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir/" install
  cd $pkgdir/usr/share/man/man3
  mv lorcon.3 lorcon-old.3
}
