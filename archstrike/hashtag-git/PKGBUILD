# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=hashtag-git
pkgver=r5.34ba672
pkgrel=1
groups=('archstrike' 'archstrike-crackers')
pkgdesc="A python script written to parse and identify password hashes."
url="https://github.com/SmeegeSec/HashTag"
arch=('any')
license=('MIT')
depends=('python2')
makedepends=('git')
replaces=('hashtag')
provides=('hashtag')
conflicts=('hashtag')
source=("$pkgname::git+https://github.com/SmeegeSec/HashTag.git")
sha512sums=('SKIP')

pkgver(){
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  cd "$pkgname"
  sed -i 's/python/python2/' HashTag.py
}

package() {
  cd "$pkgname"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/licenses/hashtag"

  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/hashtag/LICENSE.txt"
  install -Dm755 HashTag.py "$pkgdir/usr/bin/hashtag"
}
