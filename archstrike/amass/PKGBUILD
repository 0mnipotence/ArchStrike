# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="Amass"
pkgname="amass"
pkgver=3.6.0
pkgrel=1
pkgdesc=" tool to help information security professionals perform network mapping of attack surfaces and perform external asset discovery"
arch=('any')
url="https://github.com/OWASP/Amass"
license=('Apache')
makedepends=('go' 'git')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('1783691535d5b7cbbae8bb46d1ce4d30fc443b8815b453f7489ef3f351a78af9308594a4bb1e5b6b836d863a84cc309298e30f20cec579aa43b34fd18c495190')

prepare() {
  mkdir -vp "${srcdir}/github.com/OWASP"
  mv "${srcdir}/${_pkgname}-${pkgver}" "${srcdir}/github.com/OWASP/${_pkgname}"

  export GOPATH="${srcdir}"
  cd "${GOPATH}/github.com/OWASP/${_pkgname}"
  go get -d ./...
}

check() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}"
  export GOPATH="${srcdir}"
  go test ./...
}

build() {
  cd "${srcdir}/github.com/OWASP/${_pkgname}"
  export GOPATH="${srcdir}"
  go install -v ./...
}

package() {
  export GOPATH="${srcdir}"
  cd "${GOPATH}/bin"
  install -dm 755 "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  cp -a --no-preserve=ownership "${GOPATH}/github.com/OWASP/${_pkgname}/"{wordlist/,examples/} "${pkgdir}/usr/share/${pkgname}"
  chmod 644 "${pkgdir}/usr/share/${pkgname}/"{wordlist/*,examples/*}
}
