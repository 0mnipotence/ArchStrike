# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname='cupp-git'
pkgver=20160625.r20
pkgrel=1
pkgdesc='Common User Password Profiler'
url="https://github.com/Mebus/cupp"
groups=('archstrike' 'archstrike-crackers')
license=('GPL3')
depends=('python2')
makedepends=('git')
arch=('any')
replaces=('cupp')
provides=('cupp')
conflicts=('cupp')
sha512sums=('SKIP')
source=("${pkgname}::git+${url}.git")

pkgver(){
  cd ${pkgname}
  printf "%s.r%s" "$(git show -s --format=%ci master | sed "s/\ .*//g;s/-//g")" "$(git rev-list --count HEAD)"
}

prepare(){
  grep -iRl 'python' "${pkgname}" | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}

  # Base directories
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"

cat > "${pkgdir}/usr/bin/cupp" << EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
python2 ./cupp.py "\$@"
EOF

chmod 755 "${pkgdir}/usr/bin/cupp"
}
