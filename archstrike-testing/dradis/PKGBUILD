# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=dradis
pkgver=2.5.2
pkgrel=2
pkgdesc="An open source framework to enable effective information sharing."
groups=('archstrike' 'archstrike-misc')
url="http://dradisframework.org/"
license=('GPL')
depends=('ruby1.8' 'ruby1.8-sqlite3' 'ruby1.8-rake')
arch=('any')
source=(http://downloads.sourceforge.net/dradis/dradis-v$pkgver.tar.bz2)
sha512sums=('a01e833811e7d3984d0cf68ff6b0e63acfafe03ca3a6d828837cd9ed6a5dcbccea88dffc34bcb48fb291fb20a6a93c55fa6bb1698a7dfd9b5c05d4ed7e82c7aa')

prepare() {
  cd "$srcdir/dradis-2.5/server/script"
  find . -type f | xargs sed -i 's:/usr/bin/env ruby:/usr/bin/env ruby-1.8:'
}

package() {
  cd "$srcdir/dradis-2.5/server"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/dradis"

  cp -r * "$pkgdir/usr/share/dradis"
  rm "$pkgdir/usr/share/dradis/"{README,CHANGELOG}

  cd "$pkgdir/usr/bin"

  # Joint scripts.
  cat > dradis-configure << EOF
#!/bin/sh
export PATH="/opt/ruby1.8/bin:$PATH"
cd /usr/share/dradis
rake-1.8 dradis:reset
EOF

  cat > dradis-start << EOF
#!/bin/sh
export PATH="/opt/ruby1.8/bin:$PATH"
cd /usr/share/dradis
firefox https://localhost:3004/ &
ruby-1.8 script/server
EOF

  chmod +x dradis-start dradis-configure
}
