# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Tasos Latsas <tlatsas2000 at gmail dot com>

buildarch=1

pkgname=python2-pyclamd
pkgver=0.4.0
pkgrel=1
pkgdesc="A python interface to Clamd (Clamav daemon)"
arch=('any')
url="http://xael.org/norman/python/pyclamd/"
license=('LGPL3')
depends=('clamav' 'python2')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/p/pyClamd/pyClamd-$pkgver.tar.gz")
sha512sums=('b96e26729e0fe2600572534c0fa22a01238761589f1771e72a054dde9c1b8f122493c83ca04cffda167f7a0e44fbf309bdf76b8fe1de499ec50d56c677923bd4')

prepare(){
  grep -iRl 'python' "$srcdir/pyClamd-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

build() {
  cd "$srcdir/pyClamd-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/pyClamd-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  sed -i 's|#!.*/usr/bin/python3|#!/usr/bin/python2|;s|#!.*/usr/bin/env python3|#!/usr/bin/env python2|' $pkgdir/usr/lib/python2.7/site-packages/pyclamd/test_pyclamd.py
}
