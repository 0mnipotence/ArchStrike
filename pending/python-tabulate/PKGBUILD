# Maintainer: ArchStrike <team archstrike org>
# Contributor: Matthew Gamble <git@matthewgamble.net>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

buildarch=1

_pkgname=tabulate
pkgbase=python-${_pkgname}

pkgname=("python-${_pkgname}" "python2-${_pkgname}")
pkgver=0.8.2
pkgrel=1
pkgdesc="Pretty-print tabular data in Python"
arch=("any")
license=("MIT")
url="https://bitbucket.org/astanin/python-tabulate"
makedepends=("python-setuptools" "python2-setuptools")
source=("https://pypi.python.org/packages/12/c2/11d6845db5edf1295bc08b2f488cf5937806586afe42936c3f34c097ebdc/tabulate-${pkgver}.tar.gz")
sha512sums=("9e34fb84e16f4b2c1378c0f53c5ef803438fcae7bc1637ac8975f358a11653f641bba0ea19529858e8e62aa45bb5bccd3b6f0492fd2d9d9c9a3bf963dd1ac0a7")

prepare() {
  cd "${srcdir}"
  mv "${_pkgname}-${pkgver}" "${_pkgname}"
  cp -a ${_pkgname}{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-py2"
  python2 setup.py build
}

package_python-tabulate() {
  depends=("python")
  cd "${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
}

package_python2-tabulate() {
  depends=("python2")
  cd "${_pkgname}-py2"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  mv "${pkgdir}/usr/bin/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}-py2"

}

# vim:se ts=2 sw=2 sts=2 et:
