# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Andrea Benazzo <andy@qitty.net>

buildarch=220

pkgname=cryptmount
_release=5.3
pkgver=5.3.2
pkgrel=1
pkgdesc='Utility which allows an ordinary user to mount an encrypted file system'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('archstrike' 'archstrike-crypto')
url="http://cryptmount.sourceforge.net/"
license=('GPL')
depends=('cryptsetup')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-${_release}/$pkgname-$pkgver.tar.gz" "config.guess-for-aarch64.patch")
backup=('etc/cryptmount/cmtab')
sha512sums=('972c7fefadb86bb82cdeb96b185f45bec08ac07d39474a4c7e2a473bc2453624ffcc387b6836a02260dcd0d1176a5a743c45bdf3d7e78d2f519f02a48c62a6b8'
            'a39c41ccec15cbcffca93735489ee3b868ed9dfb6f49873931df25edf81e3456c91a81f2d30105bc76608aa3b2b00c854c5c880f75b2b6da3907c7a3c8f55a32')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/config.guess-for-aarch64.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-libgcrypt \
    --with-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # fixing perms
  chmod 2755 $pkgdir/usr/bin/cryptmount
}

