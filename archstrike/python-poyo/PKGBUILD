# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=poyo
pkgbase=python-${_pkgname}
pkgname=(python-${_pkgname} python2-${_pkgname})
pkgver=0.5.0
pkgrel=2
pkgdesc="A lightweight YAML Parser for Python"
arch=('any')
url="https://github.com/hackebrot/poyo"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname}::https://files.pythonhosted.org/packages/source/p/poyo/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('aad4334a2aaf63cafab0ecdbdd6f94e9e3d412e6476d7ac13a4399b4af0f43b1eb910e6a3cc7438e84262bc39413fadbe91674534e1e66f446d738c1328b9905')

prepare(){
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2" || exit
  python2 setup.py build
}

package_python-poyo() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  python3 setup.py install --root="${pkgdir}/" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-poyo() {
  depends+=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2" || exit
  python2 setup.py install --root="${pkgdir}/" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
