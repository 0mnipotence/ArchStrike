# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Diogo Leal <estranho@diogoleal.com>

buildarch=1

pkgname=exabgp
pkgver=4.0.5
pkgrel=1
pkgdesc='The BGP swiss army knife of networking'
arch=('any')
license=('custom')
groups=('archstrike' 'archstrike-networking')
url='https://github.com/Exa-Networks/exabgp'
depends=('python2-ipaddr' 'python2-nose' 'python2-psutil')
backup=('etc/exabgp.conf')
makedepends=('python2-setuptools')
source=("https://github.com/Exa-Networks/${pkgname}/archive/${pkgver}.zip"
        'exabgp.service')
sha512sums=('e370a0dce135481644a4911a8d570792e6fb66b02c63293fd48b6487eb7acc2e5a27ea65649b50940c517b5dd400a60ea968c9a71b1f4441f84a3e7fffe58cc3'
            '225726cf424d9c2a23a2c0e9d3c7ed8c5cf03993f9518efd8e45079822b2d8cbc4d6fd18b49794beee522530478b57e5e3b268b3abd92442680cdd695504360d')

build() {
    # arch python2
    find $pkgname-$pkgver -type f -exec \
        sed -i '1s,^#!/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \; -print
}

package() {
    pushd $pkgname-$pkgver
    # upstream installer
    python2 setup.py install --root="$pkgdir" --install-data=/
    # install license
    install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    popd
    # systemd
    install -D -m 644 $pkgname.service \
        "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    # install empty config file
    install -D -m 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}
