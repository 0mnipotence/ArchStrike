# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: James Fryman <jfryman@gmail.com>

buildarch=212

_pkgname=libdnet
pkgname=$_pkgname-ipv6
pkgver=1.12
pkgrel=9
pkgdesc='A simplified, portable interface to several low-level networking routines (with ipv6 support)'
url='http://code.google.com/p/libdnet/'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('python2' 'bash')
makedepends=('python2' 'pyrex')
provides=("$_pkgname")
conflicts=("$_pkgname")
license=('BSD')
source=(
  "http://libdnet.googlecode.com/files/$_pkgname-$pkgver.tgz"
  "$_pkgname-1.12.pyrex2.7.patch"
  "$_pkgname-ipv6.patch"
)
sha512sums=(
  'b763d2caa77b8d16b531142a5bce3b862ed20198406a00e1fe7f3b45ce308b97d638b0c7f03bd8593fdd206cc1645952839bc340b77537298a3df88599835bf3'
  'ac5fecf1c7028b469965cddc92ed8e8b87605282ae14417274fcf685e57485f89d05583e2573d86611043f5bbfdc924fc5f5bb761a43cc18a2bcd6b5c10f0821'
  '3f9eb49a54172bae25ab6f137020c9babcb6fdb888d8b27050bfbdb87986de83df7fe81d783068d61fa33cd803fecd74d0f9acaf26c4991585609787e783b3be'
)

prepare() {
  cd $_pkgname-$pkgver
  patch -p0 < ../$_pkgname-1.12.pyrex2.7.patch
  patch -p1 < ../$_pkgname-ipv6.patch
  rm -f python/dnet.c
}

build() {
  cd $_pkgname-$pkgver
  autoreconf -I config --force --install
  ./configure --prefix=/usr --sbindir=/usr/bin
  pyrexc python/dnet.pyx
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/license"

  cd python
  python2 setup.py install --root="$pkgdir"
}
