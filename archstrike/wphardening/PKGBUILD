# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=wphardening
pkgver=1.6
pkgrel=1
pkgdesc="A security tool for WordPress"
arch=('any')
url="https://github.com/elcodigok/wphardening"
license=('GPL3')
depends=('python2-gitpython' 'python2-jinja')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('18da9e25751d3ac2f7b8f69e6bef8e0100647e74ad2575fed12d7609300cb18bc8bf4eedbc2cfc0df9be5d59704fe71082220b88d3de41ab73f6cb850b102526')

prepare(){
  grep -iRl 'python' "${pkgname}-${pkgver}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd ${pkgname}-${pkgver}
  install -dm755 "${pkgdir}/usr/bin/"
  install -dm755 "${pkgdir}/usr/share/${pkgname}/data"
  install -dm755 "${pkgdir}/usr/share/${pkgname}/doc"
  install -dm755 "${pkgdir}/usr/share/${pkgname}/lib"
  install -dm755 "${pkgdir}/usr/share/${pkgname}/lib/minify"
  install -Dm644 lib/minify/*.py "${pkgdir}/usr/share/${pkgname}/lib/minify/"
  install -Dm644 README.md "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 data/* "${pkgdir}/usr/share/${pkgname}/data/"
  install -Dm644 doc/AUTHOR "${pkgdir}/usr/share/${pkgname}/doc/"
  install -Dm644 doc/COPYING "${pkgdir}/usr/share/${pkgname}/doc/"
  install -Dm644 lib/*.py "${pkgdir}/usr/share/${pkgname}/lib/"
  install -Dm755 wphardening.py "${pkgdir}/usr/share/${pkgname}/wphardening.py"

cat > "${pkgdir}/usr/bin/${pkgname}"<<EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
python2 ${pkgname}.py "\$@"
EOF

chmod 755 ${pkgdir}/usr/bin/${pkgname}
}

