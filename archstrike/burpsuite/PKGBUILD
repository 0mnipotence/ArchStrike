# Maintainer: ArchStrike <team@archstrike.org>

pkgname=burpsuite
pkgver=1.7.01
pkgrel=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-scanners' 'archstrike-fuzzers' 'archstrike-proxy')
pkgdesc="An integrated platform for attacking web applications (free edition)."
url='http://portswigger.net/burp/'
depends=('java-environment' 'bash')
arch=('any')
license=('custom')
noextract=("burpsuite.jar")
source=("burpsuite.jar::https://portswigger.net/DownloadUpdate.ashx?Product=Free" LICENSE)
sha512sums=('f468641305cdb15e7e9ea3cbd47e30aac4e18140847f2a924f518feb3c7b7635aa8fbb9cf228e6ab1297649f2cab18b4e7374cd58429c08e79868cfcb44da3f7'
            '37abc24b19571bfd2941e3dd7aa8a8a330bd3bbbafb3d1f813035ab0313e189c1f4a43a68f820a32656b86dd29cca94afcaefc6a4bfb4219ef18dc0fc4923b86')

package() {
  cd ${srcdir}

  # Make base directories.
  install -dm755 $pkgdir/usr/share/licenses/burpsuite
  install -dm755 $pkgdir/usr/share/burpsuite
  install -dm755 $pkgdir/usr/bin

  install -m644 burpsuite.jar $pkgdir/usr/share/burpsuite/burpsuite.jar
  install -m644 LICENSE $pkgdir/usr/share/licenses/burpsuite

  # Make joint script.
  cat > ${pkgdir}/usr/bin/burpsuite <<EOF
#!/bin/sh
exec \$JAVA_HOME/bin/java -jar /usr/share/burpsuite/burpsuite.jar "\$@"
EOF
  chmod +x ${pkgdir}/usr/bin/burpsuite
}
