# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: xorond <xorond@gmail.com>
_author='Karel Miko'
_perlmod='CryptX'

buildarch=220

pkgname=perl-crypt-x
pkgver=0.060
pkgrel=1
pkgdesc="Cryptographically secure random number generator"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://search.cpan.org/~mik/CryptX-0.054/lib/CryptX.pm"
license=('custom')
depends=('perl>=5.10.0')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIK/CryptX-${pkgver}.tar.gz")
sha512sums=('25be4447261780085c01089bb06e581298627f2a8b86cf003d6ae16b751a43a5d2c72fbd4719ffb1f40742fc343c71584aa7708e100a737b272394a035bd6825')

build() {
  cd "${srcdir}/${_perlmod}-${pkgver}"

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_perlmod}-${pkgver}"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  make install DESTDIR="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
