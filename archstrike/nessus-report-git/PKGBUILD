# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=nessus-report-git
pkgver=r13.b5015a1
pkgrel=3
pkgdesc="Automatically parse and tabulate Nessus findings into OpenDocument tables"
url="https://github.com/roeyk/nessus-report"
arch=('any')
license=('GPL3')
depends=('python2' 'python2-odfpy')
makedepends=('git')
provides=('nessus-report')
conflicts=('nessus-report')
replaces=('nessus-report')
source=("${pkgname}::git+https://github.com/roeyk/nessus-report.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}"/usr/bin/
  install -dm755 "${pkgdir}"/usr/share/${pkgname}
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -m644 README.md "${pkgdir}"/usr/share/${pkgname}/README.md
  install -m644 nessus-report.py "${pkgdir}"/usr/share/${pkgname}/nessus-report.py

cat > "${pkgdir}"/usr/bin/nessus-report <<EOF
#!/usr/bin/env bash
cd /usr/share/${pkgname}
python2 nessus-report.py "\$@"
EOF
chmod 755 "${pkgdir}"/usr/bin/nessus-report
}

