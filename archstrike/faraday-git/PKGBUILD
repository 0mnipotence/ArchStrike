# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

_pkgname=faraday
pkgname=${_pkgname}-git
pkgver=20170526.r3850
pkgrel=1
pkgdesc='An Integrated Multiuser Pentest Environment that maps and leverages all the knowledge you generate in real time'
groups=('archstrike' 'archstrike-misc')
url='http://www.faradaysec.com'
depends=('zsh' 'java-environment' 'python2-pip' 'python2-argparse' 'python2-flask' 'python2-tornado' 'python2-dateutil'  'python2-colorama' 'couchdb' 'python2-pysvn' 'python2-ipy' 'libpqxx' 'python2-couchdbkit' 'python2-requests' 'python2-whoosh' 'python2-psycopg2' 'python2-restkit' 'python2-mockito' 'ruby' 'python2-ipy' 'python2-sqlalchemy' 'python2-service-identity' 'python2-twisted' 'gobject-introspection' 'gobject-introspection-runtime')
depends_x86_64+=('lib32-glibc')
makedepends=('git')
options=('!strip')
license=('GPL')
arch=('any')
source=("${pkgname}::git+https://github.com/infobyte/faraday.git")
replaces=("$_pkgname")
conflicts=("$_pkgname")
provides=("$_pkgname")
install=${pkgname}.install
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf '%s.r%s' "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgname
  grep -iRl python | xargs sed -i 's|python$|python2|g'
}

package() {
  cd $pkgname
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/${_pkgname}"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/${_pkgname}"

  cat > "$pkgdir/usr/bin/${_pkgname}" << EOF
#!/usr/bin/env bash
cd /usr/share/${_pkgname}
python2 ${_pkgname}.py "\$@"
EOF

cat > "$pkgdir/usr/bin/${_pkgname}-server" <<EOF
#!/usr/bin/env bash
cd /usr/share/${_pkgname}
python2 ${_pkgname}-server.py "\$@"
EOF

  chmod 755 "$pkgdir"/usr/bin/{${_pkgname},${_pkgname}-server}
}

