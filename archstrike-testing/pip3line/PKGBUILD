# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=pip3line
pkgver=2.7.1
pkgrel=1
groups=('archstrike' 'archstrike-fuzzers')
pkgdesc="Swiss army knife of byte manipulation"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://github.com/nccgroup/pip3line'
license=('GPL3')
if [[ "$CARCH" == 'x86_64' ]]; then
     depends=('qt4' 'python' 'gcc-libs-multilib' 'distorm')
else
     depends=('qt4' 'python' 'gcc-libs' 'distorm')
fi
makedepends=('cmake')
source=("https://github.com/nccgroup/pip3line/archive/$pkgver.tar.gz" "libdir.patch")
sha512sums=('3c882d9d474d96d4f9638eb62e066dfd73545301a13838de2cc7c8f0301b485442bba02acc293d93337bf1c0d2bbbb788c7844155d18c05b1a2c94e2e157b926'
            '450b69f9a8d4be66b694463264f52f45ffa13d8d544dd7e7d4824ba8c5858e4aabf8c70db7f905d9911ab5b53e8f0532023b2c5c19658b17f1abde94f75292ca')

prepare(){
 cd "$srcdir/pip3line-$pkgver"
 patch -p1 -i $srcdir/libdir.patch
}

build(){
  cd "$srcdir/pip3line-$pkgver"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_SKIP_RPATH=ON -DALL=ON -DWITH_DISTORM=ON -DWITH_OPENSSL=ON -DWITH_PYTHON27=ON  -DWITH_PYTHON3=ON ../
  make
}

package(){
  cd "$srcdir/pip3line-$pkgver/build"
  make DESTDIR=$pkgdir install
}
