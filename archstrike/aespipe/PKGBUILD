# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Prurigro

buildarch=220

pkgname=aespipe
pkgver=2.4e
pkgrel=1
pkgdesc="Reads data from stdin and outputs encrypted or decrypted results to stdout"
url="http://loop-aes.sourceforge.net/aespipe"
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('archstrike')
depends=('bash')
source=("http://loop-aes.sourceforge.net/${pkgname}/${pkgname}-v${pkgver}.tar.bz2")
sha512sums=('e038f710ac971e5ba740648c0f255e8bc9fcea9be1a6b76208a453f21f013e1662c006d4f4495fa2b2560fd9b0c236947462a9b337a120073c6a5335b29f077e')

prepare() {
    cd ${pkgname}-v${pkgver}

    patch -p0 < aes-GPL.diff
}

build() {
    cd ${pkgname}-v${pkgver}

    if [ "$CARCH" = "x86_64" ]; then
        CFLAGS="-O2 -Wall -fno-pie" LDFLAGS="-no-pie" ./configure --prefix=/usr --enable-asm=amd64 --enable-padlock --enable-intelaes
    elif [ "$CARCH" = "i686" ]; then
        ./configure --prefix=/usr --enable-asm=x86 --enable-padlock --enable-intelaes
    else
        ./configure --prefix=/usr --disable-asm --disable-padlock --disable-intelaes
    fi

    make
}

check() {
   cd ${pkgname}-v${pkgver}
   make tests
}

package() {
    cd ${pkgname}-v${pkgver}

    make DESTDIR="${pkgdir}" install
    install -Dm755 bz2aespipe "${pkgdir}/usr/bin/bz2aespipe"
}
