# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=hqlmap
pkgver=r38.bb6ab46
pkgrel=1
pkgdesc="This project has been created to exploit HQL Injections"
arch=('any')
url="https://github.com/PaulSec/HQLmap"
license=('MIT')
depends=('python2')
makedepends=('git')
groups=('archstrike')
source=("$pkgname"::'git+https://github.com/PaulSec/HQLmap.git')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir/$pkgname"
  install -dm755 "$pkgdir/usr/bin/"
  install -dm755 "$pkgdir/usr/share/hqlmap"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/hqlmap/LICENSE"
  cp -R --no-preserve=ownership db "$pkgdir/usr/share/hqlmap"
  install -Dm644 HQLmap.py "$pkgdir/usr/share/hqlmap/hqlmap.py"
  sed -i 's|python|python2|' $pkgdir/usr/share/hqlmap/hqlmap.py
  cat > $pkgdir/usr/bin/hqlmap<<EOF
#!/bin/sh
cd /usr/share/hqlmap
python2 hqlmap.py
EOF
  chmod +x "$pkgdir/usr/bin/hqlmap"
}
