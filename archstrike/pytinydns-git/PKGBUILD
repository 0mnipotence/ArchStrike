# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=pytinydns-git
pkgver=r16.d8b53c4
pkgrel=2
groups=('archstrike' 'archstrike-networking')
pkgdesc="A tiny DNS server that resolves A records to IPs."
arch=('any')
url='https://github.com/chokepoint/pytinydns'
license=('MIT')
depends=('python2' 'python2-redis' 'redis')
makedepends=('git')
provides=('pytinydns')
conflicts=('pytinydns')
replaces=('pytinydns')
source=("${pkgname}::git+https://github.com/chokepoint/pytinydns.git")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  grep -iRl 'python' ${pkgname} | xargs sed -i 's|python$|python2|g'
}

package() {
  cd ${pkgname}
  install -dm755 "${pkgdir}/usr/bin"
  install -Dm755 pytinydns.py "${pkgdir}/usr/share/pytinydns/pytinydns.py"
  install -Dm755 redis_import.py "${pkgdir}/usr/share/pytinydns/redis_import.py"
  install -Dm644 pytinydns.conf "${pkgdir}/usr/share/pytinydns/pytinydns.conf"
  install -Dm644 pytinydns.host "${pkgdir}/usr/share/pytinydns/pytinydns.host"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/pytinydns/README"

cat > "${pkgdir}/usr/bin/pytinydns" <<EOF
#!/usr/bin/env bash
cd /usr/share/pytinydns
python2 pytinydns.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/pytinydns"
}
