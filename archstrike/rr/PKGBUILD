# Maintainer: ArchStrike <team@archstrike.org>

buildarch=192

pkgname=rr
pkgver=5.1.0
pkgrel=1
groups=('archstrike' 'archstrike-misc' 'archstrike-crypto' 'archstrike-analysis')
pkgdesc="A Record and Replay Framework"
arch=('i686' 'x86_64')
url='https://github.com/mozilla/rr'
makedepends=("cmake" "python2-pexpect" "gdb" "capnproto")
license=('custom')
depends_x86_64+=('libpfm4' 'lib32-gcc-libs' 'bash' 'lib32-zlib' 'python2' 'gcc-multilib')
depends_i686+=('libpfm4' 'bash' 'zlib' 'python2')
source=("https://github.com/mozilla/rr/archive/$pkgver.tar.gz")
sha512sums=('8cb7da20ddb2bfbeaf57d9bca76a3b5495d4bd74a00092c40c948570a2cda3dca6cb1e73ed65af099fe405a4d29ff1a852496baf0fcb6119258443649a65766a')

build(){
  cd "$srcdir/rr-$pkgver"
  export CFLAGS+=" -fPIC"
  mkdir build && cd build
  cmake -DCMAKE_VERBOSE_MAKEFILE=ON -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE=/usr/bin/python2 ..
  make
}

package() {
  cd "$srcdir/rr-$pkgver/build"
  make DESTDIR=$pkgdir install
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
