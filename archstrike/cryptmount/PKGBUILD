# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Andrea Benazzo <andy@qitty.net>

buildarch=212

pkgname=cryptmount
pkgver=5.2.1
pkgrel=1
pkgdesc='Utility which allows an ordinary user to mount an encrypted file system'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archstrike' 'archstrike-crypto')
url="http://cryptmount.sourceforge.net/"
license=('GPL')
depends=('cryptsetup')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-5.2/$pkgname-$pkgver.tar.gz")
backup=('etc/cryptmount/cmtab')
sha512sums=('53a4bc7da7bfb623cfa9439fdc8801fd73128a3ef57092b45538f886ec53ff2da1092e249af9870016fc4ddaa618ec491341d4c46d387b786900e04badecc71d')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-libgcrypt \
    --with-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # fixing perms
  chmod 2755 $pkgdir/usr/bin/cryptmount
}

