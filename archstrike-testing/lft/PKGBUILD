# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=lft
pkgver=3.74
pkgrel=1
groups=('archstrike' 'archstrike-recon')
pkgdesc="A layer four traceroute implementing numerous other features"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('custom')
url="http://pwhois.org/lft/"
depends=('libpcap')
source=("http://pwhois.org/dl/index.who?file=lft-$pkgver.tar.gz")
sha512sums=('00d231d0b49039d72bd0119faa36f7159229b0010cbe735b5bbee42b21843a205be9b324556c40523d48f00400262bed71e7ce36dd564e2f1c892ca5dc885fe6')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make "prefix=$pkgdir/usr" install
  install -Dm644 "$srcdir/lft-$pkgver"/COPYING \
    "$pkgdir"/usr/share/licenses/lft/LICENSE

  # Removing setuid on bin
  chmod 755 $pkgdir/usr/bin/lft
}
