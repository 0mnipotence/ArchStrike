# Maintainer: ArchStrike <archstrikelinux@gmail.com>

buildarch=212

pkgname=memdump
pkgver=1.01
pkgrel=2
groups=('archstrike' 'archstrike-forensics')
pkgdesc='Dumps system memory to stdout, skipping over holes in memory maps.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('glibc')
url='http://www.porcupine.org/forensics/tct.html'
license=('custom')
options=('!makeflags' '!strip')
source=(
  "http://www.porcupine.org/forensics/memdump-$pkgver.tar.gz"
  'makedefs.patch'
)
sha512sums=(
  '46d013f812b0a5807c7ba38d6c3940e105057ba8e64b4f45b75a0800cab212d164caf881efbc1958d5c5c239236fdcb61f6fe093886ff3e28bc0b70791aaee3e'
  '5967ee881a95bc7e7772b3c92d60a96407a42ecf0f0ec0e3a132b4147a3c5a5d5665092c2f5009bedbcd5f636e1fa4ac6d086e5ae21f33d0d9307a5c08e5182b'
)

prepare() {
  cd $pkgname-$pkgver
  patch -p0 < ../makedefs.patch
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver

  # Renamed to fix conflict with community/libmemcached
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname-kernel"
  install -Dm644 $pkgname.1 "$pkgdir/usr/share/man/man1/$pkgname-kernel.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
