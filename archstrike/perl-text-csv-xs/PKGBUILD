# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname='perl-text-csv-xs'
pkgver=1.40
pkgrel=1
pkgdesc="comma-separated values manipulation routines"
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='https://metacpan.org/pod/Text::CSV_XS'
source=("http://search.cpan.org/CPAN/authors/id/H/HM/HMBRAND/Text-CSV_XS-${pkgver}.tgz")
sha512sums=('1570c6ff08487603e428d889e9f917766ff8d053727924db6b5f2ab28b0161faa9496d44eba2f466ea2447b1ec8491313857cb447a948a0b9194a6e1c56e961c')
_distdir="Text-CSV_XS-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR=${pkgdir}"     \
      PERL_MB_OPT="--installdirs vendor --destdir ${pkgdir}" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_distdir}" || exit
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_distdir}" || exit
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "${srcdir}/${_distdir}" || exit
  make install

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
