# Maintainer: ArchStrike <team [at] archstrike [dot] org>

buildarch=220

pkgname=bettercap
pkgver=1.5.9
pkgrel=1
groups=('archstrike' 'archstrike-sniffers' 'archstrike-exploit' 'archstrike-networking')
pkgdesc='A complete, modular, portable and easily extensible MITM framework.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url='https://github.com/evilsocket/bettercap'
license=('GPL3')
depends=('ruby-rspec-core' 'libpcap' 'net-tools')
source=("https://github.com/evilsocket/bettercap/archive/v$pkgver.tar.gz")
sha512sums=('427c2d911ef4381fa6242d043ac582718532cc417be04494769eb12c3911cbb2ae5fb0f8522a742387a76de275cc84926ba660d2eb003e21af040bfc969dda65')

build() {
  cd "$pkgname-$pkgver"

  gem build bettercap.gemspec
}

package() {
  cd "$pkgname-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --no-user-install -n "$pkgdir/usr/bin" -i "$pkgdir/$_gemdir" bettercap*.gem

  # fix $pkgdir reference by deleting gem_make.out files
  find ${pkgdir}/${_gemdir} -name gem_make.out | xargs rm -rf

  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/"
}
