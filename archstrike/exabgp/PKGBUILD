# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Diogo Leal <estranho@diogoleal.com>

buildarch=1

pkgname=exabgp
pkgver=4.2.6
pkgrel=1
pkgdesc='The BGP swiss army knife of networking'
arch=('any')
license=('custom')
groups=('archstrike' 'archstrike-networking')
url='https://github.com/Exa-Networks/exabgp'
depends=('python2-ipaddress' 'python2-nose' 'python2-psutil')
backup=('etc/exabgp.conf')
makedepends=('python2-setuptools')
source=("https://github.com/Exa-Networks/${pkgname}/archive/${pkgver}.zip"
        'exabgp.service')
sha512sums=('bd81e3d881b3c7fe2d763a8b0153d0f2748fdc5fc7121fbda11d0f3ba2b11f4b97c94bdfce6fd76a545d17d86565b49baa9d66faed3d75b73c078bbb7ff45c23'
            '225726cf424d9c2a23a2c0e9d3c7ed8c5cf03993f9518efd8e45079822b2d8cbc4d6fd18b49794beee522530478b57e5e3b268b3abd92442680cdd695504360d')

build() {
    # arch python2
    find $pkgname-$pkgver -type f -exec \
        sed -i '1s,^#!/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \; -print
}

package() {
    pushd $pkgname-$pkgver
    # upstream installer
    python2 setup.py install --root="$pkgdir" --install-data=/
    # install license
    install -D -m 644 LICENCE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    popd
    # systemd
    install -D -m 644 $pkgname.service \
        "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    # install empty config file
    install -D -m 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}
