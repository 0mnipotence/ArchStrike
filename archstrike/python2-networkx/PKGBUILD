# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-networkx
pkgver=2.2
pkgrel=1
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="https://networkx.github.io"
depends=('python2-decorator')
optdepends=('python2-numpy:     Provides sparse matrix representation of graphs and many numerical scientific tools.'
            'python2-scipy:     Provides flexible drawing of graphs.'
            'python2-pyparsing: Required for pydot, GML file reading.'
            'python2-yaml:      Required for YAML format reading and writing.')
makedepends=('python2-setuptools')
checkdepends=('python2-pytest-runner')
source=("https://github.com/networkx/networkx/archive/networkx-${pkgver}.tar.gz")
sha512sums=('247ca4d66d33cec86412df846cc1fdbe1cbb0eeee83fb2a9ccdd702eb1453dd7167b397ed77d6b3dfdef6346485160ce75c12be58372238aaf98d334b4c8aeb4')

build() {
  cd networkx-networkx-${pkgver}
  python2 setup.py build
}

package() {
  cd networkx-networkx-${pkgver}
  python2 setup.py install --root="${pkgdir}" -O1
  install -D -m 644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
  mv "${pkgdir}"/usr/share/doc/networkx-${pkgver} "${pkgdir}"/usr/share/doc/${pkgname}
}
