# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=wfuzz
pkgver=2.1.5
pkgrel=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-fuzzers')
pkgdesc="Utility to fuzz web applications to find their not linked resources"
url="https://github.com/xmendez/wfuzz"
arch=('any')
license=('GPL')
depends=('python2-pycurl')
source=("https://github.com/xmendez/wfuzz/archive/v$pkgver.tar.gz")
sha512sums=('aac9c2f4289b09504652a8a1180a859c7cf71dba8f6207072e778d93dbd70a13e79d76659ea422db8e89ad634e72bd2da6f264d02933dc95ebe11b69d0f20b99')

prepare() {
  cd $pkgname-$pkgver
  while read -r; do
      sed -i -re 's|#!\s*(/usr)?(/local)?/bin/.*python.*$|#!/usr/bin/env python2|g' "$REPLY"
  done < <(egrep -rl '^\s*#!\s*(/usr)?(/local)?/bin/.*python')
}

package() {
  cd $pkgname-$pkgver
  install -dm755 ${pkgdir}/usr/share/${pkgname}/
  install -dm755 ${pkgdir}/usr/bin
  cp -a --no-preserve=ownership * ${pkgdir}/usr/share/${pkgname}/
  # fix python issues
  sed -i 's|python$|python2|' $pkgdir/usr/share/wfuzz/wfuzz.py
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/bin/bash
cd /usr/share/${pkgname}
python2 ${pkgname}.py "\$@"
EOF
  chmod +x ${pkgdir}/usr/bin/${pkgname}
}
