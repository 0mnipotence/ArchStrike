# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

buildarch=220

pkgname=multimon-ng
pkgver=1.1.3
pkgrel=1
pkgdesc="An sdr decoder, supports pocsag, ufsk, clipfsk, afsk, hapn, fsk, dtmf, zvei."
url="http://dekar.wc3edit.net/2012/05/24/multimonng/"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
groups=('archstrike')
license=("GPL2")
depends=('libpulse')
makedepends=('qt4')
optdepends=('sox: wav conversion')
source=("https://github.com/EliasOenal/multimon-ng/archive/${pkgver}.tar.gz")
sha512sums=('cb696164c194a3edd61455c215a662b1c85829f9c3f23d8df733bf32a420adf986cb66ce929005fca537c4f55f4a0320772ea98e0fbf8af9ca984d42a7c5770d')

build() {
  cd ${pkgname}-${pkgver}

  qmake-qt4 PREFIX=/usr multimon-ng.pro
  sed -i 's/-lpulse-simple/& -lpulse/' Makefile
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 multimon-ng "${pkgdir}/usr/bin/multimon-ng"
}
