# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: William Robertson <nullptr@gmail.com>

buildarch=212

pkgname=silk
pkgver=3.12.1
pkgrel=1
pkgdesc='CERT netflow tools.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://tools.netsa.cert.org/silk'
license=('GPL')
groups=('archstrike' 'archstrike-networking')
depends=('libfixbuf' 'lzo2' 'libpcap' 'python2' 'gnutls' 'c-ares' 'adns')
makedepends=('perl' 'flex' 'bison')
install="$pkgname.install"
source=("http://tools.netsa.cert.org/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('278b2d8f4167e521c72f272a9eb9bc20c553c6af360e27d08a7a850f68797a1b5ccd55b51057d3d79b07bd710442d61f0505f9324089dee55550e9cde86a8038')

prepare(){
  grep -iRl 'python' "$srcdir/$pkgname-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
        export LIBS="-lpthread"
        ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6 --with-python=$(which python2) --with-pcap \
        --with-pic --with-libfixbuf --with-gnutls --enable-output-compression --enable-localtime
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
        # Fixing python
        sed -i 's|python$|python2|' $pkgdir/usr/bin/rwidsquery $pkgdir/usr/bin/rwpcut
}
