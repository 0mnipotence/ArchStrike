# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=dislocker
pkgver=0.4.1
pkgrel=1
pkgdesc="Read BitLocker encrypted volumes under Linux"
arch=('armv6h' 'i686' 'armv7h' 'x86_64')
url="https://github.com/Aorimn/dislocker"
license=('GPL2')
groups=('archstrike')
depends=('fuse' 'mbedtls')
source=("https://github.com/Aorimn/dislocker/archive/v0.4.1.tar.gz")
sha512sums=('b6affe17f9c183422a15b0b186b6b332429e60649dad3a8b354524a1913415f3301fd21f2e89a6dbe83dd99aebf0ed349d6fd2976950842954d257d1d658eb33')

prepare() {
  cd "${pkgname}-${pkgver}/src"
  sed -i 's|polarssl|mbedtls|g' Makefile
  sed -i 's|polarssl|mbedtls|g' encommon.h
  sed -i 's|polarssl|mbedtls|g;s|sha2.h|sha256.h|' accesses/stretch_key.h
  sed -i 's|polarssl|mbedtls|g' ssl_bindings.h
  sed -i '30i#include "mbedtls/compat-1.3.h"' ssl_bindings.h
}

build() {
  cd "${pkgname}-${pkgver}/src"
  make all
}

package() {
  cd "${pkgname}-${pkgver}"
  install -dm755 "$pkgdir"/usr/bin
  install -dm755 "$pkgdir"/usr/lib
  install -dm755 "$pkgdir"/usr/share/man/man1
  install -Dm644 README.txt "$pkgdir"/usr/share/dislocker/README.txt
  install -Dm644 CHANGELOG.txt "$pkgdir"/usr/share/dislocker/CHANGELOG.txt
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/dislocker/LICENSE
  cd src
  make DESTDIR="${pkgdir}/usr" install
  ## Fixing installer issues
  cd "${pkgdir}/usr/lib"
  rm -rf libdislocker.so.0
  rm -rf libdislocker.so
  ln -s libdislocker.so.0.4.1 libdislocker.so.0
  ln -s libdislocker.so.0 libdislocker.so
}
