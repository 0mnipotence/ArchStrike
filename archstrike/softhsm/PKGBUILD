# Maintainer: ArchStrike <team@archstrike.org>
# Contributor: Javier Torres <javitonino [at] gmail [dot] com>

buildarch=220

pkgname=softhsm
pkgver=2.5.0
pkgrel=2
pkgdesc="Software PKCS11 store"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.opendnssec.org/softhsm/"
license=('BSD')
depends=('botan>=1.8.5' 'sqlite3>=3.3.9' 'openssl')
backup=("etc/softhsm2.conf")
source=("https://dist.opendnssec.org/source/softhsm-${pkgver}.tar.gz")
sha512sums=('a1e686729196dc25591eb3da57c2c8ea8494ed274ba711842b2dcae696f477a202acda13a975b8fb1eb68e8e44a79e839dbbc6ba500cab02ad13072c660752d9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # /usr/lib/softhsm -> /usr/bin (no subdirectory)
  sed -i \
  -e "s/full_libdir=\"\$full_libdir\/softhsm\"/#full_libdir=\"\$full_libdir\/softhsm\"/" \
  -e "s/libdir=\$full_libdir/#libdir=\$full_libdir/" \
  configure

  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var --sysconfdir=/etc --with-botan=/usr --disable-gost
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  chmod 755 "${pkgdir}/var/lib/${pkgname}"
}
