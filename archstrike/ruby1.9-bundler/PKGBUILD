# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=ruby1.9-bundler
_gemname=${pkgname#ruby1.9-}
pkgver=1.10.6
pkgrel=1
pkgdesc="Manages an application's dependencies through its entire life, across many machines, systematically and repeatably."
arch=('any')
url='http://gembundler.com'
license=('MIT')
depends=('ruby1.9')
source=(
  "http://rubygems.org/downloads/$_gemname-$pkgver.gem"
  'https://raw.githubusercontent.com/bundler/bundler/master/LICENSE.md'
)
noextract=("$_gemname-$pkgver.gem")
sha512sums=(
  '8b6abcaa53f6d7e5f7728fde4120bda79e6aa849e27ae5df589076c66828b7dfac16106d079a3430504c3bd42b63a8ba1ae1de0901608bc661c9dc399c70c1e7'
  'fa43908bb0e9af7b5f2efd785e052749276671c1af46df762df337b24312f0b157d291484a5a0299a644a3d1455e6465a57d120defb859aaa26b158a4a02677a'
)

package() {
  local _gemdir="$(ruby-1.9 -rubygems -e'puts Gem.default_dir')"
  if [[ "$CARCH" = arm* ]] ; then
    gem-1.9 install --ignore-dependencies --no-user-install --no-rdoc --no-ri -i "$pkgdir$_gemdir" $_gemname-$pkgver.gem
  else
    gem-1.9 install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" $_gemname-$pkgver.gem
  fi
  install -dm755 "$pkgdir/usr/bin"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  ln -s "$_gemdir/bin/bundle" "$pkgdir/usr/bin/bundle-1.9"
}
