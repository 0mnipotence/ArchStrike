# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=tplmap
pkgver=0.4.1
pkgrel=1
groups=('archstrike' 'archstrike-scanners')
arch=('any')
pkgdesc="Automatic Server-Side Template Injection Detection and Exploitation Tool"
url="https://github.com/epinna/tplmap"
license=('GPL3')
depends=('python2' 'python2-yaml' 'python2-requests')
source=("https://github.com/epinna/tplmap/archive/v${pkgver}.tar.gz")
sha512sums=('80710481a4e276b7965ac319d61cb41198910da1e83b7956c80431644975ead5f37a86c444c0a6c2d0c278c87d233c794a34dd0e5dae01a0f29f66533bb73c5c')

prepare() {
  grep -ZRil 'python' ${pkgname}-${pkgver} | xargs -0 sed -i '1 s|#!.*/usr/bin/python3?|#!/usr/bin/python2|;s|#!.*/usr/bin/env python3?$|#!/usr/bin/env python2|'
}

package() {
  cd ${pkgname}-${pkgver}
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  cp -a --no-preserve=ownership * "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}"

cat > "${pkgdir}/usr/bin/${pkgname}" <<EOF
#!/bin/sh
cd /usr/share/${pkgname}
python2 tplmap.py "\$@"
EOF
chmod 755 "${pkgdir}/usr/bin/${pkgname}"

}
