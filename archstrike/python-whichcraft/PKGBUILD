# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname=whichcraft
pkgbase=python-${_pkgname}
pkgname=(python-${_pkgname} python2-${_pkgname})
pkgver=0.6.1
pkgrel=2
arch=('any')
pkgdesc="provides cross-platform cross-python shutil.which functionality"
url="https://github.com/cookiecutter/whichcraft"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname}::https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('85075289ae5cacb8f45a23776fcbbef3f62a4d8bdf110e1d3f9f25fb4ba44117c459d5f42dec7fd7f5c9680963d6aa1c1e31fc5b9fbe6127395698468f8d77fb')

prepare(){
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2" || exit
  python2 setup.py build
}

package_python-whichcraft() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}" || exit
  python3 setup.py install --root="${pkgdir}/" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-whichcraft() {
  depends+=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2" || exit
  python2 setup.py install --root="${pkgdir}/" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
