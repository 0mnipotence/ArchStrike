# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

pkgname=suricata
pkgver=3.0
pkgrel=1
pkgdesc="An Open Source Next Generation Intrusion Detection and Prevention Engine"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="http://openinfosecfoundation.org/index.php/download-suricata"
license=('GPL2')
groups=('archstrike')
depends=('nss' 'jansson' 'libcap-ng' 'libnetfilter_queue' 'libnet' 'libpcap' 'libyaml' 'python2-magic' 'file' 'luajit' 'geoip' 'libnetfilter_log')
optdepends=('snort: suricata can use rulesets provided by snort')
options=('!emptydirs')
backup=('etc/suricata/suricata.yaml')
source=(http://openinfosecfoundation.org/download/$pkgname-$pkgver.tar.gz)
sha512sums=('98a10299804dbb605eef5b42167e2fd73a95c7999e35b874c6716211d6e42abdb5aca4883f3d98f25e445f0c4674a2d0f105b3f7670b8dea381c29f211dea984')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
  find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
}

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
  --localstatedir=/var \
  HAVE_PYTHON_CONFIG=/usr/bin/python2 \
  --enable-gccprotect \
  --enable-nfqueue \
  --enable-nflog \
  --enable-luajit \
  --enable-geoip \
  --disable-coccinelle \
  --enable-old-barnyard2 \
  --with-pic
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/var/log/$pkgname
  install -Dm644 $pkgname.yaml "${pkgdir}/etc/$pkgname/$pkgname.yaml"
  install -Dm644 classification.config "${pkgdir}/etc/$pkgname/classification.config"
  install -Dm644 reference.config "${pkgdir}/etc/$pkgname/reference.config"
  install -dm755 "${pkgdir}/etc/$pkgname/rules"
  install -Dm644 rules/*.rules "${pkgdir}/etc/$pkgname/rules/"
}
