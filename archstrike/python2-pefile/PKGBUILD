# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-pefile
pkgver=2016.3.28
pkgrel=4
pkgdesc="This is a Python library for PE file support and investigation."
arch=('any')
url='https://github.com/erocarrera/pefile'
license=('MIT')
depends=('python2-future')
makedepends=('python2-setuptools')
source=("https://github.com/erocarrera/pefile/files/192316/pefile-$pkgver.tar.gz"
        "http://research.pandasecurity.com/blogs/images/userdb.txt" )
sha512sums=('f27918634507fd839b3bc71677b57676f1b2e46c43970dba158b1b4cb4202344de41fef53fffd294dd70f89b1a52aec0124e835a410ff7f0bec9b11ece425201'
            'e23030b87c029911bb1fed9dfd245b645ffcb70781c3a0aa8f1dd0ecc5e3f02e36261ac69d1a25a6eee4622954217b4830a87feb8446483bc394a579d7bbb0ad')

prepare() {
  cd "pefile-${pkgver}"
  sed -i 's|python$|python2|' pefile.py
}

build() {
  cd "pefile-${pkgver}"
  python2 setup.py build
}

package() {
  cd "pefile-${pkgver}"
  python2 setup.py install -O1 --skip-build --root="${pkgdir}" --prefix="/usr"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 ${srcdir}/userdb.txt ${pkgdir}/usr/share/pefile/UserDB.TXT
}
