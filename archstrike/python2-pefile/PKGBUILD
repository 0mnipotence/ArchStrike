# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

pkgname=python2-pefile
pkgver=2018.8.8
pkgrel=1
pkgdesc="A Python library for PE file support and investigation."
arch=('any')
url='https://github.com/erocarrera/pefile'
license=('MIT')
depends=('python2-future')
makedepends=('python2-setuptools')
source=("https://github.com/erocarrera/pefile/releases/download/v${pkgver}/pefile-${pkgver}.tar.gz"
        "http://research.pandasecurity.com/blogs/images/userdb.txt" )
sha512sums=('44e8ed40f03e795badcc17cc6567165b3430aa80034f1b02b35fc15c94ce6b544ac195dedcea5858664c169fc4241e999df09a36f1a8a6635ce536af40284d29'
            'e23030b87c029911bb1fed9dfd245b645ffcb70781c3a0aa8f1dd0ecc5e3f02e36261ac69d1a25a6eee4622954217b4830a87feb8446483bc394a579d7bbb0ad')

prepare() {
  cd "pefile-${pkgver}"
  sed -i 's|python$|python2|' pefile.py
}

build() {
  cd "pefile-${pkgver}"
  python2 setup.py build
}

package() {
  cd "pefile-${pkgver}"
  python2 setup.py install -O1 --skip-build --root="${pkgdir}" --prefix="/usr"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 ${srcdir}/userdb.txt ${pkgdir}/usr/share/pefile/UserDB.TXT
}
