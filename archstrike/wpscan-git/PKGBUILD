# Maintainer: ArchStrike <team@archstrike.org>

buildarch=212

_pkgname=wpscan
pkgname=$_pkgname-git
pkgver=20151026.r1990
pkgrel=1
epoch=1
groups=('archstrike' 'archstrike-webapps' 'archstrike-scanners')
pkgdesc='A vulnerability scanner which checks the security of WordPress installations using a black box approach'
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url='http://wpscan.org'
license=('custom')
depends=('ruby1.9' 'ruby1.9-bundler' 'java-environment' 'libxslt')
makedepends=('git' 'unzip')
options=('!strip')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("$pkgname::git+https://github.com/wpscanteam/wpscan")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s.r%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)"
}

package() {
  cd $pkgname

  install -dm755 "$pkgdir/usr/share/$_pkgname"
  install -dm755 "$pkgdir/usr/bin"
  cp -a --no-preserve=owner * "$pkgdir/usr/share/$_pkgname"
  cd "$pkgdir/usr/share/$_pkgname"
  bundle-1.9 config build.nokogiri --use-system-libraries
  bundle-1.9 install --path vendor/bundle
  unzip -o data.zip
  rm "$pkgdir/usr/share/$_pkgname/data.zip"
  rm "$pkgdir/usr/share/$_pkgname/data/LICENSE"
  for i in $(find "$pkgdir" -type d -iname ext); do
    rm -rf "$i"
  done
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cat > "$pkgdir/usr/bin/$_pkgname" <<EOF
#!/usr/bin/env bash
cd /usr/share/$_pkgname
ruby-1.9 ./$_pkgname.rb "\$@"
EOF
  sed -i "s/ROOT_DIR + '\/log.txt/'\/home\/' + ENV['USER'] + '\/$_pkgname.log/" "$pkgdir/usr/share/$_pkgname/lib/common/common_helper.rb"
  sed -i "s/ROOT_DIR + '\/cache/'\/home\/' + ENV['USER'] + '\/cache/" "$pkgdir/usr/share/$_pkgname/lib/common/common_helper.rb"
  chmod 0755 "$pkgdir/usr/bin/$_pkgname"
}
