# Maintainer: ArchStrike <team@archstrike.org>

buildarch=220

pkgname=python2-bbio
pkgver=1.1.0
pkgrel=1
pkgdesc="A module to control BeagleBone IO channels"
arch=('armv6h' 'armv7h' 'i686' 'x86_64' 'aarch64')
license=("MIT")
url="https://github.com/adafruit/adafruit-beaglebone-io-python"
depends=("python2")
makedepends=("python2-setuptools")
source=("https://github.com/adafruit/adafruit-beaglebone-io-python/archive/$pkgver.tar.gz")
sha512sums=('fd8451bb02372d6d018e38814b2f9c7a80af56c18b8a91f1ea011aa301c55d0af10086f2d6578f193b52c4745506c6c38ae560034d7a79ec0d0315f5227de341')

prepare() {
  cd "${srcdir}/adafruit-beaglebone-io-python-${pkgver}"
  sed -i "s/^\(CFLAGS = \['-Wall',\) '-Werror',/\1/1" setup.py
}

build() {
  cd "${srcdir}/adafruit-beaglebone-io-python-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/adafruit-beaglebone-io-python-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|'
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/README.rst"
}
