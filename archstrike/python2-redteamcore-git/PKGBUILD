# Maintainer: ArchStrike <team@archstrike.org>

buildarch=1

_pkgname="redteamcore"
pkgname=python2-${_pkgname}-git
pkgver=0.0.2.r0.gb7e3de2
pkgrel=1
pkgdesc="Core Python modules for use in tooling"
arch=('any')
url="https://github.com/fedoraredteam/${_pkgname}"
license=('GPL3')
makedepends=("python2-setuptools" "git")
depends=("python2" "python2-requests" "python2-dateutil")
source=("${_pkgname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  grep -ZRil 'python' . | xargs -0 sed -i '1 s|#!.*/usr/bin/python3?|#!/usr/bin/python2|;s|#!.*/usr/bin/env python3?$|#!/usr/bin/env python2|'
}

build() {
  cd "${srcdir}/${_pkgname}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:se ts=2 sw=2 et:
